BACKUP ~weidu_external/backup/eefixpack~ // location to store files for uninstall
SUPPORT ~https://www.gibberlings3.net/forums/forum/232-ee-fixpack/~ // URL displayed if install fails
AUTO_EVAL_STRINGS
//MODDER fun_args fail missing_eval none setup_tra none

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET cd_always_eefixpack THEN BEGIN // just do this once per install
  
    OUTER_SET cd_always_eefixpack = 1

    ACTION_FOR_EACH var IN game_includes_sod game_is_bgee game_is_bg2ee game_is_iwdee game_is_pstee BEGIN
      OUTER_SET ~%var%~ = 0
    END  
    
    ACTION_IF GAME_IS bgee BEGIN 
      ACTION_IF GAME_INCLUDES sod BEGIN 
        ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @3 END // DLC Merger check
        OUTER_SET game_includes_sod = 1  
        PRINT @7      
      END ELSE BEGIN
        PRINT @6
      END
      OUTER_SPRINT game ~bgee~
    END ELSE  
    ACTION_IF GAME_IS bg2ee BEGIN OUTER_SPRINT game ~bg2ee~ PRINT @8 END ELSE
    ACTION_IF GAME_IS iwdee BEGIN OUTER_SPRINT game ~iwdee~ PRINT @9 END ELSE
    ACTION_IF GAME_IS pstee BEGIN OUTER_SPRINT game ~pstee~ PRINT @10 END
    
    OUTER_SET ~game_is_%game%~ = 1
    LOAD_TRA ~eefixpack/languages/%LANGUAGE%/%game%.tra~
    INCLUDE ~eefixpack/files/lib/cd_spell_ids.tpa~ // sets variables such as WIZARD_MAGIC_MISSILE based on spell.ids
    INCLUDE ~eefixpack/files/lib/gt_race_ids.tph~ // sets variables such as RACE_HUMAN based on race.ids
    INCLUDE ~eefixpack/files/lib/cd_functions.tpa~
    INCLUDE ~eefixpack/files/lib/gt_functions.tph~
    INCLUDE ~eefixpack/files/lib/dw_functions.tph~

  END

END

VERSION ~Alpha 2~

README ~eefixpack/languages/%LANGUAGE%/readme-eefixpack.html~ ~eefixpack/readme-eefixpack.html~

AUTO_TRA ~eefixpack/languages/%LANGUAGE%/%s~

ASK_EVERY_COMPONENT // despite being hidden, WeiDU now thinks there are enough components for the install all prompt

LANGUAGE
  ~English~
  ~en_us~
  ~eefixpack/languages/en_us/weidu.tra~
LANGUAGE
  ~Russian~
  ~ru_ru~
  ~eefixpack/languages/ru_ru/weidu.tra~
LANGUAGE
  ~Simplified Chinese~
  ~zh_CN~
  ~eefixpack/languages/zh_CN/weidu.tra~
LANGUAGE
  ~French~
  ~fr_FR~
  ~eefixpack/languages/fr_FR/weidu.tra~
  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Core Fixes                                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @0 DESIGNATED 0
REQUIRE_PREDICATE (((GAME_IS ~bgee~)  AND (FILE_EXISTS ~eefixpack/files/tph/bgee.tph~)) OR
                   ((GAME_IS ~bg2ee~) AND (FILE_EXISTS ~eefixpack/files/tph/bg2ee.tph~)) OR
                   ((GAME_IS ~iwdee~) AND (FILE_EXISTS ~eefixpack/files/tph/iwdee.tph~)) OR
                   ((GAME_IS ~pstee~) AND (FILE_EXISTS ~eefixpack/files/tph/pstee.tph~))) @4
LABEL ~cd_eefp_core_fixes~

WITH_TRA ~eefixpack/languages/%LANGUAGE%/%game%.tra~ BEGIN 
  INCLUDE ~eefixpack/files/tph/%game%.tph~
END  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Beta Core Fixes                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1 DESIGNATED 1  
REQUIRE_PREDICATE (((GAME_IS ~bgee~)  AND (FILE_EXISTS ~eefixpack/files/tph/bgee_beta.tph~)) OR
                   ((GAME_IS ~bg2ee~) AND (FILE_EXISTS ~eefixpack/files/tph/bg2ee_beta.tph~)) OR
                   ((GAME_IS ~iwdee~) AND (FILE_EXISTS ~eefixpack/files/tph/iwdee_beta.tph~)) OR
                   ((GAME_IS ~pstee~) AND (FILE_EXISTS ~eefixpack/files/tph/pstee_beta.tph~))) @4
LABEL ~cd_eefp_beta_core_fixes~

// none of these files have been created yet, as the initial releases will be betass
WITH_TRA ~eefixpack/languages/%LANGUAGE%/%game%.tra~ BEGIN 
  INCLUDE ~eefixpack/files/tph/%game%_beta.tph~
END  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Game Text Update                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2 DESIGNATED 2
REQUIRE_PREDICATE (((GAME_IS ~bgee~)  AND ((FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_bgee.tph~) OR (FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_bgee_sod.tph~))) OR
                   ((GAME_IS ~bg2ee~) AND (FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_bg2ee.tph~)) OR
                   ((GAME_IS ~iwdee~) AND (FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_iwdee.tph~)) OR
                   ((GAME_IS ~pstee~) AND (FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_pstee.tph~)) OR
                   ((!GAME_IS ~pstee~) AND (FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/infinity.tph~))) @5
LABEL ~cd_eefp_game_text_update~

ACTION_IF !game_is_pstee AND FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/infinity.tph~ BEGIN // for updating l_%lang%.lua (UI and help strings across the non-pstee games)
  INCLUDE ~eefixpack/files/lib/cd_infinity_processing.tph~
END

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/bd_tool_%game%.tph~ BEGIN // updates from BD translation tool
  INCLUDE ~eefixpack/languages/%LANGUAGE%/bd_tool_%game%.tph~
END

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_%game%.tph~ BEGIN // mainline tlk updates
  INCLUDE ~eefixpack/languages/%LANGUAGE%/gtu_%game%.tph~
END

ACTION_IF game_includes_sod BEGIN
  ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/bd_tool_%game%_sod.tph~ BEGIN // updates from BD translation tool, SoD edition
    INCLUDE ~eefixpack/languages/%LANGUAGE%/bd_tool_%game%_sod.tph~
  END

  ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/gtu_%game%_sod.tph~ BEGIN // mainline tlk updates, SoD edition
    INCLUDE ~eefixpack/languages/%LANGUAGE%/gtu_%game%_sod.tph~
  END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// infinity strings                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Automatic TPH generation for Infinity Strings~ DESIGNATED 997
REQUIRE_PREDICATE ((!GAME_IS ~pstee~) AND
                   (!MOD_IS_INSTALLED ~eefixpack/setup-eefixpack.tp2~ ~0~) AND
                   (!MOD_IS_INSTALLED ~eefixpack/setup-eefixpack.tp2~ ~1~) AND
                   (!MOD_IS_INSTALLED ~eefixpack/setup-eefixpack.tp2~ ~2~)) ~Do not run with EEFP installed in case you're using the game tlk~
NO_LOG_RECORD
DEPRECATED ~For internal use only~
LABEL ~cd_eefp_internal_infinity_strings~

// cam's very sketchy tool for generating Infinity Strings (aka the l_en_us.lua files) updates based
// off of a CSV downloaded from the Beamdog translation tool
// more info in the cd_infinity_strings.tph library

INCLUDE ~eefixpack/files/lib/cd_infinity_strings.tph~

LAF cd_infinity_comparator STR_VAR lang = ~pl_pl~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// tlk comparator                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Automatic TPH generation from tra files~ DESIGNATED 998
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~eefixpack/setup-eefixpack.tp2~ ~0~) AND
                   (!MOD_IS_INSTALLED ~eefixpack/setup-eefixpack.tp2~ ~1~) AND
                   (!MOD_IS_INSTALLED ~eefixpack/setup-eefixpack.tp2~ ~2~)) ~Do not run with EEFP installed in case you're using the game tlk~
NO_LOG_RECORD
DEPRECATED ~For internal use only~
LABEL ~cd_eefp_internal_tlk_comparator~

// cam's very sketchy tool for generating GTUs by comparing 2.6 tlk files with the tlk files downlaoded from the BD translation tool
// more info in the cd_tlk.tph library

INCLUDE ~eefixpack/files/lib/cd_tlk.tph~

OUTER_SPRINT lang pl
ACTION_FOR_EACH game IN sod iwdee bg2ee /* pstee */ BEGIN
  LAF cd_tlk_comparator INT_VAR f_tlk_exists = 1 STR_VAR      // dialogf.tlk exists for Polish
    m_tlk_26       = ~eefixpack/tlk/%game%_%lang%_m_26.tlk~   // male/sole tlk that ships with 2.6 (with path)
    m_tlk_tool     = ~eefixpack/tlk/%game%_%lang%_m_tool.tlk~ // male/sole tlk from the Beamdog tool (with path)
    f_tlk_26       = ~eefixpack/tlk/%game%_%lang%_f_26.tlk~   // female tlk that ships with 2.6 (with path)
    f_tlk_tool     = ~eefixpack/tlk/%game%_%lang%_f_tool.tlk~ // female tlk from the Beamdog tool (with path)
    output         = ~bd_tool_%game%.tra~
  END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// tph-from-tra generation                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Automatic TPH generation from tra files~ DESIGNATED 999
NO_LOG_RECORD
DEPRECATED ~For internal use only~
LABEL ~cd_eefp_internal_tph_from_tra~

// generating the STRING_SET tph from the tra files is neat, but can be a bit slow.
// run this function to generate these files automatically before shipping.

ACTION_FOR_EACH lang IN cs_cz de_de en_us es_es fr_fr hu_hu it_it ja_jp ko_kr nb_no pl_pl pt_br ru_ru tr_tr uk_ua zh_cn BEGIN

  ACTION_FOR_EACH prefix IN gtu fixes bd_tool BEGIN

    ACTION_FOR_EACH tra_file IN bgee bgee_sod bg2ee iwdee pstee BEGIN 
  
      LAUNCH_ACTION_FUNCTION cd_string_set_from_tra
        INT_VAR cd_mode = 0 
        STR_VAR input_tra  = EVAL ~eefixpack/languages/%lang%/%prefix%_%tra_file%.tra~ // function includes file_exists check, so we don't bother with it here
                output_tph = EVAL ~eefixpack/languages/%lang%/%prefix%_%tra_file%.tph~ // tph being built
      END
      
    END
  
  END
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Temporary test space                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Temporary test space~ DESIGNATED 1000
DEPRECATED ~For internal use only~
LABEL ~cd_eefp_internal_test_space~
