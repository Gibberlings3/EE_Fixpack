// fixes here will run on both bg and bg2

// luke
// **Cure Wounds**
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cure_wounds.tph"
  //LAUNCH_ACTION_FUNCTION ~CURE_WOUNDS~ END
END

// luke
// **Cause Wounds**
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cause_wounds.tph"
  //LAUNCH_ACTION_FUNCTION ~CAUSE_WOUNDS~ END
END

// luke
// **Lesser / Greater Restoration**
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/restoration.tph"
  //LAUNCH_ACTION_FUNCTION ~RESTORATION~ END
END

// luke
// Mimic Glue should not set the GREASE stat
WITH_SCOPE BEGIN
  COPY_EXISTING ~%MIMIC_GLUE%.spl~ ~override~ // Mimic Glue spin914
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 158 "opcode" = 215 "parameter2" = 1 STR_VAR "resource" = "GREASED" END // Grease overlay => Play visual effect, Mode: Over target (attached)
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Lesser Fire Spirit � Suppress misleading "One of the spell has failed" message
// 1) Set subspell's range to a very high value (max signed value)
// 2) Set subspell's projectile to either "RANGE05", "RANGE07" or "RANGE10". The second one is the most appropriate IMHO (given ELEMENTAL_FIRE_SMALL selection circle)...
WITH_SCOPE BEGIN
  COPY_EXISTING "bdsha12a.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("MISSILE" "OneTarget_Range_07") END
  BUT_ONLY_IF_IT_CHANGES
END// grease should work like web and check for grease once/round
COPY_EXISTING ~grease.pro~ ~override/grease1.pro~
  WRITE_SHORT 0x210 100 // one round
  WRITE_BYTE  0x216   4 // repititions (3 rds + 1 rd/level)
ADD_PROJECTILE ~override/grease1.pro~

OUTER_FOR (index = 2 ; index < 21 ; ++index) BEGIN

  COPY_EXISTING ~grease1.pro~ ~override/grease%index%.pro~
    WRITE_BYTE  0x216 (index + 3) // repititions (3 rds + 1 rd/level)
  ADD_PROJECTILE ~override/grease%index%.pro~

END

// luke
// Grease � Remove duplicate op321 effect
// grease should work like web and check for grease once/round
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_GREASE%.spl~ ~override~ // Grease spwi101
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "321" "resource" = "%DEST_RES%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 321 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ STR_VAR ~match_resource~ = ~%DEST_RES%~ END
      END
      READ_SHORT 0x68 abil_num
      FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = (index - 1) match_duration = (18 + (6 * index)) duration = 6 END
        LPF ALTER_HEADER INT_VAR header = (index - 1) projectile = EVAL ~grease%index%~ END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Magic Missile � better feedback for the player when you cast it at someone protected by WIZARD_SHIELD
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_MAGIC_MISSILE%.spl~   ~override~ // spwi112
                ~%TRAP_MAGIC_MISSILE%.spl~     ~override~ // spwi003
                ~%MAGIC_MISSILE_SURE_HIT%.spl~ ~override~ // spin685
                ~%BEHOLDER_MAGIC_MISSILE%.spl~ ~override~ // spin962
                ~balth06.spl~ ~override~
    LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ INT_VAR "insert_point" = 0 "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("SPLSTATE" "WIZARD_SHIELD") "parameter2" = 110 "resist_dispel" = 2 "power" = 1 STR_VAR "resource" = "%DEST_RES%" END
  BUT_ONLY_IF_IT_CHANGES IF_EXISTS // balth06 is bg2ee only
END

// luke
// Larloch's Minor Drain
// 1) subspell's effects should have `power=0`
// 2) subspell should not play `casting graphics` and `casting sound`
// 3) subspell should not use the `exclusion flags` field
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_LARLOCH_MINOR_DRAIN%a.spl~ ~override~ // spwi119a
                ~%INNATE_LARLOCHS_MINOR_DRAIN%a.spl~ ~override~ // spin104a
    // Header
    WRITE_ASCII 0x10 "" #8
    WRITE_LONG 0x1E 0
    WRITE_SHORT 0x22 0
    // Feature blocks
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "power" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Detect Evil � should not interact with spell protections
// 1) there's no need for the subspell
//// 1a) its effects have incorrect power levels
//// 1b) more importantly, as of v2.6, you should no longer be able to bypass other's Magic Resistance by targeting yourself with an area-effect ability => see `https://gibberlings3.github.io/iesdp/file_formats/ie_formats/eff_v2.htm#effv2_Body_0x5C` (BIT3) for further details...
// 2) creatures flagged as STATE_NONDETECTION (see f.i. "Cloak of Non-Detection: Whispers of Silence") should not be immune to it
//// I mean, STATE_NONDETECTION protects against op47 and op116; this spell uses op115, so... Am I missing something?
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_DETECT_EVIL%.spl" "override" // spwi202
                "%CLERIC_DETECT_EVIL%.spl" "override" // sppr104
                "%PALADIN_DETECT_EVIL%.spl" "override" // spcl212
                "spin120.spl" "override" // old ajantis detect evil
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 146 ~opcode~ = 115 ~parameter1~ = 0 ~parameter2~ = 0 ~timing~ = 1 ~duration~ = 0 STR_VAR ~resource~ = ~~ END // Cast spell => Detect alignment
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 318 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Stinking Cloud
// 1) op39 can automatically display a portrait icon, there's no need to apply a separate op142 effect
// 2) remove the "Bypass Mirror Image" flag (it's only relevant for opcode #12 and opcode #25)
// 3) provide better feedback for the player when attacking Golems or Undead
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_STINKING_CLOUD%.spl" "override" // spwi213
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
        GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
        PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
          WRITE_LONG ("%fx_off%" + 0x24) (THIS BAND BNOT BIT24)
        END
      END
    END
    PATCH_IF game_is_bg2ee BEGIN
      LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ INT_VAR "insert_point" = 0 "opcode" = 324 "target" = 2 "parameter2" = 55 "resist_dispel" = 2 "power" = 2 STR_VAR "resource" = "%DEST_RES%" END
    END
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 142 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 39 ~special~ = 126 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Stinking Cloud
// 1) op39 can automatically display a portrait icon, there's no need to apply a separate op142 effect
// 2) remove the "Bypass Mirror Image" flag (it's only relevant for opcode #12 and opcode #25)
// 3) provide better feedback for the player when attacking Golems or Undead
WITH_SCOPE BEGIN
  COPY_EXISTING "%TRAP_STINKING_CLOUD%.spl" "override" // spwi004
                "%MEPHIT_STINKING_CLOUD%.spl" "override" // spin940
                "spwm187.spl" "override" // Wild surge
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
        GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
        PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
          WRITE_LONG ("%fx_off%" + 0x24) (THIS BAND BNOT BIT24)
        END
      END
    END
    LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ INT_VAR ~insert_point~ = 0 ~target~ = 2 ~opcode~ = 324 ~parameter2~ = 55 STR_VAR ~resource~ = ~%DEST_RES%~ END // Immunity to resource and message
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 142 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 39 ~special~ = 126 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Lightning Bolt � remove duplicate op318 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_LIGHTNING_BOLT%.spl" "override" // spwi308
                "%TALOS_LIGHTNING_BOLT%.spl" "override" // spcl722
                "%TRAP_LIGHTNING_BOLT%.spl" "override" // spwi002
                "spdr301.spl" "override" // Avenger
                "%CELESTIAL_BOLT%.spl" "override" // spin714
                "%GORION_LIGHTNING%.spl" "override" // spwi997
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "318" "resource" = "%DEST_RES%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 318 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ STR_VAR ~match_resource~ = ~%DEST_RES%~ END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Protection From Normal Missiles � remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_PROTECTION_FROM_NORMAL_MISSILES%.spl" "override" // spwi311
                "spra303.spl" "override" // Stalker
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_MISSILES")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_MISSILES") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

COPY_EXISTING ~spra303.spl~ ~override~ // stalker verison of pfnm lacks opening sound
  LPF CLONE_EFFECT INT_VAR match_opcode = 174 match_timing = 4 timing = 1 duration = 0 STR_VAR insert = after resource = eff_m02 END  

// luke
// Vampiric Touch
// 1) subspell's effects should have `power=0`
// 2) subspell should not play `casting graphics` and `casting sound`
// 3) subspell's type should match its parent (to maintain casting level!)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_VAMPIRIC_TOUCH%a.spl~ ~override~ // spwi314a
    // Header
    WRITE_ASCII 0x10 "" #8
    WRITE_SHORT 0x1C 1 // wizard
    WRITE_SHORT 0x22 0
    // Feature blocks
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "power" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Vampiric Touch (innate)
// 1) subspell's effects should have `power=0`
// 2) subspell should not play `casting graphics` and `casting sound`
WITH_SCOPE BEGIN
  COPY_EXISTING ~%INNATE_VAMPIRIC_TOUCH%a.spl~ ~override~ // spin106a
                ~%TANARI_VAMPIRIC_TOUCH%a.spl~ ~override~ // spin997a
    // Header
    WRITE_ASCII 0x10 "" #8
    WRITE_SHORT 0x22 0
    // Feature blocks
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "power" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Minor Spell Deflection � remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_MINOR_SPELL_DEFLECTION%.spl" "override" // spwi318
                "spra302.spl" "override" // Stalker
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Improved Invisibility
// Applying "Improved" Invisibility will cause the effect to be duplicated, with the duplicate using "Normal" invisibility.
//// There's no reason to apply both Normal and Improved invisibility in the same spell/item (unless you want them to have different probabilities/hd limits, but this is not the case).
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_IMPROVED_INVISIBILITY%.spl" "override" // spwi405
                "ohbwi405.spl" "override"
                "spdr401.spl" "override" // Avenger
                "%PSIONIC_SUPERIOR_INVISIBILITY%.spl" "override" // spin544
                "%NON_DETECTION_SELF%.spl" "override" // spin698
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 20 ~match_parameter2~ = 0 END
  BUT_ONLY_IF_IT_CHANGES IF_EXISTS // ohbwi405 is bg2 asset
END

// luke
// Emotion, Hopelessness
// Op39 can automatically display a portrait icon, there's no need to apply a separate op142 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_EMOTION_HOPELESSNESS%.spl" "override" // spwi411
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 142 ~match_parameter2~ = 44 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 39 ~special~ = 44 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Shadow Door
// Applying "Improved" Invisibility will cause the effect to be duplicated, with the duplicate using "Normal" invisibility.
//// There's no reason to apply both Normal and Improved invisibility in the same spell/item (unless you want them to have different probabilities/hd limits, but this is not the case).
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SHADOW_DOOR%.spl" "override" // spwi505
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 20 ~match_parameter2~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Feeblemind � make sure it is truly permanent (until dispelled or character death)
//// Unlike other effects that alter a creature's stat, this one does leave behind a removable effect when applied with `timing=1`
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_FEEBLEMIND%.spl" "override" // spwi509
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 76 ~timing~ = 1 ~duration~ = 0 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 142 ~timing~ = 1 ~duration~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Protection From Normal Weapons � remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_PROTECTION_FROM_NORMAL_WEAPONS%.spl" "override" // spwi511
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_WEAPONS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_WEAPONS") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Minor Spell Turning � remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_MINOR_SPELL_TURNING%.spl" "override" // spwi522
                "%BEHOLDER_SPELL_TURNING%.spl" "override" // spin920
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Spell Deflection � remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPELL_DEFLECTION%.spl" "override" // spwi618
                "%SPELL_DEFLECTION_NO_VISUAL%.spl" "override" // spin710
                "spwi802.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Spell Turning � remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPELL_TURNING%.spl" "override" // spwi701
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Mass Invisibility
// 1) Applying "Improved" Invisibility will cause the effect to be duplicated, with the duplicate using "Normal" invisibility.
//// There's no reason to apply both Normal and Improved invisibility in the same spell/item (unless you want them to have different probabilities/hd limits, but this is not the case).
// 2) "Improved" Invisibility naturally provides a bonus of 4 to all saves, there's no need for separate op33-37 effects.
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_MASS_INVISIBILITY%.spl~ ~override~ // spwi721
    PATCH_WITH_SCOPE BEGIN
      PATCH_FOR_EACH "match_opcode" IN "33" "34" "35" "36" "37" BEGIN
        LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "match_opcode" END
      END
    END
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "match_opcode" = 20 "match_parameter2" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Spell Trap � remove duplicate op328 / op282 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPELL_TRAP%.spl" "override" // spwi902
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "282" "parameter2" = "8" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 282 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = 8 END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Command**
// - Creatures with `37` or more Hit Dice (or experience levels) should not be immune to it
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_COMMAND%.spl" "override" // sppr102
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_dicenumber~ = 36 ~dicenumber~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

/*// luke
// **Sanctuary**
// - Ending sound effect should play after `60` seconds (not immediately)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_SANCTUARY%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 174 ~duration~ = 60 END
  BUT_ONLY_IF_IT_CHANGES
END*/

// luke
// **Armor of Faith**
// - remove duplicate `op328` effect(s)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_ARMOR_OF_FAITH%.spl" "override" // sppr111
    PATCH_WITH_SCOPE BEGIN
      SET "parameter2" = IDS_OF_SYMBOL ("SPLSTATE" "ARMOR_OF_FAITH")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "ARMOR_OF_FAITH") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Slow Poison**
// - Make sure it truly slows poison (so as to match `IWDEE` and spell description/name)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_SLOW_POISON%.spl~ ~override~ // sppr212
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 11 "opcode" = 329 "parameter1" = 10 "parameter2" = 0 END // Cure poison => Slow poison
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Summon Insects, Insect Plague**
// - remove duplicate `op233` effect(s)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_SUMMON_INSECTS%.spl" "override" // sppr319
                "%CLERIC_INSECT_PLAGUE%.spl" "override" // sppr517
    PATCH_WITH_SCOPE BEGIN
      SET "parameter2" = IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_globals~ = 0 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Poison**
// - Make sure `duration=60 // 1 turn` (instead of `62`)
// - Use multiple `op25` effects instead of `spwi411[a-e].eff` (so as to make it work with `CLERIC_SLOW_POISON`)
//// - As far as I know, `op329` cannot slow poison if `op25` is applied via `op177`...
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_POISON%.spl" "override" // sppr411
    PATCH_WITH_SCOPE BEGIN
      LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 177 END // Use EFF file
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        FOR ("i" = 1 ; "%i%" <= "%hdr%" + 2 ; "i" += 1) BEGIN
          LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ INT_VAR ~header~ = ~%hdr%~ ~match_opcode~ = 12 ~opcode~ = 25 ~parameter1~ = 6 ~parameter2~ = 3 ~timing~ = 0 ~duration~ = 60 ~dicenumber~ = 0 ~dicesize~ = 0 ~special~ = 6 STR_VAR ~insert~ = ~below~ ~resource~ = ~~ END
        END
      END
    END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_duration~ = 62 ~duration~ = 60 END
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 142 "match_parameter2" = 6 END // op25 naturally provides the "Poisoned" portrait icon
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Chaotic Commands**
// - remove duplicate `op328` effect(s)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_CHAOTIC_COMMANDS%.spl" "override" // sppr508
    PATCH_WITH_SCOPE BEGIN
      SET "parameter2" = IDS_OF_SYMBOL ("SPLSTATE" "CHAOTIC_COMMANDS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_globals~ = 0 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "CHAOTIC_COMMANDS") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Blade Barrier**
// - Make sure `duration=60 // 1 turn` (instead of `62`)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_BLADE_BARRIER%.spl" "override" // sppr603
                "%NPC_BLADE_BARRIER%.spl" "override" // sppr698
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_timing~ = 0 ~duration~ = 60 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_timing~ = 4 ~duration~ = 60 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Dolorous Decay**
// - Make sure all poison-related effects have a duration of `50` (instead of `56`)
// - Make sure the "Decaying" portrait icon does not bypass Magic Resistance
// - Make sure the delayed `op174` effect (the one related with Poison) offers a Save vs. Poison @ -2
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_DOLOROUS_DECAY%.spl" "override" // sppr610
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 142 "match_parameter2" = 101 ~resist_dispel~ = BIT0 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_duration~ = 56 ~duration~ = 50 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 174 "match_timing" = 4 "match_duration" = 50 ~savingthrow~ = BIT2 ~savebonus~ = ~-2~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Physical Mirror**
// - remove duplicate `op328` / `op233` effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_PHYSICAL_MIRROR%.spl" "override" // sppr613
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_WEAPONS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "CLERIC_PHYSICAL_MIRROR")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_WEAPONS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_PHYSICAL_MIRROR") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Shield of the Archons**
// - remove duplicate `op328` / `op233` effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_SHIELD_OF_THE_ARCHONS%.spl" "override" // sppr701
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "CLERIC_SHIELD_OF_THE_ARCHONS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_SHIELD_OF_THE_ARCHONS") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Holy / Unholy Word**
// - recode from scratch so as to use `op324` magic instead of messy `op177` effects
// - make sure it causes arcane spell failure (not just divine spell failure)
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/holy_unholy_word.tph~ // sppr710.spl, sppr715.spl
  LAF ~CLERIC_HOLY_UNHOLY_WORD~ END
END

// luke
// **Creeping Doom** (Black Dragon)
// - use same projectile as `"%CLERIC_CREEPING_DOOM%"`
WITH_SCOPE BEGIN
  COPY_EXISTING "%BLACK_DRAGON_INSECT%.spl" "override" // spin689
    LPF "ALTER_SPELL_HEADER" INT_VAR "projectile" = IDS_OF_SYMBOL ("MISSILE" "Chain_Insect") END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Absorb Health** (Blackguard)
// - should not bypass Magic Resistance
// - subspell should scale up to level `40` (`BG2` level cap)
// - subspell's Primary/Secondary type should match parent `SPL` file
// - subspell should not play a casting sound
WITH_SCOPE BEGIN
  WITH_SCOPE BEGIN
    COPY_EXISTING "%BLACKGUARD_ABSORB_HEALTH%.spl" "override" // spcl102
      LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 146 "resist_dispel" = BIT0 END
    BUT_ONLY_IF_IT_CHANGES
  END
  WITH_SCOPE BEGIN
    COPY_EXISTING "spdn01a.spl" "override"
      WRITE_ASCII 0x10 "" #8 // Casting sound
      WRITE_BYTE 0x25 7 // Primary type: NECROMANCER
      WRITE_BYTE 0x27 10 // Secondary type: OFFENSIVEDAMAGE
      /* Feature blocks */
      LPF "CD_EXTEND-O-MATIC" INT_VAR "level_cap" = 50 END
      PATCH_WITH_SCOPE BEGIN
        GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
        PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
          GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
          PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
            PATCH_MATCH (SHORT_AT "%fx_off%") WITH
              "12" BEGIN
                WRITE_LONG ("%fx_off%" + 0x4) (SHORT_AT ("%ab_off%" + 0x10) * 2) // 2 points of damage per level
              END
              DEFAULT
            END
          END
        END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END

// kai using dispel magic icon
COPY_EXISTING ~spcl144.spl~ ~override~
  WRITE_ASCII 0x3a ~spcl144~ #8
  BUT_ONLY

// extend paladin lay on hands to l50
COPY_EXISTING ~%PALADIN_LAY_ON_HANDS%.spl~ ~override~ // spcl211, lay on hands
  LPF CD_EXTEND-O-MATIC INT_VAR min_dur = 10000 level_cap = 50 RET abil_delta END // fixed duration, just need the new headers
  // heals 2 hp/level
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = (abil_num - abil_delta) ; index < abil_num ; ++index) BEGIN // loop through new abilities only
    READ_SHORT  (abil_off + 0x10 + (index * 0x28)) level       // just read the level
    READ_SHORT  (abil_off + 0x1e + (index * 0x28)) abil_fx_num
    READ_SHORT  (abil_off + 0x20 + (index * 0x28)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off +        ((index2 + abil_fx_idx) * 0x30)) op
      PATCH_IF op = 17 BEGIN // cur hp bonus
        WRITE_LONG (fx_off + 0x04 + ((index2 + abil_fx_idx) * 0x30)) (level * 2)
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~spcl721.spl~ ~override~ // don't keep repeating animation
  LPF ALTER_EFFECT INT_VAR match_opcode = 215 duration = 2 END
  BUT_ONLY   

// make protection from evil 10' radius actually, uh, have a 10' radius
COPY_EXISTING ~spargrpa.pro~ ~override/spprot10.pro~ // new 10' projectile
  WRITE_SHORT 0x204 171
  WRITE_SHORT 0x206 171
ADD_PROJECTILE ~override/spprot10.pro~
COPY_EXISTING ~spcl233.spl~ ~override~ // protection from evil 10'
              ~sppr408.spl~ ~override~ // protection from evil 10'
  LPF ALTER_HEADER INT_VAR projectile = spprot10 END

COPY_EXISTING ~spin698.spl~ ~override~ // non_detection_self aka improved invisibility [bg2]
  LPF ALTER_EFFECT INT_VAR header = 0 match_opcode = 69 duration = 60 END // non-detection outlasts invis by five rounds

// tbd, cam
// update psychic drain to use generic mind flayer spell animation, spflayer
COPY_EXISTING ~spin804.spl~  ~override~ // psychic drain - plays spmindat
  LPF ALTER_EFFECT INT_VAR match_opcode = 215 STR_VAR match_resource = spmindat resource = spflayer END
  BUT_ONLY

//tbd, cam
// should be setting ex str, not incrementing
COPY_EXISTING ~spin826.spl~ ~override~ // mazzy strength buff
  LPF ALTER_EFFECT INT_VAR match_opcode = 97 parameter2 = 1 END // wrong on first header only
  BUT_ONLY

// Psychic struggle for control of wizard elemental summons breaks when elemental is issued order (also in bg2ee.tph)
COPY_EXISTING ~spin906.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR match_opcode=169 match_parameter2=55 opcode=365 parameter1=0 parameter2=1 timing=1 duration=0 STR_VAR insert=first END
	LPF CLONE_EFFECT INT_VAR match_opcode=169 match_parameter2=55 opcode=321 parameter1=0 parameter2=0 timing=4 STR_VAR insert=first resource= EVAL ~%SOURCE_RES%~ END
  BUT_ONLY // Make elemental unselectable during psychic struggle

// tbd, cam
// delayed sound effect doesn't bypass mr though the rest of the spell does
COPY_EXISTING ~spin910.spl~ ~override~ // psionic domination
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 resist_dispel = 2 END
  BUT_ONLY

COPY_EXISTING ~spin956.spl~ ~override~ // hell hound breath [bg2]
  LPF CD_TRIM-O-MATIC INT_VAR level_cap = 1 END // has a bunch of headers that never get used and don't match

COPY_EXISTING ~sppr110.spl~ ~override~ //shillelagh
  LPF ALTER_EFFECT INT_VAR header = 19 match_duration = 142 duration = 144 END // duration is two seconds too short at level 20

COPY_EXISTING ~%CLERIC_CALL_LIGHTNING%.spl~ ~override~ // sppr302, call lightning - caps at 18
  LPF CD_LEVEL_SELECT-O-MATIC INT_VAR step_dur = 0 damage_rolls = 1 base_dmg_rolls = 2 save_for_half = 1 RET abil_delta END // damage is 2d8 + 1d8 per level of caster
  // duration is one round/4 levels in bg2, 1 round/level in iwdee--both are handled by projectile
  PATCH_IF abil_delta > 0 BEGIN // if we added abilities
    READ_LONG  0x64 abil_off
    READ_SHORT 0x68 abil_num
    FOR (index = (abil_num - abil_delta) ; index < abil_num ; ++index) BEGIN // loop through new abilities only
      READ_SHORT  (abil_off + 0x10 + (index * 0x28)) level       // just read the level
      // iwdee scales projectile 1/level so last projectile is level 11; bg2ee does 1/4 levels so cap is 44
      PATCH_IF level > (43 - (33 * game_is_iwdee)) BEGIN SET proj = 91 END ELSE BEGIN SET proj = (80 + (level / (4 - (3 * game_is_iwdee)))) END // 91 is last call lightning projectile
      WRITE_SHORT (abil_off + 0x26 + (index * 0x28)) proj // projectile
    END
  END
  BUT_ONLY

COPY_EXISTING ~%CLERIC_GLYPH_OF_WARDING%.spl~ ~override~ // sppr304, glyph of warding - capping at 18
  LPF CD_LEVEL_SELECT-O-MATIC INT_VAR step_dur = 0 damage_rolls = 1 END // damage is 1d4/level, save negates
  BUT_ONLY

COPY_EXISTING ~sppr508.spl~ ~override~ // chaotic commands [bg, bg2] - capping at 18, other bugs
  LPF CD_LEVEL_SELECT-O-MATIC INT_VAR step_dur = (10 * round) END // 1 turn/level
  PATCH_IF game_is_bg2ee BEGIN
    LPF DELETE_EFFECT INT_VAR header = 0 match_opcode = 267 match_parameter1 = 16233 END // blocks "gulp" string (bg2 only)
  END
  READ_SHORT 0x68 abil_num
  FOR (index = 1 ; index < abil_num ; ++index) BEGIN // start at 1 to skip first header which doesn't have dupe
    LPF DELETE_EFFECT INT_VAR header = index multi_match = 1 match_opcode = 101 match_parameter2 = 128 END // all headers except first have dupe confusion immunity
  END

COPY_EXISTING ~sppr509.spl~ ~override~ //magic resistance
  LPF ALTER_EFFECT INT_VAR header = 10 match_duration = 126 duration = 132 END // duration is one round short at level 19
  LPF ALTER_EFFECT INT_VAR header = 11 match_duration = 126 duration = 138 END // duration is two rounds short at level 20

COPY_EXISTING ~sppr608.spl~ ~override~ // harm
              ~sppr414.spl~ ~override~ // cause serious wounds
              ~sppr510.spl~ ~override~ // cause critical wounds
  LPF ALTER_EFFECT INT_VAR match_duration = 15 duration = 12 END // duration is three seconds long

// at levels 14-15, shield of the archons absorbs 9 levels of level 3 spells instead of the correct 7
COPY_EXISTING ~sppr701.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 201 match_parameter1 = 9 parameter1 = 7 match_duration = 252 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 201 match_parameter1 = 9 parameter1 = 7 match_duration = 270 END
  BUT_ONLY

COPY_EXISTING ~sppr709.spl~ ~override~ // Confusion [bg2]
  LPF CLONE_EFFECT INT_VAR header = 0 match_opcode = 174 timing = 1 duration = 0 STR_VAR match_resource = eff_e05 resource = eff_m05 END // missing sound at min level

// tbd, cam
// shield spell shouldn't stack
COPY_EXISTING ~spwi114.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 142 opcode = 321 power = 0 parameter2 = 0 timing = 1 duration = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END
  BUT_ONLY

// tbd, cam
// know alignment's save should match description
COPY_EXISTING ~spwi208.spl~ ~override~ // know alignment, arcane - lacks saving throw
              ~sppr209.spl~ ~override~ // know alignment, divine - has -2 penalty
  LPF ALTER_EFFECT INT_VAR savingthrow = BIT0 savebonus = 0 END
  BUT_ONLY

COPY_EXISTING ~spwi517.spl~ ~override~ // protection from acid [bg, bg2, iwd]
  LPF DELETE_EFFECT INT_VAR header = 0 match_opcode = 141 END                 // shouldn't be here
  LPF DELETE_EFFECT INT_VAR header = 0 match_opcode =  61 multi_match = 1 END // dupe effect

COPY_EXISTING ~%WIZARD_PHANTOM_BLADE%.spl~ ~override~ // spwi518, phantom blade - caps at 18
  LPF CD_LEVEL_SELECT-O-MATIC INT_VAR base_dur = (round * 3) END // 3 rounds + 1 round/level
  BUT_ONLY

//tbd, cam
// conjure fire elemental pauses the caster five rounds at level 19, correct three rounds at all other levels
COPY_EXISTING ~spwi620.spl~ ~override~ // conjure fire elemental
  LPF ALTER_EFFECT INT_VAR match_opcode = 165 duration = 18 END // pause target is wrong specifically at level 19
  BUT_ONLY

COPY_EXISTING ~spwi622.spl~ ~override~ // Conjure Earth Elemental [sod, bg2]
  LPF ALTER_EFFECT INT_VAR header = 0 match_duration = 840 duration = 660 END // some durations wrong when cast at min level

// mismatching save types between cosmetics and actual effects
COPY_EXISTING  ~spwm179.spl~  ~override~ // Polymorph Other (wild surge)
  LPF ALTER_EFFECT INT_VAR match_savingthrow = BIT4 savingthrow = BIT0 END 
