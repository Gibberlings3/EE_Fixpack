/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~ BEGIN
  INCLUDE ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~
END

/////                                                  \\\\\
///// lua fixes                                        \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// ids/2da/ini fixes                                \\\\\
/////                                                  \\\\\

// tbd, cam
// Names of parameters 2 and 3 for ExploreMapChunk() should be swapped in ACTION.IDS
// adding non-res version of ForceSpellNoFeedback action
COPY_EXISTING ~action.ids~ ~override~
  REPLACE_TEXTUALLY ~435 ExploreMapChunk(P:Center\*,I:Radius\*,I:Mode\*)~ ~435 ExploreMapChunk(P:Center*,I:Mode*,I:Radius*)~
  REPLACE_TEXTUALLY ~^\(466[ %TAB%].+\)$~ ~466 ForceSpellNoFeedback(O:Target,I:Spell*Spell)%WNL%\1~ // add non-res version
  BUT_ONLY

COPY_EXISTING ~animate.ids~ ~override~
  REPLACE_TEXTUALLY ~0xf005[ %TAB%]+cl_4_Collector_Thug~ ~0xf006 cl_4_Collector_Thug~
  REPLACE_TEXTUALLY ~0xf042[ %TAB%]+Trelon~ ~0xf043 Trelon~
BUT_ONLY

// syncing damages.ids header between games (cosmetic)
COPY_EXISTING ~damages.ids~ ~override~
  REPLACE_TEXTUALLY ~^\(13\)~ ~IDS V1.0%LNL%\1~ // pstee

// multi-step process to add immunity to Abyssal Fury to imdeath item: see splstate.ids, imdeath.itm, spwi912.spl
APPEND ~splstate.ids~ ~136 DEATH_IMMUNITY~

INCLUDE ~eefixpack/files/tph/a7/action_changestoremarkup_fix.tph~ // fix parameter order in the ChangeStoreMarkup() script action
INCLUDE ~eefixpack/files/tph/tbd_splprot_kitfix.tph~ // tbd, cam: fixing kit check to be a numerical equality check, not bitwise

INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves
INCLUDE ~eefixpack/files/tph/a7/pst_ini_extra.tph~  // extra animation slots for special characters
INCLUDE ~eefixpack/files/tph/animation_ini_fixes.tph~ // errors in animation definitions

// ensure that ids tables are properly sorted
COPY_EXISTING_REGEXP ~^.+\.ids$~ ~override~
  LPF sort_ids END
BUT_ONLY

CLEAR_IDS_MAP // reload ids, if we end up having any changes

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

// tbd, sam.
// merging multi-part area animations to eliminate issues
ACTION_FOR_EACH file IN
  a0508bl2 a0508bl3 a0508bl4 a0508tr1 a0508tr2 a0508tr3 a1201g1l a1201g1r a1201g2l a1201g2r
  a1201g3l a1201g3r a1202gb1 a1202gb2 a1202gb3 a1202gb4 a13wzc2a a13wzc2b a13wzca a13wzcb
BEGIN
  COPY ~eefixpack/files/bam/area_anim/%file%.bam~ ~override~
END

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/baf/pst~

INCLUDE ~eefixpack/files/tph/pst_script_fixes.tph~

// tbd, cam
// remove LOCALS set as it doesn't actually do anything in region script
COPY_EXISTING ~1204trig.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("DebugOutput","LOCALS",1)~ ~~
  END
  BUT_ONLY

INCLUDE ~eefixpack/files/tph/ending_cutscenes.tph~ // tightening end of cutscenes

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/assign_area_scripts.tph~ // assign/correct area scripts to areas

INCLUDE ~eefixpack/files/tph/pst_are_fixes.tph~ // ARE-specific fixes
INCLUDE ~eefixpack/files/tph/pst_wed_fixes.tph~ // WED-specific fixes
INCLUDE ~eefixpack/files/tph/pst_wmp_fixes.tph~ // WMP-specific fixes

// tbd, cam
// actors on impassable terrain: https://www.gibberlings3.net/forums/topic/34875-bug-report-ee-content-bugs/page/9/#comment-315538
LAF cd_move_actor INT_VAR old_x =  802 old_y =  218 new_x =  738 new_y =  262 STR_VAR area = ar0902 END // Anarchist (actor #5) at position [802.218] is on impassable terrain

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_cre_fixes_pstee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/pst_cre_color_fixes.tph~ // creature color fixes

// Trias is vulnerable to several spell effects that make it impossible to finish the game
COPY_EXISTING ~trias.cre~  ~override~
              ~triasa.cre~ ~override~
              ~triasb.cre~ ~override~
              ~triasc.cre~ ~override~
  REMOVE_CRE_ITEM no_curse
  ADD_CRE_ITEM ~no_curse~ #0 #0 #0 ~NONE~ ~cloak~
  ADD_CRE_ITEM ~imdeath~ #0 #0 #0 ~NONE~ ~ring~
  //LPF ADD_CRE_EFFECT INT_VAR opcode = 206 target = 1 timing = 9 STR_VAR resource = spwi912 END 
  //LPF ADD_CRE_EFFECT INT_VAR opcode = 83 target = 1 parameter2 = 233 timing = 9 END 

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

//INCLUDE ~eefixpack/files/tph/tbd_bulk_itm_fixes_pstee.tph~ // bulk general changes (nothing here yet)

// multi-step process to add immunity to Abyssal Fury to imdeath item: see splstate.ids, imdeath.itm, spwi912.spl
COPY_EXISTING ~imdeath.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 328 target = 1 parameter2 = 136 timing = 2 special = 1 END

// The Number of Ku'u Yin should protect from Chaotic creatures
COPY_EXISTING ~kyname.itm~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 0 END   // Remove: AC bonus
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 33 END  // Remove: Save vs. Death bonus
  PATCH_FOR_EACH align IN ~49~ ~50~ ~51~ BEGIN // alignment: chaotic good/neutral/evil
    LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode        = 219   // AC vs. Creature Type Modifier
      target        = 1     // Self
      timing        = 2     // While equipped
      parameter1    = align // IDS value
      parameter2    = 8     // IDS target: ALIGN.IDS
    END
  END
BUT_ONLY

// Not usable by Annah
COPY_EXISTING ~uhrknf.itm~ ~override~
  WRITE_LONG 0x1e (THIS | BIT28)  // Exclude: Annah
BUT_ONLY

// Not usable by Priests and various NPCs
COPY_EXISTING ~supwep.itm~ ~override~
              ~fork.itm~   ~override~
  // Exclude: Priests, Dak'kon, Fall-From-Grace, Vhailor, Ignus, Morte, Nordom, Annah
  WRITE_LONG 0x1e (THIS | BIT7 | BIT20 | BIT21 | BIT23 | BIT24 | BIT25 | BIT26 | BIT28 )
BUT_ONLY

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

//INCLUDE ~eefixpack/files/tph/tbd_bulk_spl_fixes_pstee.tph~ // bulk general changes (nothing here yet)

// multi-step process to add immunity to Abyssal Fury to imdeath item: see splstate.ids, imdeath.itm, spwi912.spl  
COPY_EXISTING ~spwi912.spl~ ~override/spwi912a.spl~ // payload: this is the original spell and effects
  WRITE_LONG 0x08 `0 // blank name
  
COPY_EXISTING ~spwi912.spl~ ~override/spwi912b.spl~ // if target is immune, they get this spell which just floats an immunity message
  WRITE_LONG 0x08 `0 // blank name
  LPF ALTER_HEADER INT_VAR projectile = 1 END 
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 330 target = 2 parameter1 = RESOLVE_STR_REF(@100) timing = 1 END 
  
COPY_EXISTING ~spwi912.spl~ ~override~ // the original spell; applies spwi912a or b depending on whether the target is immune via imdeath.itm
  LPF ALTER_HEADER INT_VAR projectile = 1 END 
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = 136 parameter2 = 110 timing = 1 STR_VAR resource = spwi912b END 
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = 136 parameter2 = 111 timing = 1 STR_VAR resource = spwi912a END 

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\

// prevent spell called via op232 from interrupting caster sounds
INCLUDE ~eefixpack/files/tph/noisy_fireshields.tph~

// removing reference to invalid game file
DISABLE_FROM_KEY ~.bcs~

INCLUDE ~eefixpack/files/tph/pst_bam_fixes.tph~ // BAM-specific fixes

// tbd, cam
// this works fine, just doing some cleanup
COPY_EXISTING ~ar0502.ini~ ~override~
  REPLACE_TEXTUALLY ~\[guard5\][ %TAB%%LNL%%MNL%%WNL%]+\([%LNL%%MNL%%WNL%]\[guard[56]\]\)~ ~\1~
  BUT_ONLY

// tbd, cam
// fix broken spawn coordinates
COPY_EXISTING ~ar1600.ini~ ~override~
  REPLACE_TEXTUALLY ~^\([ %TAB%]*spawn_point[ %TAB%]*=[ %TAB%]*\[[ %TAB%]*[0-9]+\)[ %TAB%]*,[ %TAB%]*\([0-9]+[ %TAB%]*:[ %TAB%]*[0-9]+[ %TAB%]*\]\)~ ~\1.\2~

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_pstee_probabilities.tph~ // ie-6021 & ie-6037, cam: probability fixes
