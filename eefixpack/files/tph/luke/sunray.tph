// This function moves Sunray so as to use 326 magic rather than those messy 177s.
// This is a "eefixpack\files\tph\luke\7eyes\7eyes.tph" follow-up...

DEFINE_ACTION_FUNCTION "CLERIC_SUNRAY"
BEGIN
	ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
		COPY_EXISTING - "%CLERIC_SUNRAY%.spl" "override"
			GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
			PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
				GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
				PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
					PATCH_MATCH SHORT_AT "%fx_off%" WITH
						326 WHEN (SLONG_AT ("%fx_off%" + 0x4) == IDS_OF_SYMBOL ("splstate" "BLIND_IMMUNITY")) AND (LONG_AT ("%fx_off%" + 0x8) == 111) BEGIN
							READ_ASCII ("%fx_off%" + 0x14) "subspell"
						END
						DEFAULT
					END
				END
			END
		BUT_ONLY_IF_IT_CHANGES
		//
		COPY_EXISTING "%subspell%.spl" "override"
			GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
			PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
				PATCH_IF SHORT_AT ("%ab_off%" + 0x10) > 1 BEGIN // min level
					WRITE_BYTE "%ab_off%" 0xFF
				END
			END
			LPF "DELETE_SPELL_HEADER" INT_VAR "header_type" = 0xFF END
		BUT_ONLY
	END ELSE BEGIN // iwdee
		COPY_EXISTING "%CLERIC_SUNRAY%.spl" "override"
			GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
			PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
				GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
				PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
					PATCH_MATCH SHORT_AT "%fx_off%" WITH
						326 WHEN SLONG_AT ("%fx_off%" + 0x4) == IDS_OF_SYMBOL ("splstate" "BLIND_IMMUNITY") AND LONG_AT ("%fx_off%" + 0x8) == 111 BEGIN
							READ_ASCII ("%fx_off%" + 0x14) "subspell"
							WRITE_LONG ("%fx_off%" + 0x24) BIT0 // save vs. spell
						END
						DEFAULT
					END
				END
			END
		BUT_ONLY
		//
		COPY_EXISTING "%subspell%.spl" "override\%CLERIC_SUNRAY%b.spl"
			LPF "DELETE_EFFECT" INT_VAR "match_duration" = 60 END
			LPF "ALTER_EFFECT" INT_VAR "savingthrow" = 0 END
		BUT_ONLY
		//
		COPY_EXISTING "%subspell%.spl" "override\%CLERIC_SUNRAY%c.spl"
			LPF "DELETE_EFFECT" INT_VAR "match_duration" = 12 END
			LPF "ALTER_EFFECT" INT_VAR "savingthrow" = 0 END
		BUT_ONLY
		//
		COPY_EXISTING "%subspell%.spl" "override"
			LPF "DELETE_EFFECT" END
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 326 "target" = 2 "parameter2" = 32 "timing" = 1 "resist_dispel" = BIT0 BOR BIT1 STR_VAR "resource" = "%CLERIC_SUNRAY%b" END // Apply effects list: !(GENERAL=UNDEAD || RACE=MYCONID)
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 326 "target" = 2 "parameter2" = 31 "timing" = 1 "resist_dispel" = BIT0 BOR BIT1 STR_VAR "resource" = "%CLERIC_SUNRAY%c" END // Apply effects list: (GENERAL=UNDEAD || RACE=MYCONID)
		BUT_ONLY
	END
END