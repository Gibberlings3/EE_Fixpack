// This function moves Searing Orb so as to use 326 magic rather than those messy 177s.
// This a "eefixpack\files\tph\luke\7eyes\7eyes.tph" follow-up...

DEFINE_ACTION_FUNCTION "CLERIC_SOL_SEARING_ORB"
BEGIN
	COPY_EXISTING "sorb.itm" "override"
		/* Header */
		WRITE_ASCII 0x10 ~~ #8 // Used up item
		/* Feature blocks */
		GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
		PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
			GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
			PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
				PATCH_MATCH SHORT_AT "%fx_off%" WITH
					326 WHEN (SLONG_AT ("%fx_off%" + 0x4) == IDS_OF_SYMBOL ("splstate" "BLIND_IMMUNITY")) AND (LONG_AT ("%fx_off%" + 0x8) == 111) BEGIN
						READ_ASCII ("%fx_off%" + 0x14) "subspell"
					END
					DEFAULT
				END
			END
		END
		LPF "DELETE_ITEM_EFFECT" INT_VAR "opcode_to_delete" = "-1" END // fresh start
		//
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 2 "opcode" = 215 "target" = 2 "timing" = 1 STR_VAR "resource" = "ICFIRSDI" END // Play visual effect (Visual/Cosmetic effect (no save, bypass MR))
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 2 "opcode" = 326 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("splstate" "BLIND_IMMUNITY") "parameter2" = 111 "timing" = 1 STR_VAR "resource" = "%subspell%" END // Apply effects list
		// 6d6 (fire), Save vs. Spell for half damage
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 2 "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("DMGTYPE" "FIRE") "timing" = 1 "resist_dispel" = BIT0 "dicenumber" = 6 "dicesize" = 6 "savingthrow" = BIT0 "special" = BIT8 END
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 2 "opcode" = 318 "target" = 2 "parameter2" = 2 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource (GENERAL!=UNDEAD)
		// 6d6 (fire), no save (for GENERAL=UNDEAD only)
		// - 12d6 (fire) upon a failed Save vs. Spell
		// - 9d6 = 3d6 + 6d6 (fire) upon a successful Save vs. Spell
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 2 "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("DMGTYPE" "FIRE") "timing" = 1 "resist_dispel" = BIT0 "dicenumber" = 6 "dicesize" = 6 END
	BUT_ONLY_IF_IT_CHANGES
	//
	WITH_SCOPE BEGIN
		COPY_EXISTING "%subspell%.spl" "override"
			LPF "DELETE_EFFECT" END
			// Blinds undead target for 12 rounds (Save vs. Spell for 6 rounds)
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 326 "target" = 2 "parameter2" = 1 "timing" = 1 "resist_dispel" = BIT0 | BIT1 STR_VAR "resource" = "sorb01" END // Apply effects list (GENERAL=UNDEAD)
			// Blinds living target for 1d6 rounds (Save vs. Spell negates)
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 326 "target" = 2 "parameter2" = 2 "timing" = 1 "resist_dispel" = BIT0 | BIT1 "savingthrow" = BIT0 STR_VAR "resource" = "sorb02" END // Apply effects list (GENERAL!=UNDEAD)
		BUT_ONLY
	END
	// Subspell B => GENERAL = UNDEAD => "Blinds undead target for 12 rounds (Save vs. Spell for 6 rounds)"
	WITH_SCOPE BEGIN
		COPY_EXISTING "%subspell%.spl" "override/sorb01.spl"
			LPF "DELETE_EFFECT" END // delete current content
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 74 "target" = 2 "duration" = 72 "resist_dispel" = BIT0 + BIT1 "savingthrow" = BIT0 END // Blindness (12 rounds): Save vs. Spell
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 2 "parameter1" = ("%game_is_bgee%" ? 31786 : 14674) "timing" = 1 "resist_dispel" = BIT0 + BIT1 "savingthrow" = BIT0 END // Display string: Save vs. Spell
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 318 "target" = 2 "resist_dispel" = BIT0 + BIT1 "savingthrow" = BIT0 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource: Save vs. Spell
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 74 "target" = 2 "duration" = 36 "resist_dispel" = BIT0 + BIT1 END // Blindness (6 rounds): no save
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 2 "parameter1" = ("%game_is_bgee%" ? 31786 : 14674) "timing" = 1 "resist_dispel" = BIT0 + BIT1 END // Display string: no save
		BUT_ONLY_IF_IT_CHANGES
	END
	// Subspell C => GENERAL != UNDEAD => "Blinds living target for 1d6 rounds (Save vs. Spell negates)"
	WITH_SCOPE BEGIN
		COPY_EXISTING "%subspell%.spl" "override/sorb02.spl"
			/* Feature blocks */
			LPF "DELETE_EFFECT" END // delete current content
			// poor approximation of 1d6
			FOR ("i" = 0 "j" = 16 "k" = 1 ; "%k%" <= 6 ; "i" += 17 "j" += 17 "k" += 1) BEGIN
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 74 "target" = 2 "duration" = ("%k%" * 6) "resist_dispel" = BIT0 + BIT1 "probability2" = "%i%" "probability1" = "%j%" END // Blindness (1d6 rounds)
			END
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 2 "parameter1" = ("%game_is_bgee%" ? 31786 : 14674) "timing" = 1 "resist_dispel" = BIT0 + BIT1 END // Display string
		BUT_ONLY_IF_IT_CHANGES
	END
END