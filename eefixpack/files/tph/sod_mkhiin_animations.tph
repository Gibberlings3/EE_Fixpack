/*
The broad overview: as a goblin, m'khiin doesn't have the normal range of animations that normal
playable races do, e.g. goblin animations include their weapons, not have them overlaid like a human
animation does. So she has to switch animations entirely based on what's equipped. In case that's not
complicating enough, she only has a handful of animations:

  axe & shield  - her default, used even when nothing's equipped or using non-axe/bow/staff, e.g. sling
  axe & shield, armored - used when an axe is equipped, only when armored
  bow - used with bow
  bow, armored - use when wearing armor (any) with a bow
  staff - used with a staff equipped

So we facilitate these changes by having weapons apply animation changes via eff. These effs are
targeted to class:shamans, and non-goblin race creatures are made immune. Bows apply both the armored
and unarmored animation effs; armor blocks the unarmored animation change when worn.

known issue: Because the bow/axe animations rely on armor being equipped first, it means the player
can get the wrong iteration of the bow animation by e.g. removing/equipping armor after a bow.

other big issue: for this to fully work, other mods also need to apply these effects to anything that
goes in the armor slot, or to any axe/staff/bow that m'khiin can equip.
*/


// first remove existing anim changes from items
COPY_EXISTING ~bdax1h02.itm~ ~override~
              ~bdax1h05.itm~ ~override~
              ~bdbow01.itm~  ~override~
              ~bdstaf02.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 53 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter1 = 75 match_parameter2 = 76 END

// first remove existing anim changes from items
COPY_EXISTING ~bdchan01.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 328 match_parameter2 = 75 END

// add effs that will effect animation changes
COPY ~eefixpack/files/eff/cdmk15.eff~  ~override~ // bow
     ~eefixpack/files/eff/cdmk15a.eff~ ~override~ // bow, armored
     ~eefixpack/files/eff/cdmk25.eff~  ~override~ // axe
     ~eefixpack/files/eff/cdmk25a.eff~ ~override~ // axe, armored
     ~eefixpack/files/eff/cdmk26.eff~  ~override~ // staff

// mode 4 mkhiin items are pointless; retan only those items which are actually mkhiin-only and update mode
OUTER_SET mkhiin_use = RESOLVE_STR_REF(@106)
APPEND ~item_use.2da~ ~bdleat02 MKHIIN %mkhiin_use% 2 44149~
COPY_EXISTING ~item_use.2da~ ~override~ // remove arbitrary restrictions
  REPLACE_TEXTUALLY CASE_INSENSITIVE
    ~^\(bdax1h02\|bdax1h05\|bdbow01\|bdchan01\|bdchan02\|bdleat02\|bdleat06\|bdrobe03\|bdshld07\|bdstaf02\)\([ %TAB%]+MKHIIN[ %TAB%]+\)[0-9]+\([ %TAB%]+\)4\([ %TAB%]+[0-9]+\)~
    ~\1\2%mkhiin_use%\32\4~ // update lines for actual mkhiin-only equipment
  REPLACE_TEXTUALLY ~^[^ %TAB%]+[ %TAB%]+MKHIIN[ %TAB%]+[0-9]+[ %TAB%]+4[ %TAB%]+[0-9]+[ %TAB%%LNL%%MNL%]+~ ~~ // delete other, existing mkhiin lines
  PRETTY_PRINT_2DA

ACTION_FOR_EACH item IN

  ~ax1h01~    // battle axe
  ~ax1h01q~   // quality battle axe
  ~ax1h02~    // battle axe +1
  ~ax1h03~    // battle axe of mauletar +2
  ~ax1h04~    // throwing axe
  ~ax1h05~    // throwing axe +2
  ~ax1h06~    // rhyte's last arrow +2
  ~ax1h07~    // wizard slayer
  ~ax1h11~    // battle axe +2
  ~ax1h16~    // k'logarath +4
  ~ax1h17~    // battle axe +3
  ~ax1h18~    // beruel's retort +1
  ~ax1h19~    // golden axe +1
  ~bdax1h01~  // battle axe
  ~bdax1h02~  // axe of kruntur +2
  ~bdax1h03~  // chill axe +2
  ~bdax1h04~  // throwing axe +1
  ~bdax1h05~  // grubdoubler's axe +1
  ~bdblun06~  // the uncursed staff +1
  ~bdbow01~   // blightbringer +1
  ~bdbow03~   // fleshripper +2
  ~bdbow05~   // savage shortbow +2
  ~bdchan01~  // jegg's leathers +2
  ~bdchan02~  // kruntur's armor +3
  ~bdchan03~  // crusader uniform
  ~bdchan05~  // corwin's armor +2
  ~bdchanca~  // mail of the hallowed hero +3
  ~bdleat01~  // cornugon's revenge +2
  ~bdleat02~  // goblin hide armor +2
  ~bdleat04~  // changeling's vest
  ~bdleat05~  // dragonscale armor
  ~bdleat06~  // goblin leathers +1
  ~bdleat07~  // the night's embrace +3
  ~bdleat08~  // hide armor +1
  ~bdplat03~  // bloody bone plate +2
  ~bdplat04~  // rhino beetle plate
  ~bdrobe03~  // the ghostdreamers' robe
  ~bdrobe04~  // cleric's vestments
  ~bdshstf1~  // wooden staff
  ~bdstaf01~  // brevin's quarterstaff +1
  ~bdstaf02~  // the soulherder's staff +2
  ~bdstaf03~  // echo of the fiend +3
  ~bow05~     // shortbow
  ~bow06~     // shortbow +1
  ~bow08~     // protector of the dryads +2
  ~bow18~     // shortbow +2
  ~enstaff~   // quarterstaff +3
  ~ensw2h~    // battle axe +3
  ~godbow~    // shortbow
  ~iax1h01~   // battle axe
  ~ileat04~   // studded leather armor
  ~leat01~    // leather armor
  ~leat02~    // leather armor +1
  ~leat03~    // protector of the second +2
  ~leat04~    // studded leather armor
  ~leat05~    // studded leather armor +1
  ~leat06~    // armor of missile attraction +2
  ~leat07~    // telbar's studded leather +2
  ~leat09~    // karajah's life and death +3
  ~leat10~    // hide armor
  ~leat11~    // leather armor +2
  ~leat12~    // leather armor +3
  ~leat15~    // studded leather armor +2
  ~leat25~    // rugged leathers +2
  ~plat06~    // ankheg plate mail
  ~staf01~    // quarterstaff
  ~staf02~    // quarterstaff +1
  ~staf03~    // quarterstaff
  ~staf05~    // staff of striking +3
  ~staf06~    // staff mace +2
  ~staf07~    // staff spear +2
  ~staf08~    // aule's staff +3
  ~staf18~    // quarterstaff +2
  ~staf25~    // the ossifier
  ~stafn1~    // neera's staff +1
  ~ttaxe1~    // battle axe
  ~ttleat~    // leather armor +1
BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    READ_LONG  0x18 flags
    READ_LONG  0x1e use
    //SET alter = 0
    PATCH_IF (((use & BIT30) = 0) AND ((flags & BIT2) = BIT2)) BEGIN // usable by druids and droppable
      READ_SHORT 0x1c type
      READ_BYTE  0x31 prof
      PATCH_IF ((type = 26) AND (prof = 102)) BEGIN // staff
        //SET alter = 1
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 318 target = 1 parameter1 = 161 parameter2 = 114 timing = 2 STR_VAR resource = ~cdmk%type%~ END // block non-goblins
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 177 target = 1 parameter1 =  21 parameter2 =   5 timing = 2 STR_VAR resource = ~cdmk%type%~ END // apply effs to only shamans
      END ELSE
      PATCH_IF (((type = 25) AND (prof =  92)) OR     // axe
                ((type = 15) AND (prof = 105))) BEGIN // shortbow
        //SET alter = 1
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 318 target = 1 parameter1 = 161 parameter2 = 114 timing = 2 STR_VAR resource = ~cdmk%type%a~ END // block non-goblins
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 318 target = 1 parameter1 = 161 parameter2 = 114 timing = 2 STR_VAR resource = ~cdmk%type%~  END // block non-goblins
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 177 target = 1 parameter1 =  21 parameter2 =   5 timing = 2 STR_VAR resource = ~cdmk%type%a~ END // apply effs to only shamans
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 177 target = 1 parameter1 =  21 parameter2 =   5 timing = 2 STR_VAR resource = ~cdmk%type%~  END // apply effs to only shamans
      END ELSE
      PATCH_IF (type = 2) BEGIN // armor
        //SET alter = 1
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 318 target = 1 timing = 2 STR_VAR resource = ~cdmk25~ END // block non-armored axe animation
        LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 999 opcode = 318 target = 1 timing = 2 STR_VAR resource = ~cdmk15~ END // block non-armored bow animation
      END
    END
  //  PATCH_IF alter AND debug BEGIN
  //    READ_LONG 0x0c name
  //    PATCH_IF (name > 0) AND (name < 999999) BEGIN
  //      READ_STRREF 0x0c name
  //    END ELSE BEGIN
  //      READ_STRREF 0x08 name
  //    END
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^........$" = 0) BEGIN SPRINT spc " " END ELSE
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^.......$" = 0) BEGIN SPRINT spc "  " END ELSE
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^......$" = 0) BEGIN SPRINT spc "   " END ELSE
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^.....$" = 0) BEGIN SPRINT spc "    " END ELSE
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^....$" = 0) BEGIN SPRINT spc "     " END ELSE
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^...$" = 0) BEGIN SPRINT spc "      " END ELSE
  //    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^..$" = 0) BEGIN SPRINT spc "       " END ELSE
  //                                                              BEGIN SPRINT spc "        " END
  //    PATCH_PRINT "              ~%SOURCE_FILE%~%spc%~override~ // %name%"
  //  END
    BUT_ONLY

END
