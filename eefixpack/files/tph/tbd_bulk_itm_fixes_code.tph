// cd_bulk_changes_cre array generated by tbd_bulk_cre_fixes_[game].tph

// don't need these yet
OUTER_SPRINT itm_speed_text @103
OUTER_SPRINT itm_weight_text @104
OUTER_SPRINT itm_thac0_text @105

ACTION_PHP_EACH cd_bulk_changes_itm AS params => file BEGIN

  //file, timing, magical, power, speed, weight, thac0, lore, type, prof, enchantment, dispellable, range => file // notes
  COPY_EXISTING ~%file%.itm~ ~override~
    PATCH_IF (IS_AN_INT params_1) BEGIN // has equipped effects with bad timing
      LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 timing = 2 duration = 0 END // instant/while equipped for all global effects
    END
    PATCH_IF (IS_AN_INT params_2) BEGIN // add/remove magical flag
      PATCH_IF (params_2 = 0)   BEGIN WRITE_BYTE 0x18 THIS & `BIT6 END // remove magical flag
      PATCH_IF (params_2 = 1)   BEGIN WRITE_BYTE 0x18 THIS | BIT6  END // add magical flag
    END
    PATCH_IF (IS_AN_INT params_3) BEGIN // has equipping effects with wrong power
      LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = params_3 END
    END
    PATCH_IF (IS_AN_INT params_4) BEGIN // wrong weapon speed
      LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 speed = params_4 END // anything that turns up in weapon slots (e.g. not magical abilities)
    END
    PATCH_IF (IS_AN_INT params_5) BEGIN // wrong weight
      WRITE_LONG 0x4c params_5
    END
    PATCH_IF (IS_AN_INT params_6) BEGIN // wrong to-hit
      LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 to_hit = params_6 END
    END
    PATCH_IF ((IS_AN_INT params_4) OR (IS_AN_INT params_5) OR (IS_AN_INT params_6)) BEGIN // update description for speed/weight/thac0 changes
      LPM cd_bulk_fix_item_strings_bulk
    END
    PATCH_IF (IS_AN_INT params_7)  BEGIN WRITE_SHORT 0x42 params_7  END // lore
    PATCH_IF (IS_AN_INT params_8)  BEGIN WRITE_SHORT 0x1c params_8  END // type
    PATCH_IF (IS_AN_INT params_9)  BEGIN WRITE_BYTE  0x31 params_9  END // prof
    PATCH_IF (IS_AN_INT params_10) BEGIN WRITE_LONG  0x60 params_10 END // enchantment
    PATCH_IF (IS_AN_INT params_11) BEGIN // has equipped effects which should bypass MR
      LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_resist_dispel = 1 resist_dispel = 2 END
      LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_resist_dispel = 3 resist_dispel = 2 END
    END
    PATCH_IF (IS_AN_INT params_12) BEGIN // wrong range
      LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 range = params_12 END
    END
    BUT_ONLY IF_EXISTS

END
