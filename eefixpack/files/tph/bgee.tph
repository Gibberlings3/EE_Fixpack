
/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~ BEGIN
  INCLUDE ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~
END

ACTION_IF game_includes_sod BEGIN
  ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/fixes_%game%_sod.tph~ BEGIN
    INCLUDE ~eefixpack/languages/%LANGUAGE%/fixes_%game%_sod.tph~
  END
END

/////                                                  \\\\\
///// lua fixes                                        \\\\\
/////                                                  \\\\\

ACTION_IF game_includes_sod BEGIN
  WITH_SCOPE BEGIN
    INCLUDE ~eefixpack/files/tph/a7/sod_cinematics.tph~ // enable subtitles for sod cinematics
  END
END

COPY_EXISTING ~bgee.lua~ ~override~
  PATCH_IF game_includes_sod BEGIN
    REPLACE_TEXTUALLY ~\({'BDORCM1L',[ %TAB%]*1},\)~ ~\1%WNL%%TAB%{'HELM',%TAB%1},%WNL%%TAB%{'HVLN',%TAB%1},~
    REPLACE_TEXTUALLY ~\({'BDORCF1L',[ %TAB%]*2},\)~ ~\1%WNL%%TAB%{'SKAN',%TAB%2},~
    REPLACE_TEXTUALLY // has duplicate blocks of the OH areas in area listing 
      ~{"OH1000".+[%LNL%%MNL% %TAB%]+{"OH2000".+[%LNL%%MNL% %TAB%]+{"OH2010".+[%LNL%%MNL% %TAB%]+{"OH3000".+[%LNL%%MNL% %TAB%]+{"OH3010".+[%LNL%%MNL% %TAB%]+{"OH3020".+[%LNL%%MNL% %TAB%]+{"OH3100".+[%LNL%%MNL%]+\([ %TAB%]+}\)~
      ~\1~
/* comment this section, pending verification for mobile versions
    REPLACE_TEXTUALLY ~\({'BAELOTH',[ %TAB%]*1},\)~ ~\1%WNL%%TAB%{'NCERND',%TAB%1},%WNL%%TAB%{'NKELDOR',%TAB%1},%WNL%%TAB%{'NANOMEN',%TAB%1},%WNL%%TAB%{'NEDWIN',%TAB%1},%WNL%%TAB%{'NHAER',%TAB%1},%WNL%%TAB%{'NJAN',%TAB%1},%WNL%%TAB%{'NMINSC',%TAB%1},%WNL%%TAB%{'NVALYGA',%TAB%1},%WNL%%TAB%{'NYOSHIM',%TAB%1},%WNL%%TAB%{'NKORGAN',%TAB%1},~
    REPLACE_TEXTUALLY ~\({'NEERA',[ %TAB%]*2},\)~ ~\1%WNL%%TAB%{'NNALIA', 2},%WNL%%TAB%{'NMAZZY', 2},%WNL%%TAB%{'NAERIE', 2},~
  END ELSE BEGIN
    PATCH_FOR_EACH portrait IN BAELOTH NCERND NKELDOR NANOMEN NEDWIN NHAER NJAN NMINSC NVALYGA NYOSHIM NKORGAN NNALIA NMAZZY NAERIE NJAHEIR NVICON BEGIN
      REPLACE_TEXTUALLY ~--\({'%portrait%',[ %TAB%]*[12]},\)~ ~\1~
    END
*/
  END
  BUT_ONLY

INCLUDE ~eefixpack/files/tph/debug_area_listings.tph~ // correcting area names/styles in console menu

/////                                                  \\\\\
///// ids/2da/ini fixes                                \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/a7/action_changestoremarkup_fix.tph~ // fix parameter order in the ChangeStoreMarkup() script action
INCLUDE ~eefixpack/files/tph/a7/anim_3001.tph~ // fix missing creature animation definitions for Neothelid
INCLUDE ~eefixpack/files/tph/a7/cre_anim_fixes.tph~ // fix height offsets of health bar positions for creature animations
INCLUDE ~eefixpack/files/tph/animation_ini_fixes.tph~ // errors in animation definitions
INCLUDE ~eefixpack/files/tph/tbd_splprot_kitfix.tph~ // tbd, cam: fixing kit check to be a numerical equality check, not bitwise
INCLUDE ~eefixpack/files/tph/portrait_2da.tph~ // add defaults for portraits not listed in portrait.2da
INCLUDE ~eefixpack/files/tph/infravision.tph~ // correcting animations subject to infravision
INCLUDE ~eefixpack/files/tph/ids_sync.tph~ // sync race.ids, class.ids

// syncing damages.ids header between games (cosmetic)
COPY_EXISTING ~damages.ids~ ~override~
  REPLACE_TEXTUALLY ~^\(0x0001\)~ ~IDS V1.0%LNL%13%LNL%\1~ // bgee/sod

ACTION_IF (game_includes_sod) BEGIN
  INCLUDE ~eefixpack/files/tph/a7/sod_fogtype.tph~ // apply workaround for buggy FOG_TYPE implementation
END

APPEND ~interdia.2da~ ~BAELOTH BDBAELOB~ // missing banter file assignment for baeloth

// tbd, cam (from jmerry)
// clck31 available in bp, but missing in armor exclusionlist (fixed in sod)
APPEND ~itemexcl.2da~ ~clck31 1~ UNLESS ~^clck31[ %TAB%]~ // clck31 available via black pits

ACTION_IF !game_includes_sod BEGIN // add missing movie entries for bgee/sod
  APPEND ~movidesc.2da~ ~LOGO 25295~ // only logo missing w/o SoD
END ELSE BEGIN
  APPEND ~movidesc.2da~ // lots of stuff missing with SoD
~BGENTER 19660
CAMP 19662
ELDRCITY 19665
ENDMOVIE 19666
FRARMINN 19667
MINEFLOD 19671
NASHKELL 19672
WRECK 31834~
END
COPY_EXISTING ~movidesc.2da~ ~override~ // and then make it pretty
  PRETTY_PRINT_2DA

// imoen should only use one cre file, like other NPCs
COPY_EXISTING ~npclevel.2da~ ~override~
  REPLACE_TEXTUALLY ~Imoen2~ ~Imoen1~

// tbd, cam
// dupe text for wild surges: https://forums.beamdog.com/discussion/comment/1169266/#Comment_1169266
COPY_EXISTING ~wildmag.2da~ ~override~
  REPLACE_TEXTUALLY ~SPWM109\([ %TAB%]+\)[0-9]+~ ~SPWM109\131725~ // wrong text for surge

/*
luke
**splprot.2da**
- the newly added (v2.6) header descriptions are incorrect for the alignment rows
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "splprot.2da" "override"
    COUNT_2DA_COLS "cols"
    SET_2DA_ENTRY 33 0 "%cols%" "33_ALIGNMENTBITS=GOOD" // NOT BIT1 = Not NEUTRAL or EVIL
    SET_2DA_ENTRY 34 0 "%cols%" "34_ALIGNMENTBITS!=GOOD" // HAS BIT1 = Is NEUTRAL or EVIL
    SET_2DA_ENTRY 35 0 "%cols%" "35_ALIGNMENTBITS=NEUTRAL" // NOT BIT0 = Not GOOD or EVIL
    SET_2DA_ENTRY 36 0 "%cols%" "36_ALIGNMENTBITS!=NEUTRAL" // HAS BIT0 = Is GOOD or EVIL
    SET_2DA_ENTRY 37 0 "%cols%" "37_ALIGNMENTBITS=EVIL"
    SET_2DA_ENTRY 38 0 "%cols%" "38_ALIGNMENTBITS!=EVIL"
    SET_2DA_ENTRY 59 0 "%cols%" "59_ALIGNMENTBITS=LAWFUL" // NOT BIT5 = Not CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 60 0 "%cols%" "60_ALIGNMENTBITS!=LAWFUL" // HAS BIT5 = Is CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 61 0 "%cols%" "61_ALIGNMENTBITS=CHAOTIC"
    SET_2DA_ENTRY 62 0 "%cols%" "62_ALIGNMENTBITS!=CHAOTIC"
    SET_2DA_ENTRY 118 0 "%cols%" "118_ALIGNMENTBITS!=n" // Clearly state it is a bitwise check
    // Formatting
    PRETTY_PRINT_2DA
  BUT_ONLY_IF_IT_CHANGES
END

INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves

// Add missing "SPELL.IDS" entries (borrowed from SCS)
WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "spell_ids_missing"
  ACTION_DEFINE_ASSOCIATIVE_ARRAY "spell_ids_missing" BEGIN
    // Divine
    "1718" => "CLERIC_SYMBOL_STUN"
    "1719" => "CLERIC_SYMBOL_DEATH"
    // Arcane
    "2124" => "WIZARD_NAHALS_RECKLESS_DWEOMER" // alternate symbolic name
    "2222" => "WIZARD_CHAOS_SHIELD"
    "2302" => "WIZARD_REMOVE_MAGIC" // alternate symbolic name
    "2723" => "WIZARD_IMPROVED_CHAOS_SHIELD"
    "2490" => "WIZARD_POLYMORPH_NATURAL_FORM"
    "2493" => "WIZARD_POLYMORPH_FLIND"
    "2494" => "WIZARD_POLYMORPH_OGRE"
    "2495" => "WIZARD_POLYMORPH_SPIDER"
    "2496" => "WIZARD_POLYMORPH_MUSTARD_JELLY"
    "2497" => "WIZARD_POLYMORPH_BROWN_BEAR"
    "2498" => "WIZARD_POLYMORPH_BLACK_BEAR"
    "2499" => "WIZARD_POLYMORPH_WOLF"
    "2705" => "WIZARD_KHELBENS_WARDING_WHIP" // alternate symbolic name
    "2921" => "WIZARD_IMPROVED_ALACRITY" // alternate symbolic name
    "2931" => "SUCCUBUS_TELEPORT"
    "2985" => "RED_HOLY_MIGHT"
    
    // Innate   
    "3101" => "INNATE_CURE_LIGHT_WOUNDS"
    "3102" => "INNATE_SLOW_POISON"
    "3103" => "INNATE_DRAW_UPON_HOLY_MIGHT"
    "3104" => "INNATE_LARLOCHS_MINOR_DRAIN"
    "3105" => "INNATE_HORROR"
    "3106" => "INNATE_VAMPIRIC_TOUCH"
    "3122" => "AVENGER_SHAPESHIFT_NATURAL_FORM"
    "3123" => "DRUID_HUMAN_FORM"
    "3123" => "DRUID_SHAPESHIFT_FROM_BLACKBEAR"
    "3124" => "SHAPESHIFTER_SHAPESHIFT_NATURAL_FORM"
    "3150" => "SHAPESHIFT_NATURAL_FORM_1"
    "3151" => "SHAPESHIFT_NATURAL_FORM_2"
    "3152" => "SHAPECHANGE_MIND_FLAYER"
    "3153" => "SHAPECHANGE_IRON_GOLEM"
    "3154" => "SHAPECHANGE_GIANT_TROLL"
    "3155" => "SHAPECHANGE_GREATER_WOLFWERE"
    "3156" => "SHAPECHANGE_FIRE_ELEMENTAL"
    "3157" => "SHAPECHANGE_EARTH_ELEMENTAL"
    "3701" => "DEATHKNIGHT_FIREBALL"
    "3890" => "DEMON_FEAR"
    "3996" => "TANARI_DEATH_GAZE"
    "3997" => "TANARI_VAMPIRIC_TOUCH"
    "3998" => "TANARI_SILENCE"
    "3999" => "TANARI_PARALYZE"
    // Class
    "4101" => "DARK_GIFT" // exists in bgee but is unused
    "4212" => "PALADIN_DETECT_EVIL"
    "4213" => "PALADIN_PROTECTION_FROM_EVIL"
    "4321" => "BERSERKER_ENRAGE"
    "4412" => "THIEF_SET_SNARE" // alternate symbolic name
    "4414" => "THIEF_SET_SPECIAL_SNARE" // alternate symbolic name
    "4611" => "DRUID_SHAPESHIFT_BROWNBEAR"
    "4612" => "DRUID_SHAPESHIFT_WOLF"
    "4613" => "DRUID_SHAPESHIFT_BLACKBEAR"
    "4632" => "AVENGER_SHAPESHIFT_SWORDSPIDER"
    "4633" => "AVENGER_SHAPESHIFT_BABYWYVERN"
    "4634" => "AVENGER_SHAPESHIFT_FIRESALAMANDER"
    "4643" => "SHAPESHIFTER_SHAPESHIFT_WEREWOLF"
    "4644" => "SHAPESHIFTER_SHAPESHIFT_GREATERWEREWOLF"
    "4721" => "TALOS_STORMSHIELD"
    "4722" => "TALOS_LIGHTNING_BOLT"
    "4732" => "HELM_TRUE_SIGHT"
  END
  ACTION_PHP_EACH "spell_ids_missing" AS "code" => "name" BEGIN
    APPEND "spell.ids" "%code% %name%" UNLESS "%code%[ %TAB%]+%name%"
  END
END

// tbd, cam
// sync bgee/bg2ee spell.ids entries with extra sod spells
ACTION_IF ((!FILE_EXISTS_IN_GAME ~sppr116.spl~) OR (!FILE_EXISTS_IN_GAME ~spwi126.spl~) OR(!FILE_EXISTS_IN_GAME ~spwi228.spl~)) BEGIN
  INCLUDE ~eefixpack/files/tph/tbd_missing_spells.tph~
END

LAM "SPELL_IDS" // relaunch so as to take into account the BG2 entries

// ensure that ids tables are properly sorted
COPY_EXISTING_REGEXP ~^.+\.ids$~ ~override~
  LPF sort_ids END
BUT_ONLY

CLEAR_IDS_MAP // reload ids, if we end up having any changes

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

COPY ~eefixpack/files/bam/ixbow04.bam~  ~override~ // tbd, cam: fix light crossbow BAM
     ~eefixpack/files/2da/clabmo02.2da~ ~override~ // tbd, cam: borked kit ability table, dark moon monk
     ~eefixpack/files/2da/clabmo03.2da~ ~override~ // tbd, cam: borked kit ability table, sun soul monk
     ~eefixpack/files/2da/clabrn02.2da~ ~override~ // tbd, cam: borked kit ability table, archer
     ~eefixpack/files/2da/spwi417.2da~  ~override~ // tbd, cam: allow enchant weapon to be cast via contingency
     ~eefixpack/files/bam/mdem~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, demogorgon
     ~eefixpack/files/bam/mwyv~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, wyvern big - also add shadows
     ~eefixpack/files/bam/cmnk1inv.bam~ ~override~ // monk (tutorial, not pc) paperdoll
     ~eefixpack/files/bam/wphf2oin.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wplf2inv.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wplf2oin.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a1.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a3.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a5.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a7.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a8.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a9.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2g1.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2oa7.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2oa8.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2oa9.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2og1.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/sppr314d.bam~ ~override~ // new unholy blight portrait icon
     ~eefixpack/files/bam/sgraspa.bam~  ~override~ // fix spell icon
     ~eefixpack/files/bam/sgraspb.bam~  ~override~ // fix spell icon
     ~eefixpack/files/bam/sgraspc.bam~  ~override~ // fix spell icon
     ~eefixpack/files/bam/mvaf~         ~override~ // sam.: removing shadows from vampires, female
     ~eefixpack/files/bam/mvam~         ~override~ // sam.: removing shadows from vampires, male
     ~eefixpack/files/bam/nboh~         ~override~ // sam.: removing shadows from vampires, bodhi

// fixing shadows on item icons
COPY ~eefixpack/files/bam/item_shadows/bgee~       ~override~
     ~eefixpack/files/bam/item_shadows/bgee_bg2ee~ ~override~
     ~eefixpack/files/bam/item_shadows/bgee_iwdee~ ~override~
     ~eefixpack/files/bam/item_shadows/universal~  ~override~

INCLUDE ~eefixpack/files/tph/spell_bams.tph~ // updated spell BAMs

DISABLE_FROM_KEY ~guiwdb20.bam~ // incomplete resource; only referenced by unused CHU file

ACTION_IF !game_includes_sod BEGIN
    
  COPY ~eefixpack/files/bam/mcor~ ~override~ // adding cornugon animation for albert/rufie change
       ~eefixpack/files/wav/mcor~ ~override~ // adding cornugon animation for albert/rufie change
     
 END ELSE BEGIN      

  COPY ~eefixpack/files/bam/coadbar_.bam~ ~override~ // fix loading bar animation
       ~eefixpack/files/bam/coadcntr.bam~ ~override~ // fix logo animation
       ~eefixpack/files/bam/pxbow15.bam~  ~override~ // fix item description image
       ~eefixpack/files/bam/mgo7gu.bam~   ~override~ // m'khiin's missing getting-up animation

  COPY_EXISTING ~eefixpack/files/bam/mdr1~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon red

  // fixing shadows on item icons
  COPY ~eefixpack/files/bam/item_shadows/sod~             ~override~
       ~eefixpack/files/bam/item_shadows/sod_bg2ee~       ~override~
       ~eefixpack/files/bam/item_shadows/sod_bg2ee_iwdee~ ~override~

  // installing BAM V2 resources
  INCLUDE ~eefixpack/files/tph/install_bam_v2.tph~
  ACTION_FOR_EACH bam_resref IN cbdhel09 BEGIN
    LAF install_bam_v2 STR_VAR bam_resref END
  END
END

// tbd, sam.
// merging multi-part area animations to eliminate issues
ACTION_IF game_includes_sod BEGIN
  ACTION_FOR_EACH file IN bdfug01 bdfug02 bdfug03 bdfug04 BEGIN
    COPY ~eefixpack/files/bam/area_anim/%file%.bam~ ~override~
  END
END

// tbd, cam (from jmerry)
// curing poison should also cure insta-death from green slimes
//INCLUDE ~eefixpack/files/tph/tbd_green_slime.tph~ now part of 'cure' fixes

// BGCS-3460, cam
// death tyrant animation fixes
INCLUDE ~eefixpack/files/tph/b3460_death_tyrant.tph~ // BGCS-3460, cam: death tyrant animation fixes

// bgcs-3397, cam
// revenant animation fixes
INCLUDE ~eefixpack/files/tph/b3397_revenant.tph~ // bgcs-3397, cam: revenant animation fixes

// tbd, cam
// charmed dialogue fixes: https://www.gibberlings3.net/forums/topic/36173-bg-a-casual-200ish-dialogue-fixes/
INCLUDE ~eefixpack/files/tph/tbd_bgee_charm_dlg_fixes.tph~

/////                                                  \\\\\
///// mechanics fixes / overhauls                      \\\\\
/////                                                  \\\\\

// kjeron
WITH_SCOPE BEGIN
  WITH_TRA "eefixpack/languages/en_us/luke/polymorph_overhaul.tra" "eefixpack/languages/%LANGUAGE%/luke/polymorph_overhaul.tra" BEGIN
    INCLUDE "eefixpack/files/tph/luke/polymorph_overhaul.tph"
    LAF "POLYMORPH_OVERHAUL" END
  END
END

/*
luke
**Wing Buffet vs. MR**
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/wing_buffet_vs_mr.tph"
  LAUNCH_ACTION_FUNCTION "WING_BUFFET_VS_MR" END
END

// argent77
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/a7/improved_troll_regeneration.tph"
END

ACTION_IF game_includes_sod BEGIN 
  INCLUDE ~eefixpack/files/tph/sod_mkhiin_animations.tph~
END 

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes

ACTION_IF game_includes_sod BEGIN
  COMPILE ~eefixpack/files/d/%game%_sod_core_fixes.d~ // misc dialogue fixes
END

COMPILE ~eefixpack/files/d/bgee_tbd_nothing_to_say.d~ // nothing to say fixes for obg dialogues https://www.gibberlings3.net/forums/topic/36173-bg-a-casual-200ish-dialogue-fixes/

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_dopples_fixes.tph~ // tbd, cam (from k4thos/eet): gorion/teth/elminster dopples breaks if one is one-shot; also prevents disguised dopples from trying to shift if already dead
INCLUDE ~eefixpack/files/tph/aldeth_dopple.tph~     // more dopple fixes, this time for aldeth's quest

// tbd, cam (from k4thos/eet)
// if you lure aec'letec out of the basement, much of the cultist scripting breaks
COPY_EXISTING ~ar1002.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("TanariXP","GLOBAL",1)~    ~TriggerActivation("Door1003",TRUE) SetGlobal("TanariXP","GLOBAL",1)~
    REPLACE_TEXTUALLY ~SetGlobal("Cult63Spawn","GLOBAL",1)~ ~TriggerActivation("Door1003",FALSE) SetGlobal("Cult63Spawn","GLOBAL",1)~
  END
  BUT_ONLY

// tbd, cam
// xzar block loops the area script
COPY_EXISTING ~ar2700.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Exists("\(Xzar\|Imoen\|Montaron\)")~ ~InMyArea("\1")~
  END
  BUT_ONLY

// tarom should not purge breakable items when updating his store (don't reset depreciation)
COPY_EXISTING ~baldur.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~RemoveStoreItem("taerum","[^"]+",0)~ ~~
  END
  BUT_ONLY  
  
ACTION_IF game_includes_sod BEGIN

  // checking wrong var for dead safana
  COPY_EXISTING ~bd0101.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN 
      REPLACE_TEXTUALLY ~\(InPartyAllowDead("safana")[ %TAB%%LNL%%MNL%%WNL%]+\)Global("BD_SAFANA_DIED_IN_BG","GLOBAL",0)~ ~\1Global("BD_SAFANA_DIED_IN_BG","GLOBAL",1)~
    END
    BUT_ONLY

  // tbd, cam
  // can't reach the required deathcount for comments on lower difficulty since too many 'Followers' despawn
  COPY_EXISTING ~bd0120.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~NumDeadGT("Followers",4)~ ~NumDeadGT("Followers",2)~
    END
    BUT_ONLY
    
  EXTEND_TOP ~bd0120.bcs~ ~eefixpack/files/baf/bd0120_bpring.baf~ // remove slave rings if BP characters are imported directly to SoD

  // tbd, cam
  // syntax: an OR(4) only has two conditions
  COPY_EXISTING ~bd1000.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OR(4)~ ~OR(2)~ // luckily only one so easy replace
    END
    BUT_ONLY


  // tbd, cam
  // neera checking the wrong state of the skull
  COPY_EXISTING ~bd2000.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OpenState("Bhaal_Skull",TRUE)~ ~OpenState("Bhaal_Skull",FALSE)~ // wrong state
    END
    BUT_ONLY


  // tbd, cam
  // trying to activate a region via Ambient activate
  COPY_EXISTING ~bd2000.bcs~   ~override~
                ~bdbarghe.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~AmbientActivate("enlightenment"~ ~TriggerActivation("Enlightment"~ // yes, it's spelled that way
    END
    BUT_ONLY

  // tbd, cam
  // dialogues for blind albino cave can fire elsewhere (see also bddynahj.dlg et al)
  COPY_EXISTING ~bd5100aw.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~I[fs]ValidForPartyDialo\(g\|gue\)("\([^"]+\)")~ ~Range("\2",20) IfValidForPartyDialog("\2")~
    END
    BUT_ONLY

  // tbd, cam
  // referring to wrong DV for Halatathlaer
  // wrong variable scope for timer
  COPY_EXISTING ~bd5300.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~DisplayStringHead("bdhalat")~ ~DisplayStringHead("bdhalata")~ // script has a createcreature bdhalat, so need to extra bit of trigger to match
      REPLACE_TEXTUALLY ~"bd_sdd350b_ot_timer","bd2000"~ ~"bd_sdd350b_ot_timer","bd5300"~ // wrong variable scope
    END
    BUT_ONLY


  // tbd, cam
  // referring to wrong DV for Imoen
  COPY_EXISTING ~bd6200.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~ActionOverride("bdimoen"~ ~ActionOverride("imoen"~
    END
    BUT_ONLY


  // tbd, cam
  // referring to wrong DV for Corwin
  COPY_EXISTING ~bd7300.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"bdcorwin"~ ~"corwin"~
    END
    BUT_ONLY

  // tbd, cam (from k4thos/eet)
  //Dead NPCs stays in party in chapter 13
  COPY_EXISTING ~bdcut61.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~InParty("\([^"]+\)")~ ~InPartyAllowDead("\1")~
    END
    BUT_ONLY

  // correctly count monsters where you have to kill a specific number of them (see also bdwe1str.bcs et al)
  COPY_EXISTING ~bdffdop1.bcs~ ~override~ // Result: Continuously creates inventory items on the ground (lots of valuable stuff). 
                ~bdhelp.bcs~   ~override~ // Result: An endless stream of new animals are continuously summoned if you petrify or freeze one of the animals. 
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(StateCheck(Myself,STATE_REALLY_DEAD)\)~ ~StateCheck(Myself,STATE_DEAD) Global("cd_dead_do_once","LOCALS",0)~
      REPLACE_TEXTUALLY ~\(GiveItemCreate("misc07",Myself,250,0,0)\)~ ~SetGlobal("cd_dead_do_once","LOCALS",1) \1~ // bdffdop1
      REPLACE_TEXTUALLY ~\(ActionOverride("BDCUTID",ChangeAIScript("BDSPAWN",OVERRIDE))\)~ ~SetGlobal("cd_dead_do_once","LOCALS",1) \1~ // bdhelp
    END
    BUT_ONLY

  // tbd, cam
  // sod menhir quest fix: menhirs which spawn enemies on failure are checking/setting the wrong variable
  ACTION_FOR_EACH script IN 1 4 BEGIN

    COPY_EXISTING ~bdmenhi%script%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("activated","LOCALS"~ ~Global("BDMENHI%script%_Rune","GLOBAL"~
      END
      BUT_ONLY

  END

  // tbd, cam
  // possibly missing Minsc-rasaad banter due to typo
  COPY_EXISTING ~bdminsc.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"rasaad'"~ ~"rasaad"~
    END
    BUT_ONLY


  // tbd, cam
  // saying 'go back to the crypt entrance' in korlasz crypt doesn't make NPCs do anything
  COPY_EXISTING ~bdparty.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("dynaheir"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[780.1575])~ // dynaheir save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("edwin"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[780.1575])~ // edwin save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("baeloth"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[840.1590])~ // baeloth save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("khalid"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[840.1590])~ // khalid save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("safana"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[870.1710])~ // safana save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("dorn"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[920.1660])~ // dorn save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("minsc"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[920.1660])~ // minsc save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("neera"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[920.1660])~ // neera save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("jaheira"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[935.1720])~ // jaheira save location
      REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("viconia"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~
        ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[935.1720])~ // viconia save location
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("dynaheir"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ ~\1 RESPONSE #0 EscapeAreaMove("bd0120",780,1575,NE)~ // move dynaheir
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("edwin"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~    ~\1 RESPONSE #0 EscapeAreaMove("bd0120",780,1575,NE)~ // move edwin
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("baeloth"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~  ~\1 RESPONSE #0 EscapeAreaMove("bd0120",840,1590,NE)~ // move baeloth
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("khalid"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~   ~\1 RESPONSE #0 EscapeAreaMove("bd0120",840,1590,NE)~ // move khalid
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("safana"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~   ~\1 RESPONSE #0 EscapeAreaMove("bd0120",870,1710,NE)~ // move safana
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("dorn"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~     ~\1 RESPONSE #0 EscapeAreaMove("bd0120",920,1660,NE)~ // move dorn
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("minsc"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~    ~\1 RESPONSE #0 EscapeAreaMove("bd0120",920,1660,NE)~ // move minsc
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("neera"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~    ~\1 RESPONSE #0 EscapeAreaMove("bd0120",920,1660,NE)~ // move neera
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("jaheira"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~  ~\1 RESPONSE #0 EscapeAreaMove("bd0120",935,1720,NE)~ // move jaheira
      REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("viconia"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~  ~\1 RESPONSE #0 EscapeAreaMove("bd0120",935,1720,NE)~ // move viconia
    END
    BUT_ONLY


  // tbd, cam
  // spike trigger is a little too happy to murderize everyone, swap trigger
  COPY_EXISTING ~bdlever2.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~WalkedToTrigger~ ~Clicked~
    END
    BUT_ONLY

  // tbd, cam
  // don't tell shadow to kill bugbears if they're already dead (see also bdshadow.dlg)
  EXTEND_TOP ~bdshadow.bcs~ ~eefixpack/files/baf/bdshadow.baf~
  
END 

// shaman summon script errors - fix for bgee, bg2ee, iwdee
COPY_EXISTING ~bdshunsu.bcs~ ~override~ // chaman summun AI script
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!\(Detect(\[PC\.0\.0\.SHAMAN\])[ %TAB%%LNL%%MNL%]+CombatCounter(0)\)~ // have to include extra line to avoid other blocks
    	~\1~ // should be detect, not !detect (sod only)
    REPLACE_TEXTUALLY ~\(MoveToObject(\[PC\.0\.0\.SHAMAN])\)~ ~\1 Continue()~ // don't block item bonuses with infinite Move commands
  END   

// tbd, cam (from jmerry)
// bp: hogarl not using the right potion when he's hurt
COPY_EXISTING ~bpgiafir.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(SetGlobalTimer("GIANT_POTION","LOCALS",ONE_ROUND)[%TAB% %LNL%%MNL%%WNL%]+\)UseItem("POTN27",NearestEnemyOf(Myself))~ ~\1 UseItem("POTN52",Myself)~
  END
  BUT_ONLY

// tbd, cam (from jmerry)
// characters imported from bp retain their bp scripting
EXTEND_TOP ~bpplot.bcs~ ~eefixpack/files/baf/bpplotpatch.baf~ // BP script removes itself if not in BP area

// correctly count monsters where you have to kill a specific number of them (see also bdffdop1.bcs et al)
COPY_EXISTING ~bdwe1str.bcs~ ~override~
              ~dopald.bcs~   ~override~ // Result: Aldeth thanks you prematurely for killing all doppelgangers (ALDETH.DLG). Other dialogs may be affected as well (e.g. Brandilar or Scar). 
              ~gnolldr.bcs~  ~override~ // Result: Drizzt does not respond when you try to talk to him after all gnolls have been defeated (DRIZZT.DLG). 
              ~negoblin.bcs~ ~override~ // Result: The ranger Magreb doesn't recogize that you've killed all the goblins (NEMAGREB.DLG). 
              ~ogres.bcs~    ~override~ // Result: Sarhedra doesn't acknowledge that you defeated the ogres; quest cannot be completed (SARHED.DLG). 
              ~zombiew.bcs~  ~override~ // zombie farm count
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(StateCheck(Myself,STATE_REALLY_DEAD)\)\([ %TAB%%LNL%%MNL%%WNL%]+\)\(AreaCheck("OH2010")\)~ ~\3\2\1~ // invert negoblin triggers so we can use the same patch below
    REPLACE_TEXTUALLY ~\(StateCheck(Myself,STATE_REALLY_DEAD)\|Die()\)\([ %TAB%%LNL%%MNL%%WNL%]+THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE #[0-9]+[ %TAB%%LNL%%MNL%%WNL%]+\)\(IncrementGlobal("[^"]+","[^"]+",1)\)~
      ~StateCheck(Myself,STATE_DEAD) Global("cd_dead_do_once","LOCALS",0) \2 SetInterrupt(FALSE) SetGlobal("cd_dead_do_once","LOCALS",1) \3 SetInterrupt(TRUE)~
  END
  BUT_ONLY IF_EXISTS // bdwe1str is SoD 

// tbd, cam
// sod imoen missing xp-matching scripting, among other things
ACTION_IF game_includes_sod BEGIN
  EXTEND_BOTTOM ~imoen.bcs~ ~eefixpack/files/baf/sod_imoen_bottom.baf~
END


// tbd, cam
// NPCs should get their better starting equipment when doing the level-up thing; must follow the imoen.bcs script extension fix for sod games (currently immediately above)
COPY_EXISTING ~ajantis.bcs~ ~override~ // ajantis gets better armor
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("chan01","plat01")~ // chain > plate
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(16\|8\)000,SET)\)~
      ~\1 TransformItem("chan01","chan04")~ // chain > splint
  END
  BUT_ONLY

COPY_EXISTING ~dorn.bcs~ ~override~ // dorn gets helmet at 9k, plate at 36k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("chan04","plat01")~ // splint > plate
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\)000,SET)\)~
      ~\1 CreateItem("helm01",0,0,0) XEquipItem("helm01",Myself,SLOT_HELMET,EQUIP)~ // add helmet
  END
  BUT_ONLY

COPY_EXISTING ~edwin.bcs~ ~override~ // edwin gets throwing daggers at 41k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 CreateItem("dagg05",10,0,0) XEquipItem("dagg05",Myself,SLOT_WEAPON1,EQUIP)~ // add throwing daggers
  END
  BUT_ONLY

COPY_EXISTING ~eldoth.bcs~ ~override~ // eldoth gets bow & arrows at 10k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\)000,SET)\)~
      ~\1 CreateItem("arow01",20,0,0) XEquipItem("arow01",Myself,SLOT_AMMO0,EQUIP) CreateItem("bow03",0,0,0) XEquipItem("bow03",Myself,SLOT_WEAPON1,EQUIP)~ // add bow, arrows
  END
  BUT_ONLY

COPY_EXISTING ~imoen.bcs~ ~override~ // imoen short sword at 1300, studded leather at 20k; imoen can still use imoen2 cre file so skip short sword
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~
      ~\1 CreateItem("leat04",0,0,0) XEquipItem("leat04",Myself,SLOT_ARMOR,EQUIP)~ // add studded leather
  END
  BUT_ONLY

COPY_EXISTING ~jaheira.bcs~ ~override~ // jaheira gets sling & bullets at 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~
      ~\1 CreateItem("bull01",20,0,0) XEquipItem("bull01",Myself,SLOT_AMMO0,EQUIP) CreateItem("slng01",0,0,0) XEquipItem("slng01",Myself,SLOT_WEAPON1,EQUIP)~ // add sling & bullets
  END
  BUT_ONLY

COPY_EXISTING ~kagain.bcs~ ~override~ // kagain gets throwing axes at 2k, armor upgrades at 8 and 32
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~
      ~\1 CreateItem("ax1h04",10,0,0) XEquipItem("ax1h04",Myself,SLOT_WEAPON1,EQUIP)~ // add throwing axes
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(16\|8\)000,SET)\)~
      ~\1 TransformItem("leat04","chan01")~ // studded leather > chain
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("leat04","chan04")~ // studded leather > splint
  END
  BUT_ONLY

COPY_EXISTING ~khalid.bcs~ ~override~ // khalid gets bow & arrows at 2k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~
      ~\1 CreateItem("arow01",20,0,0) XEquipItem("arow01",Myself,SLOT_AMMO0,EQUIP) CreateItem("bow03",0,0,0) XEquipItem("bow03",Myself,SLOT_WEAPON1,EQUIP)~ // add bow, arrows
  END
  BUT_ONLY

COPY_EXISTING ~minsc.bcs~ ~override~ // minsc gets studded leather at 2k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~
      ~\1 TransformItem("leat01","leat04")~ // leather > studded leather
  END
  BUT_ONLY

COPY_EXISTING ~montaron.bcs~ ~override~ // montaron gets throwing axes at 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~
      ~\1 CreateItem("ax1h04",10,0,0) XEquipItem("ax1h04",Myself,SLOT_WEAPON1,EQUIP)~ // add throwing axes
  END
  BUT_ONLY

COPY_EXISTING ~safana.bcs~ ~override~ // montaron gets darts at 4k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\)000,SET)\)~
      ~\1 CreateItem("dart01",20,0,0) XEquipItem("dart01",Myself,SLOT_WEAPON1,EQUIP)~ // add darts
  END
  BUT_ONLY

COPY_EXISTING ~sharteel.bcs~ ~override~ // shar-teel gets xbow & bolts at 8k, armor upgrades at 8 and 32k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~
      ~\1 CreateItem("bolt01",20,0,0) XEquipItem("bolt01",Myself,SLOT_AMMO0,EQUIP) CreateItem("xbow04",0,0,0) XEquipItem("xbow04",Myself,SLOT_WEAPON1,EQUIP)~ // add sling & bullets
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(16\|8\)000,SET)\)~
      ~\1 TransformItem("leat04","chan04")~ // studded leather > splint
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("leat04","plat01")~ // studded leather > plate
  END
  BUT_ONLY

COPY_EXISTING ~skie.bcs~ ~override~ // skie gets short sword at 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~
      ~\1 CreateItem("sw1h07",0,0,0) XEquipItem("sw1h07",Myself,SLOT_WEAPON1,EQUIP)~ // add short sword
  END
  BUT_ONLY

COPY_EXISTING ~viconia.bcs~ ~override~ // viconia gets armor upgrades at 8k, 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,8000,SET)\)~
      ~\1 TransformItem("leat01","leat04")~ // leather > studded leather
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~
      ~\1 TransformItem("leat01","chan01")~ // leather > chain
  END
  BUT_ONLY

// tbd, cam
// tanar'ri do not hail from the nine hells; change albert to something more appropriate
COPY_EXISTING ~albert.bcs~   ~override~
              ~rufcut01.bcs~ ~override~
              ~rufcut02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Polymorph(TANARRI)~ ~Polymorph(CORNUGON)~
  END
  BUT_ONLY

// tbd, cam (from scs)
// make cydermac and friends go hostile more reliably (see also banditcy.baf)
EXTEND_TOP ~banditcy.bcs~ ~eefixpack/files/baf/bancyadd.baf~

// tbd, cam
// bad string refs for AI scripting
COPY_EXISTING ~bdimoenc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,46150)~ ~DisplayString(Myself,31249)~ // *quaffs a potion*
  END
  BUT_ONLY

COPY_EXISTING ~bdeldotc.bcs~ ~override~
              ~bdgarrcc.bcs~ ~override~
              ~bdgarric.bcs~ ~override~
              ~bdkivanc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,56560)~ ~DisplayString(Myself,31249)~ // *quaffs a potion*
  END
  BUT_ONLY

OUTER_SET use_wand = RESOLVE_STR_REF (@100)
COPY_EXISTING ~bddefai.bcs~  ~override~
              ~bddynac.bcs~  ~override~
              ~bdedwinc.bcs~ ~override~
              ~bdxzarc.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,66959)~ ~DisplayString(Myself,%use_wand%)~ // *uses a wand*
  END
  BUT_ONLY

// tbd, cam (from scs)
// make gretek and friends go hostile more reliably (see also arlin.cre et al, gretek.dlg)
COMPILE ~eefixpack/files/baf/gretek.baf~

//tbd, cam
// make marl's second bark dependent on Dunkin being available for dialogue (see marl.dlg)
COPY_EXISTING ~marl.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([ %TAB%]Num\(berOf\)?TimesTalkedTo(1)\)~ ~\1 !StateCheck("Dunkin",CD_STATE_NOTVALID)~
  END
  BUT_ONLY

// tbd, cam
// mulahey stutter fix (see also mulahe.dlg) https://www.gibberlings3.net/forums/topic/35178-scripting-bugs/#comment-308786
COPY_EXISTING ~mulahey.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("\(CDMulahey\|TalkedToMulahey\)","\(AR5405\|GLOBAL\)",0)\)~ ~\1 !StateCheck(Myself,STATE_CHARMED)~
    REPLACE_TEXTUALLY ~\(Allegiance(Myself,NEUTRAL)\)~ ~\1 !StateCheck(Myself,STATE_CHARMED)~
  END
  BUT_ONLY

// tbd, cam
// region/container scripts can't use LOCALS-scoped variables
// https://www.gibberlings3.net/forums/topic/35914-scripting-using-locals-in-non-sprite-triggers/
COPY_EXISTING ~netrig1.bcs~  ~override~ // trigger to bury rilsa should stop once rilsa is buried
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Global("TALK","LOCALS",1)~ ~GlobalLT("BURY_RILSA","GLOBAL",2)~
  END
  BUT_ONLY

// neera shouldn't initiate dialogue afer initial red wizard ambush if panicked
COPY_EXISTING ~neera.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\(Global("RedWizards","AR3300",3)[ %TAB%%LNL%%MNL%]+!ActuallyInCombat()\)~ 
      ~\1 !StateCheck(Myself,CD_STATE_NOTVALID)~
    REPLACE_TEXTUALLY ~\([ %TAB%]Global("RedWizards","AR3300",4)\)~ 
      ~\1 !StateCheck(Myself,CD_STATE_NOTVALID)~
  END  

INCLUDE ~eefixpack/files/tph/ending_cutscenes.tph~ // tightening end of cutscenes

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/assign_area_scripts.tph~ // assign/correct area scripts to areas

// bgcs-3454, cam
// lady's house lacks its normal music
COPY_EXISTING ~ar0132.are~ ~override~
  READ_LONG 0xbc song_off
  WRITE_LONG song_off 11
  BUT_ONLY

// tbd, cam
// actors on impassable terrain: https://www.gibberlings3.net/forums/topic/34875-bug-report-ee-content-bugs/page/9/#comment-315538
LAF cd_move_actor INT_VAR old_x = 2028 old_y = 1213 new_x = 1880 new_y = 1222 STR_VAR area = ar0500 END // Greater ghoul (actor #15) at position [2028.1213] is on impassable terrain
LAF cd_move_actor INT_VAR old_x = 1843 old_y =  188 new_x = 1400 new_y =  188 STR_VAR area = bd0020 END // Noblewoman (actor #122) at position [1843.188] is placed outside of map range
LAF cd_move_actor INT_VAR old_x = 3953 old_y = 2018 new_x = 3967 new_y = 1992 STR_VAR area = bd5000 END // Ogre (actor #7) at position [3953.2018] is on impassable terrain

// tbd, cam (from k4thos/eet)
//junk in BG1801.ARE Rest Spawn entries (harmless but annoying during save update)
COPY_EXISTING ~ar1801.are~ ~override~
  READ_LONG  0xc0 "rest_off" ELSE 0
  READ_SHORT rest_off + 0x98 cre_num
  FOR (index = cre_num ; index < 10 ; ++index) BEGIN
    WRITE_ASCII (rest_off + 0x48 + (0x08 * index)) ~~ #8 // blank any entry beyond the valid number of creatures
  END
  BUT_ONLY

// tbd, cam
// ambient blocks footstep sounds
COPY_EXISTING ~bd0116.are~ ~override~ // Ducal Palace, Basement
              ~tu0018.are~ ~override~ // Baldur's Gate Estate Cellar (Tutorial area)
  READ_SHORT 0x82 amb_num // inactive ambient
  READ_LONG  0x84 amb_off
  FOR (index = 0 ; index < amb_num ; ++index) BEGIN
    READ_ASCII (amb_off + 0x30 + (index * 0xd4)) sound1
    PATCH_IF ("%sound1%" STRING_COMPARE_CASE "gacavm02" = 0) BEGIN // main ambient
      WRITE_LONG (amb_off + 0x90 + (index * 0xd4)) (THIS BOR BIT1) // adds looping flag
    END
  END
  BUT_ONLY IF_EXISTS // bd0116 is sod

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_cre_fixes_bgee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/alignment_changes_bgee.tph~ // bulk alignment changes

INCLUDE ~eefixpack/files/tph/tbd_elemental_summons.tph~ // tbd, cam: elemental prince summons shouldn't grant XP
INCLUDE ~eefixpack/files/tph/tbd_conjure_animals_bearposu.tph~ // tbd, cam: bears summoned by conjure animals should used summoned scripts, not bear scripts

// tbd, cam (from scs)
// make gretek and friends go hostile more reliably (see also gretek.baf, gretek.dlg)
COPY_EXISTING ~arlin.cre~  ~override~
              ~catura.cre~ ~override~
              ~gretek.cre~ ~override~
              ~nader.cre~  ~override~
              ~pargus.cre~ ~override~
              ~wilf.cre~   ~override~
  FOR (index = 0x248 ; index < 0x270 ; index += 8) BEGIN
    READ_ASCII index script
    PATCH_IF (("%script%" STRING_COMPARE_CASE "" = 0) OR ("%script%" STRING_COMPARE_CASE "none" = 0)) BEGIN
      WRITE_ASCII index ~gretek~ #8
      SET index = 0x270
    END
  END
  BUT_ONLY

// tbd, cam (originally from jmerry)
// Nonstandard wand of missiles on Imoen and Mordaine replaced
COPY_EXISTING ~imoen1.cre~ ~override~
              ~imoen2.cre~ ~override~
              ~imoen4.cre~ ~override~
              ~imoen6.cre~ ~override~
              ~mordai.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = wand12 item = wand03 END
  BUT_ONLY // Nonstandard wand of missiles on Imoen and Mordaine replaced

// tbd, cam
// HD too low for most ankhegs; bgee ones also had pickpocketable shells
INCLUDE ~eefixpack/files/tph/tbd_ankheg.tph~

//tbd, cam
// poison mists can be summoned but can also spawn as normal creatures, so need both a gender: neither and gender: summoned version
INCLUDE ~eefixpack/files/tph/tbd_poison_mists.tph~

// corinth shouldn't have undroppable items, especially since the player can be given them
COPY_EXISTING ~bdcorint.cre~ ~override~
  LPF ALTER_CREATURE_ITEM INT_VAR flag_undroppable = 0 STR_VAR match_item = leat10 END
  LPF ALTER_CREATURE_ITEM INT_VAR flag_undroppable = 0 STR_VAR match_item = sw1h07 END
  BUT_ONLY IF_EXISTS // sod quest

// tbd, cam (from k4thos/eet)
// telania's victims shouldn't have player scripting, could potentially offer to join party
COPY_EXISTING ~bdpetr01.cre~ ~override~
              ~bdpetr02.cre~ ~override~
              ~bdpetr03.cre~ ~override~
              ~bdpetr04.cre~ ~override~
              ~bdpetr05.cre~ ~override~
  WRITE_ASCII 0x268 ~~ #8 // had dplaye3 assigned
  BUT_ONLY IF_EXISTS // sod quest

// tbd, cam (from k4thos/eet)
//Beamdog added SHOUT.BCS to Cultists that are meant to stay and do nothing during Aec'Letec fight. They react to shout by moving which can make them stuck on each other. Vanilla BG1 implementation is more safe in this case
COPY_EXISTING ~cultd1.cre~ ~override~
              ~cultd2.cre~ ~override~
              ~cultd3.cre~ ~override~
              ~cultd4.cre~ ~override~
              ~cultd5.cre~ ~override~
              ~cultd6.cre~ ~override~
  WRITE_ASCII 0x248 ~~ #8 // override script
  BUT_ONLY

// tbd, cam
// restore unique dialogues to creatures
COPY_EXISTING ~ftobe7.cre~ ~override~
              ~mtob4.cre~  ~override~ // for ub: currently unused, should be in a room at the splurgin' sturgeon
              ~mtob5.cre~  ~override~ // for ub: currently unused, should be at elfsong
              ~read4.cre~  ~override~
  WRITE_ASCIIE 0x2cc ~%SOURCE_RES%~ #8 // dialogue

// tbd, cam
// ordulian should give his cloak, or not have it. We choose that he doesn't have it
COPY_EXISTING ~orduli.cre~ ~override~
  LPF DELETE_CRE_ITEM STR_VAR item_to_delete = clck06 END
  BUT_ONLY

// should not be able to pickpocket the spider body from the spider
COPY_EXISTING ~spidland.cre~ ~override~
  LPF cd_no_pickpocket STR_VAR item = misc60 END

// BGCS-483, luke
// CRE files should not use non-existing resources
WITH_SCOPE BEGIN
  COPY_EXISTING ~bdmcarri.cre~ ~override~ // Mutated Crawler – currently equipped with the non-existing "bdmcarri.itm"
    REPLACE_CRE_ITEM ~carrio1~ #0 #0 #0 ~unstealable&undroppable~ ~weapon~ EQUIP // the other mutated crawler file "bdcrawmu.cre" uses "carrio1", so we'll put "carrio1" here as well...
  BUT_ONLY IF_EXISTS // sod
END

WITH_SCOPE BEGIN
  // ~mepmag01.cre~ (Magma Mephit) – currently equipped with the non-existing "mepmag.itm"
  // Since there's no other Magma Mephit file, let us make "mepmag.itm" from scratch (based on BG2:EE)
  COPY_EXISTING "b1-4.itm" "override/mepmag.itm"
    // Header
    WRITE_LONG NAME1 "-1"
    WRITE_LONG NAME2 "-1"
    WRITE_LONG UNIDENTIFIED_DESC "-1"
    WRITE_LONG DESC "-1"
    // Extended header
    LPF "ALTER_ITEM_HEADER" INT_VAR "dicenumber" = 1 "dicesize" = 2 "damage_type" = 3 END // 1d2 (slashing)
    // Feature block
    LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("DMGTYPE" "FIRE") "timing" = 1 "dicenumber" = 1 "dicesize" = 8 END // 1d8 (fire)
  BUT_ONLY_IF_IT_CHANGES
END

// tbd, graion
// restore SoD ACTION4-ACTION7 lines
INCLUDE ~eefixpack/files/tph/tbd_sod_action4-7_restoration.tph~

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_itm_fixes_bgee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/tbd_bgee_146.tph~ // tbd, cam: fixes items that cast spells
INCLUDE ~eefixpack/files/tph/tbd_energy_blades.tph~ // tbd, cam: per descript, energy blades should do missile damage, not slashing
INCLUDE ~eefixpack/files/tph/usability_arrow_bolt.tph~ // monks using arrows/bolts

ACTION_IF game_includes_sod BEGIN

  // poison resistance blocking dispel
  COPY_EXISTING ~bdbelhi1.itm~ ~override~ // Attack - causes poison
                ~bdbelhi2.itm~ ~override~ // Attack - causes poison
    LPF CLONE_EFFECT INT_VAR match_opcode = 146 opcode = 400 STR_VAR insert = first END
    LPF DELETE_EFFECT INT_VAR match_opcode = 146 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 400 opcode = 146 END
    LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 25 parameter1 = 3 END // weapon for 'stronger' belhifet has weaker poison
 
  // belgifet immue to arcae lightning bolt, but not avenger ot sotrmlord versions
  COPY_EXISTING ~bdbelhia.itm~ ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi308 resource = spdr301 END 
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi308 resource = spcl722 END 
    BUT_ONLY IF_EXISTS 

  // poison resistance blocking *gulp*
  COPY_EXISTING ~bdpotn02.itm~ ~override~ // (itm) Viper's Venom - causes poison
    LPF CLONE_EFFECT INT_VAR match_opcode = 139 opcode = 400 STR_VAR insert = first END
    LPF DELETE_EFFECT INT_VAR match_opcode = 139 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 400 opcode = 139 END

  // immune to hold, but missing spmindat immunity
  COPY_EXISTING ~bdmisc1c.itm~ ~override~ // invulnerable - immune to fear - immune to stun - immune to hold - cures fear
                ~bdplant.itm~  ~override~ // ring - immune to stun - immune to hold
    LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 175 opcode = 296 parameter2 = 0 STR_VAR resource = spmindat END
    BUT_ONLY

  // apply new description image for Helmet of Dumathoin
  COPY_EXISTING ~bdhelm09.itm~ ~override~
    WRITE_ASCII 0x58 ~cbdhel09~ (8)
    BUT_ONLY

END

// tie otyugh slow icon to slow-via-disease effect so it gets cleared by curing disease
COPY_EXISTING ~bddraggh.itm~ ~override~ // (itm) Attack - causes disease level drain
              ~pudden01.itm~ ~override~ // (itm) <Invalid Strref -1> - causes disease
  LPF ALTER_EFFECT INT_VAR match_opcode = 78 match_parameter2 = 10 special = 41 END // change slow-via-disease to use slow icon instead of disease (other disease effect already uses disease icon)
  IF_EXISTS // sod asset(s)

// tie otyugh slow icon to slow-via-disease effect so it gets cleared by curing disease
COPY_EXISTING ~bdotyg01.itm~ ~override~ // (itm) Attack - causes disease - portrait icon 41
              ~bdotyg02.itm~ ~override~ // (itm) Attack - causes disease - portrait icon 41
              ~otyugh.itm~   ~override~ // (itm) Attack - causes disease - portrait icon 41
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 41 END // delete separate slow icon (not present on bddraggh and pudden01)
  LPF ALTER_EFFECT INT_VAR match_opcode = 78 match_parameter2 = 10 special = 41 END // change slow-via-disease to use slow icon instead of disease (other disease effect already uses disease icon)
  IF_EXISTS // sod asset(s)

// fixing waterdeep books (fix also used for bg2ee)
INCLUDE ~eefixpack/files/tph/waterdeep_books.tph~

// elven charm immunity can block level drain and other effects
OUTER_SET charm_immunity = IDS_OF_SYMBOL (~splstate~ ~CHARM_IMMUNITY~)
COPY_EXISTING ~gorwom1.itm~  ~override~ // Nalmissra's attack
  LPF DELETE_EFFECT INT_VAR match_opcode = 324 END // delete existing 324s
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 probability1 = 50 END // charm is 50%, but icon is 100%
  PATCH_FOR_EACH op IN 5 215 142 BEGIN
    LPF CLONE_EFFECT  INT_VAR match_opcode = op match_probability1 = 50 STR_VAR insert = last END
    LPF DELETE_EFFECT INT_VAR match_opcode = op match_probability1 = 50 multi_match = 1 END
  END
  LPF CLONE_EFFECT INT_VAR match_opcode = 5 opcode = 324 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 savebonus = 0 parameter1 = charm_immunity parameter2 = 110 END // charm immunity
  LPF CLONE_EFFECT INT_VAR match_opcode = 5 opcode = 324 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 savebonus = 0 parameter1 = 0 parameter2 = 19 probability1 = 15 END // half-elf immunity
  LPF CLONE_EFFECT INT_VAR match_opcode = 5 opcode = 324 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 savebonus = 0 parameter1 = 0 parameter2 = 15 probability1 = 45 END // elf immunity

COPY_EXISTING ~helm14.itm~ ~override~ // don't leave a bunch of op23's laying around on the creature
  LPF ALTER_EFFECT INT_VAR match_opcode = 23 special = 0 END 

// displaying null string instead of 'devour brain'; many other parameters also wrong
COPY_EXISTING ~mindflay.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 target = 2 timing = 1 duration = 0 parameter1 = RESOLVE_STR_REF(@102) parameter2 = 0 END

// move planetar dispel-on-hit to after vorpal effect so that death ward can work better (in bgee, bg2ee, iwdee.tph)
COPY_EXISTING ~planetar.itm~ ~override~
  LPF CLONE_EFFECT  INT_VAR match_opcode = 58 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 58 multi_match = 1 END

// clone immunity to webtrav (web) projectile into web1p (single-target web) projectile immunity (in bgee, bg2ee, iwdee.tph)
COPY_EXISTING ~plyspid.itm~  ~override~ // arcane polymorph spider
              ~plyspid2.itm~ ~override~ // avenger sword spider
  LPF CLONE_EFFECT INT_VAR match_opcode = 83 match_parameter2 = 319 parameter2 = 259 END // fortunately projectiles are all same value sin bg/bg2/iwd

// tbd, cam
// elixir of health should cure intoxication, not set it to zero
COPY_EXISTING ~potn17.itm~ ~override~ // Elixir of Health - set drunkenness to 0, just change to 'cure drunk' instead
  LPF ALTER_EFFECT INT_VAR match_opcode = 94 opcode = 164 parameter2 = 0 END // flesh out effects below

// potion of perception should provide bonus to move silently
COPY_EXISTING ~potn39.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 275 opcode = 59 STR_VAR insert = below END

// poison resistance blocking con penalty, choking noises
COPY_EXISTING ~potn48.itm~ ~override~ // Vial of Mysterious Liquid - causes poison
  LPF CLONE_EFFECT INT_VAR match_opcode = 324 STR_VAR insert = last END
  LPF CLONE_EFFECT INT_VAR match_opcode = 25 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR multi_match = 2 match_opcode = 324 END
  LPF DELETE_EFFECT INT_VAR multi_match = 1 match_opcode = 25 END

COPY_EXISTING ~potn99.itm~ ~override~ // blinding powder lacks description, minor fixes
  SAY 0x50 @107 
  SAY 0x54 @107 
  LPF ALTER_EFFECT INT_VAR power = 0 END // flesh out effects below
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 timing = 1 resist_dispel = 1 END // flesh out effects below
  IF_EXISTS // sod item

// immune to confusion, but missing spconfus immunity
COPY_EXISTING ~princess.itm~ ~override~ // (placeholder) story ring - immune to fear - immune to stun - immune to feeblemind - immune to confusion - immune to hold - blocks spmindat - blocks cdhorror
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 128 opcode = 296 parameter2 = 0 STR_VAR resource = spconfus END
  BUT_ONLY IF_EXISTS

// spell and scroll use different strings for name, descript; standardize them
COPY_EXISTING ~scrl5d.itm~ ~override~ // protection from evil 10' scroll
  WRITE_LONG 0x0c 25824
  WRITE_LONG 0x54 25825
  BUT_ONLY

//tbd, cam
// unused, but shield of devotion adding two level 1-2 spells instead of one level 1-3 spells
COPY_EXISTING ~shld09p.itm~ ~override~ // hell hound flame
  LPF ALTER_EFFECT INT_VAR match_opcode = 62 match_parameter2 = (BIT0 + BIT1) parameter2 = BIT2 END
  BUT_ONLY

// the feeblemind effect already sets INT=3, so purging redundant effects
COPY_EXISTING ~sirine.itm~   ~override~ // Ghoul hand - causes feeblemind - unknown long effect - plays sound EFF_E05 - portrait icon 48
  LPF DELETE_EFFECT INT_VAR match_opcode = 19 END

INCLUDE ~eefixpack/files/tph/burning_earth.tph~ // fix burning earth bonuses

// viper's edge poison immunity block poorly placed
COPY_EXISTING ~sw1p01.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 318 probability1 = 23 probability2 = 0 STR_VAR insert = last END
  LPF CLONE_EFFECT INT_VAR match_opcode = 25 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR multi_match = 2 match_opcode = 318 END
  LPF DELETE_EFFECT INT_VAR multi_match = 2 match_opcode = 25 END

// remove undocumented +2 save bonus from wand of fear
INCLUDE ~eefixpack/files/tph/wand02.tph~

// tbd, cam
// special wand of magic missiles shouldn't bypass shield
COPY_EXISTING ~wand12.itm~ ~override~ // wand of missiles
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 324 target = 2 parameter1 = 126 parameter2 = 110 timing = 0 resist_dispel = 2 insert_point = 0 STR_VAR resource = wand12 END // block with shield spell
  LPF ADD_ITEM_EFFECT INT_VAR opcode =  61 target = 2 parameter1 = ((0 << 24) + (30 << 16) + (120 << 8)) parameter2 = (25 << 16) timing = 1 resist_dispel = 1 END // rgb fade
  BUT_ONLY


// luke
// Mis-indexed effects
WITH_SCOPE BEGIN
  COPY_EXISTING ~ption41.itm~ ~override~ // Potion of Power
    LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Paralytic Bolt +1**
// - `Save vs. Paralysis` negates (was `Save vs. Spell`)
// - `Weight` should be `0` (was `1`)
// - Hit target is stunned for `5 rounds` (was `6 rounds`)
// - `resist_dispel` should be `0` (effects should not be dispellable and should ignore MR)
WITH_SCOPE BEGIN
  COPY_EXISTING "sahbolt.itm" "override"
    /* Header */
    WRITE_LONG 0x4C 0
    /* Feature blocks */
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~match_opcode~ = 45 ~duration~ = 30 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~savingthrow~ = BIT2 ~resist_dispel~ = 0 END
  BUT_ONLY IF_EXISTS // sod
END

// luke
// – Neera's Staff +1 => When the staff strikes a target, there is a 10% chance that either the target or the wielder will take 1 point of fire damage
//// – In particular, on 10% of all hits there's an extra point of fire damage, so Neera gets hit 5% of the time and her target 5%
//// – This isn't implemented properly since the two op12 effects share the same probability range... Also, while we're at it, make sure it's 10% chance and not 11% chance...
WITH_SCOPE BEGIN
  COPY_EXISTING ~stafn1.itm~ ~override~ // Neera's Staff +1
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~multi_match~ = 1 ~match_opcode~ = 12 ~probability1~ = 4 END // 5% chance (target)
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~match_opcode~ = 12 ~match_probability1~ = 10 ~probability2~ = 5 ~probability1~ = 9 END // 10% chance (wielder)
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Chill Touch – better feedback for the player when attacking Golems or Undead
WITH_SCOPE BEGIN
  COPY_EXISTING "chillt.itm" "override"
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 177 END
    LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~insert_point~ = 0 ~type~ = 1 ~target~ = 2 ~opcode~ = 324 ~parameter2~ = 55 STR_VAR ~resource~ = ~%DEST_RES%~ END // Immunity to resource and message
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Shocking Grasp**
- Should not interact with level-based spell protections
- Secondary type should be `COMBINATION`
- Delete expiry sound since the spell can end prematurely
- Make sure `Range: Touch`
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/shocking_grasp.tph"
  LAUNCH_ACTION_FUNCTION "WIZARD_SHOCKING_GRASP" END
END

/*
luke
**Black Blade of Disaster**
- Should not interact with level-based spell protections
- Make sure *"The caster is considered to be proficient to the point of Grand Mastery in this weapon"* always works
  - `op233` needs to be on the normal effects list to be evaluated after chargen proficiency effects and override them!
- Provide a workaround for the following bugs:
  - if you Dual-class when the Black Blade is equipped, the character permanently gains grand mastery in long swords
  - if, for example, you have a warrior with an item that grants dagger proficiency while equipped, they can then take dagger specialization at a level-up, remove the item, and keep the specialization permanently
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/black_blade_of_disaster.tph"
    LAUNCH_ACTION_FUNCTION "WIZARD_BLACK_BLADE_OF_DISASTER" END
  END
END

// luke
// **Searing Orb**
// - should not interact with level-based spell protections
// - recode from scratch so as to use `op326` effects instead of messy `op177` effects
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/searing_orb.tph"
  LAUNCH_ACTION_FUNCTION ~CLERIC_SOL_SEARING_ORB~ END
END

/*
luke
**Magical Weapon Slot**
(Based on the previous tweak to the Black Blade of Disaster)
- Make sure natural creature weapons / touch attacks do not benefit from Fighting Styles
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/magical_weapon_slot.tph"
    LAUNCH_ACTION_FUNCTION "MAGICAL_WEAPON_SLOT" END
  END
END

/*
+--------------------------------------------------------------------+
| [Luke]                                                             |
+--------------------------------------------------------------------+
| - Rancor +1 (Dorn)                                                 |
|   - in this case, we need a two-stage subspell (so as to make sure |
|     the wielder is indeed Dorn)                                    |
| - Crimson Dawn +2                                                  |
| - Dervish Crescent +2                                              |
+--------------------------------------------------------------------+
| It is the *sword* that should make the kill, not something else    |
+--------------------------------------------------------------------+
*/

WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "gt_fixpack_array"
  ACTION_DEFINE_ASSOCIATIVE_ARRAY "gt_fixpack_array" BEGIN
    "sw2hd1" => "ohdsw1" // Rancor +1 (Dorn)
    "bdsw1h01" => "bdcrimsn" // Crimson Dawn +2 (sod)
    "bdsw1h08" => "bdsw1h08" // Dervish Crescent +2 (sod)
  END
  ACTION_PHP_EACH "gt_fixpack_array" AS "itm" => "spl" BEGIN
    COPY_EXISTING "%itm%.itm" "override"
      PATCH_MATCH "%itm%" WITH
        "sw2hd1" BEGIN
          INNER_ACTION BEGIN
            CREATE ~spl~ ~ohdsw4~
              /* Header */
              WRITE_LONG NAME1 "-1"
              WRITE_LONG NAME2 ~-1~
              WRITE_LONG UNIDENTIFIED_DESC "-1"
              WRITE_LONG DESC ~-1~
              WRITE_LONG 0x18 (BIT14 BOR BIT25)
              WRITE_LONG 0x34 1 // Level
              WRITE_LONG 0x64 0x72 // Extended Header offset
              WRITE_SHORT 0x68 1 // Extended Header count
              WRITE_LONG 0x6a 0x9a // Feature Block Table offset
              INSERT_BYTES 0x72 0x28
              /* Extended Header */
              WRITE_SHORT 0x80 32767 // Ability range
              WRITE_SHORT 0x82 1 // Minimum level
              WRITE_SHORT 0x98 IDS_OF_SYMBOL ("MISSILE" "None") // Projectile
              /* Feature blocks */
              LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 146 "target" = 9 "parameter2" = 1 STR_VAR "resource" = "%spl%" END // cast spell
          END
          LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~type~ = 1 "opcode" = 326 "target" = 2 "parameter2" = 138 "parameter1" = IDS_OF_SYMBOL ("state" "STATE_DEAD") STR_VAR "resource" = "ohdsw4" END // apply effects list
        END
        DEFAULT
          LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~type~ = 1 "opcode" = 326 "target" = 2 "parameter2" = 138 "parameter1" = IDS_OF_SYMBOL ("state" "STATE_DEAD") STR_VAR "resource" = "%spl%" END // apply effects list
      END
      LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "check_headers" = 0 "match_opcode" = 232 STR_VAR "match_resource" = "%spl%" END // no longer needed
    BUT_ONLY_IF_IT_CHANGES IF_EXISTS
  END
END

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_spl_fixes_bgee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/bg_bg2_common_spell_fixes.tph~     // spell fixes in common between bg, bg2
INCLUDE ~eefixpack/files/tph/bg_bg2_iwd_common_spell_fixes.tph~ // spell fixes in common between bg, bg2, and iwd

INCLUDE ~eefixpack/files/tph/tbd_324_traps_bgee.tph~ // tbd, cam - game can crash if a spell-via-trap uses 318/324 vs. a spell without a name

// tbd, cam
// misindexed effects (only wrong with SoD, but harmless to run on BGEE)
COPY_EXISTING ~spin558.spl~   ~override~
  LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY

// expiry sound has wrong duration
COPY_EXISTING ~bdsleep.spl~  ~override~ // <Invalid Strref -1> - causes sleep - plays sound EFF_E05
  LPF ALTER_EFFECT INT_VAR match_duration = 30 duration = 300 END
  IF_EXISTS // sod asset

// tbd, cam
// fear portrait icon expires too early
COPY_EXISTING ~bdfear.spl~  ~override~ // Panic - causes fear - unknown long effect - portrait icon 36
  LPF ALTER_EFFECT INT_VAR match_duration = 12 duration = 30 END
  IF_EXISTS // sod asset

// tbd, cam
// combat feedback used with instant/limited timing and durations
COPY_EXISTING ~bdshriek.spl~ ~override~ // Piercing Shriek - causes confusion deafness stun
              ~spin878.spl~  ~override~ // Level Drain - causes blind - causes level drain - unknown long effect
              ~spin893.spl~  ~override~ // Shadow Dragon Breath - causes blind - causes level drain - unknown long effect
              ~spin929.spl~  ~override~ // Mist Ball - causes blind - unknown long effect
              ~spin931.spl~  ~override~ // Sooty Ball - causes blind - unknown long effect
              ~spwi714.spl~  ~override~ // Prismatic Spray - causes blind - causes feeblemind - unknown long effect
              ~spwm178.spl~  ~override~ // Blindness - causes blind - unknown long effect - plays sound EFF_E07 [expiry sound]
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 timing = 1 duration = 0 END
  IF_EXISTS // bdshriek is sod asset

// change survivable shoal kiss to use magic damage, not acid
COPY_EXISTING ~shoal.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 opcode = 55 parameter1 = 0 parameter2 = BIT2 END 
  LPF CLONE_EFFECT INT_VAR match_opcode = 55 opcode = 324 parameter1 = 3 parameter2 = 113 STR_VAR resource = shoal insert = below END

// change survivable shoal kiss to use magic damage, not acid
COPY_EXISTING ~shoal1.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 parameter2 = (1 + (64 << 16)) END 

// tbd, cam (from jmerry)
// misordered 321 removes several (cosmetic) protections from barbarian rage
COPY_EXISTING ~spcl152.spl~ ~override~ // Barbarian rage
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR insert=first match_resource=SPCL152 resource=SPCL102 END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 STR_VAR match_resource=SPCL152  END
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR match_resource=SPCL102 resource=SPCL152 END
  BUT_ONLY // Effect removing previous instances of rage needs to be first in stack

// has permanent-timed effect with duration
COPY_EXISTING ~spin672.spl~              ~override~ // <Invalid Strref -1> - causes invisibility - plays sound EFF_M51
              ~spin878.spl~              ~override~
              ~spin927.spl~              ~override~ // (spl) Boiling Rain Storm - causes stun - plays sound EFF_P27
              ~%WIZARD_FLAME_ARROW%.spl~ ~override~ // spwi303
              ~%FORCE_DAMAGE_1%.spl~     ~override~ // spwi888
              ~%CLERIC_BARKSKIN%.spl~    ~override~ // sppr202
  LPF ALTER_EFFECT INT_VAR match_timing = 1 duration = 0 END

//tbd, cam
// wrong projectiles for mephit flame fan and hell hound breath https://www.gibberlings3.net/forums/topic/36105-a-projectile-issue/
COPY_EXISTING ~spin938.spl~ ~override~ // mephit flame fan
              ~spin956.spl~ ~override~ // hell hound flame
  LPF ALTER_HEADER INT_VAR projectile = 361 END // burnhand.pro
  BUT_ONLY

// luke
// Mis-indexed effects
WITH_SCOPE BEGIN
  COPY_EXISTING ~%ERINYES_CHARM%.spl~ ~override~ // Charm Person spin558
    LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY_IF_IT_CHANGES
END


// luke
// Night Club +1 => THAC0: +2 at night (implemented via op232)
// 1) Remember that "blun38.spl" will be cast immediately whenever you load, and twice as often with haste, so you cannot avoid an overlapping duration without applying a leading op321 effect.
// 2) Similarly, remember that Slow will reduce the trigger of op232 to 200 ticks!
// As a result, we will double the duration of op54. I.e., We will not allow Slow and Haste to impact it (really, it does not make sense in this case)
// Additionally, sectype should be 0|NONE
WITH_SCOPE BEGIN
  COPY_EXISTING ~blun38.spl~ ~override~
    // Header
    WRITE_BYTE 0x27 0 // Secondary type: NONE
    // Feature blocks
    LPF "ADD_SPELL_EFFECT" INT_VAR "insert_point" = 0 "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 54 "duration" = 13 END // Base THAC0 bonus => Some random duration value strictly greater than 200 ticks...
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Hamatula, Barbed Defense – Suppress misleading "One of the spell has failed" message
// 1) Set subspell's range to a very high value (max signed value)
// 2) Set subspell's projectile to either "RANGE05", "RANGE07" or "RANGE10". The last one is the most appropriate IMHO (given ABISHAI_BLUE selection circle)...
// NOTE: the combat log will still display string "Hamatula : Barbed Defense <TARGET>" when hit out of range!
//// as far as I know, there's no way to suppress it only when hit out of range...
WITH_SCOPE BEGIN
  COPY_EXISTING "bdbarbde.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("MISSILE" "OneTarget_Range_10") END
  BUT_ONLY IF_EXISTS // sod
END

// luke
// Shield of Barnassus: The Suncatcher – Suppress misleading "One of the spell has failed" message
// 1) Set subspell's range to a very high value (max signed value)
// 2) Set subspell's projectile to either "RANGE05", "RANGE07" or "RANGE10". The second one is the most appropriate IMHO (given it's a player-usable item + all player animations have the same selection circle)...
WITH_SCOPE BEGIN
  COPY_EXISTING "bdshld02.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("MISSILE" "OneTarget_Range_07") END
  BUT_ONLY IF_EXISTS // sod
END

// luke
// Chaos Shield – remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_CHAOS_SHIELD%.spl" "override" // spwi222
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "CHAOS_SHIELD")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "CHAOS_SHIELD") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Cloudkill
// Remove the "Bypass Mirror Image" flag from op55 / op215 / op174 (it's only relevant for opcode #12)
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_CLOUDKILL%.spl" "override" // spwi502
                "%TRAP_CLOUDKILL%.spl" "override" // spwi016
                "%SPORE_DEATH%.spl" "override" // spin673
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
        GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
        PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
          PATCH_MATCH SHORT_AT ("%fx_off%" + 0x0) WITH
            "12" BEGIN END
            DEFAULT
              WRITE_LONG ("%fx_off%" + 0x24) (THIS BAND BNOT BIT24)
          END
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Improved Chaos Shield – remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_IMPROVED_CHAOS_SHIELD%.spl" "override" // spwi723
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "IMPROVED_CHAOS_SHIELD")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "IMPROVED_CHAOS_SHIELD") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Energy Drain – provide better feedback for the player
// cam note: could be in joint bg/bg2 fixes except that it needs  different string
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_ENERGY_DRAIN%.spl" "override" // spwi914
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ INT_VAR ~match_opcode~ = 216 ~opcode~ = 139 ~parameter1~ = 25803 STR_VAR ~insert~ = ~below~ END // Display string
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Creeping Doom**
// - remove duplicate `op233` effect(s)
// - *"For each round the victim remains inside the cloud, <PRO_HESHE> must make a Save vs. Spell at -2 or run away in fear for one round."* => match `BG2EE` implementation
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/creeping_doom.tph~ // sppr717, sppr717a
  LAF ~CLERIC_CREEPING_DOOM~ END
END

// luke
// **Creeping Doom** (Black Dragon)
// - use same projectile as `"%CLERIC_CREEPING_DOOM%"`
WITH_SCOPE BEGIN
  COPY_EXISTING "%BLACK_DRAGON_INSECT%.spl" "override" // spin689
    LPF "ALTER_SPELL_HEADER" INT_VAR "projectile" = IDS_OF_SYMBOL ("MISSILE" "Chain_Insect") END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Absorb Health** (Blackguard)
// - should not bypass Magic Resistance
// - subspell should scale up to level `40` (`BG2` level cap)
// - subspell's Primary/Secondary type should match parent `SPL` file
// - subspell should not play a casting sound
WITH_SCOPE BEGIN
  WITH_SCOPE BEGIN
    COPY_EXISTING "%BLACKGUARD_ABSORB_HEALTH%.spl" "override" // spcl102
      LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 146 "resist_dispel" = BIT0 END
    BUT_ONLY_IF_IT_CHANGES
  END
  WITH_SCOPE BEGIN
    COPY_EXISTING "spdn01a.spl" "override"
      WRITE_ASCII 0x10 "" #8 // Casting sound
      WRITE_BYTE 0x25 7 // Primary type: NECROMANCER
      WRITE_BYTE 0x27 10 // Secondary type: OFFENSIVEDAMAGE
      /* Feature blocks */
      LPF "CD_EXTEND-O-MATIC" INT_VAR "level_cap" = 40 END
      PATCH_WITH_SCOPE BEGIN
        GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
        PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
          GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
          PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
            PATCH_MATCH (SHORT_AT "%fx_off%") WITH
              "12" BEGIN
                WRITE_LONG ("%fx_off%" + 0x4) (SHORT_AT ("%ab_off%" + 0x10) * 2) // 2 points of damage per level
              END
              DEFAULT
            END
          END
        END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END

/*
luke
Summon Nishruu / Hakeashar
- their attack (`nishrusu.itm`) should ignore all `AC` modifiers
  - previously, since it was using `damage_type=piercing`, the Nishruu (`THAC0=11`) would need to roll a Critical Hit in order to hit `AC -9 vs. piercing` attacks targets. A similar argument holds for the Hakeashar
    - this is certainly unintended since they don't actually deal piercing damage
- they should be vulnerable to Dispel/Remove Magic
- they should be flagged as `GENERAL=MONSTER` / `RACE=MIST` / `CLASS=MIST` on all games
- make sure the Nishruu has 100% Poison resistance
- The Hakeashar should have the same passive traits as the Nishruu
  - i.e., it should be immune to Poison and see invisible creatures
- the Nishruu should use the `NISHRUU` animation on all games
- the Hakeashar should use the `HAKEASHAR` animation on all games
- removed unused spell setup
*/
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/nishruu_hakeashar.tph~
  LAF ~NISHRUU_HAKEASHAR~ END
END

/*
+-----------------------------------------------------------------+
| [Luke]                                                          |
+-----------------------------------------------------------------+
| - Crimson Dawn +2                                               |
| - Dervish Crescent +2                                           |
+-----------------------------------------------------------------+
| It is the *sword* that should make the kill, not something else |
+-----------------------------------------------------------------+
*/

WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "gt_fixpack_array"
  ACTION_DEFINE_ASSOCIATIVE_ARRAY "gt_fixpack_array" BEGIN
    "bdsw1h01" => "bdcrimsn" // Crimson Dawn +2 (sod)
    "bdsw1h08" => "bdsw1h08" // Dervish Crescent +2 (sod)
  END
  ACTION_PHP_EACH "gt_fixpack_array" AS "itm" => "spl" BEGIN
    COPY_EXISTING "%spl%.spl" "override"
      LPF "ALTER_EFFECT" INT_VAR ~check_globals~ = 0 ~match_target~ = 1 "target" = 9 END // self => original caster
    BUT_ONLY_IF_IT_CHANGES IF_EXISTS
  END
END

/////                                                  \\\\\
///// projectile fixes                                 \\\\\
/////                                                  \\\\\

// tbd, cam
// myconid cloud looks terrible w/o blending: https://www.gibberlings3.net/forums/topic/34875-bug-report-ee-content-bugs/page/7/#comment-314026
COPY_EXISTING ~bdstink.pro~ ~override~
  WRITE_LONG 0x240 (THIS BOR BIT13) // add blend flag
  IF_EXISTS // sod asset

// grease projectiles created in spell section since vars needed 

// luke
// Ice Storm should last 4 rounds (not 3)
WITH_SCOPE BEGIN
  COPY_EXISTING ~icestorm.pro~ ~override~
    WRITE_BYTE 0x216 4 // # repetitions
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Insect Plague**
// - make sure it affects up to 6 creatures (as per spell description)
WITH_SCOPE BEGIN
  COPY_EXISTING ~insec2.pro~ ~override~
    WRITE_SHORT 0x200 (THIS BOR BIT15) // Single target
    WRITE_SHORT 0x244 6 // # dice for multiple targets
    WRITE_SHORT 0x246 1 // Dice size for multiple targets
  BUT_ONLY_IF_IT_CHANGES
END

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

// tbd, cam (from jmerry)
// Correct number of charges on the Sandthief's Ring at tier 3
COPY_EXISTING ~bpxith03.sto~ ~override~
  LPF REPLACE_STORE_ITEM INT_VAR number_in_stock = 1 charges1 = 8 charges2 = 0 charges3 = 0 STR_VAR old_item = ring05 new_item = ring05 END
  BUT_ONLY

// tbd, cam
// Frair Tajik is only merchant that takes helmets but not hats (peacock hat)
COPY_EXISTING ~bdfrair.sto~ ~override~
  READ_LONG 0x2c buy_off   ELSE 0
  READ_LONG 0x30 buy_num   ELSE 0
  INSERT_BYTES buy_off 0x04
  WRITE_LONG buy_off 72
  WRITE_LONG 0x30 (buy_num + 1)
  PATCH_FOR_EACH offset IN 0x34 0x4c 0x70 BEGIN // adjust offsets if needed, thankfully the same in v9, v1, and v1.1
    READ_LONG offset off
    PATCH_IF off > buy_off BEGIN
      WRITE_LONG offset (off + 0x04)
    END
  END
  BUT_ONLY IF_EXISTS // sod asset

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\

// illusionary creatures shouldn't save when dispelled
INCLUDE ~eefixpack/files/tph/illusionary_die.tph~

// corrupted wed fixes
COPY_EXISTING ~ar1000.wed~ ~override~
              ~oh2010.wed~ ~override~
  SET overlay_delta = 3 // oh2010
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "ar1000" = 0) BEGIN SET overlay_delta = 1 END // ar1000
  READ_LONG  0x08 overlay_num
  READ_LONG  0x10 overlay_off
  READ_LONG  0x14 2hdr_off
  SET del1_length = (overlay_delta * 0x18)
  SET del1_offset = (2hdr_off - del1_length)
  READ_LONG (overlay_off + 0x14 + ((overlay_num - 1) * 0x18)) del2_offset
  SET del2_length = (overlay_delta * 0x0c)
  DELETE_BYTES del2_offset del2_length // delete unused bytes
  DELETE_BYTES del1_offset del1_length // delete unused overlay
  SET overlay_num -= overlay_delta
  WRITE_LONG  0x08 overlay_num // update overlay count
  PATCH_FOR_EACH offset IN 0x10 0x14 0x18 0x1c BEGIN
    SET adj = 0
    READ_LONG offset val
    PATCH_IF val > del1_offset BEGIN SET adj += del1_length END 
    PATCH_IF val > del2_offset BEGIN SET adj += del2_length END 
    WRITE_LONG offset (val - adj)
  END 
  READ_LONG  0x14 2hdr_off // re-rad since it hss changed
  PATCH_FOR_EACH offset IN 0x04 0x08 0x0c 0x10 BEGIN // second header offsets  
    SET adj = 0
    READ_LONG (offset + 2hdr_off) val
    PATCH_IF val > del1_offset BEGIN SET adj += del1_length END 
    PATCH_IF val > del2_offset BEGIN SET adj += del2_length END 
    WRITE_LONG (offset + 2hdr_off) (val - adj)
  END 
  FOR (index = 0 ; index < overlay_num ; ++index) BEGIN
    PATCH_FOR_EACH offset IN 0x10 0x14 BEGIN 
      SET adj = 0
      READ_LONG (offset + overlay_off + (index * 0x18)) val
      PATCH_IF val > del1_offset BEGIN SET adj += del1_length END 
      PATCH_IF val > del2_offset BEGIN SET adj += del2_length END 
      WRITE_LONG (offset + overlay_off + (index * 0x18)) (val - adj)
    END
  END   
  READ_LONG 0x0c door_num
  READ_LONG 0x18 door_off
  FOR (index = 0 ; index < door_num ; ++index) BEGIN
    PATCH_FOR_EACH offset IN 0x12 0x16 BEGIN 
      SET adj = 0
      READ_LONG (offset + door_off + (index * 0x1a)) val
      PATCH_IF val > del1_offset BEGIN SET adj += del1_length END 
      PATCH_IF val > del2_offset BEGIN SET adj += del2_length END 
      WRITE_LONG (offset + door_off + (index * 0x1a)) (val - adj)
    END
  END   
  BUT_ONLY  

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/scripting_states.tph~ // cleans up scripting states

// prevent spell called via op232 from interrupting caster sounds
INCLUDE ~eefixpack/files/tph/noisy_fireshields.tph~

INCLUDE ~eefixpack/files/tph/free_action.tph~ // free action swap from 126 > 337

INCLUDE ~eefixpack/files/tph/tbd_bgee_probabilities.tph~ // tbd, cam: probability fixes
INCLUDE "%MOD_FOLDER%/files/tph/dw_fixes.tph" // tbd, davidw:  318/324 immunity, 109/175/185 disentanglement, and more

WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/redundant_effects.tph"
  LAF "remove_redundant_effects" END
END

INCLUDE ~eefixpack/files/lib/cd_effect_batches_functions.tpa~         // function for effect batches
INCLUDE ~eefixpack/files/lib/cd_effect_batches_arrays_bg_bg2_iwd.tpa~ // array definitions for effect batches
INCLUDE ~eefixpack/files/tph/tbd_vfx_removal_bg.tph~                  // use effect batches to remove vfx from effects which have been removed

