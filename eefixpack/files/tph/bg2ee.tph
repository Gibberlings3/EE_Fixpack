/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~ BEGIN
  INCLUDE ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~
END

INCLUDE ~eefixpack/files/tph/launcher_damage_types.tph~ // many bows/xbows lack damage type in their description

/////                                                  \\\\\
///// lua fixes                                        \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/debug_area_listings.tph~ // correcting area names/styles in console menu

/////                                                  \\\\\
///// ids/2da/ini fixes                                \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/a7/action_changestoremarkup_fix.tph~ // fix parameter order in the ChangeStoreMarkup() script action
INCLUDE ~eefixpack/files/tph/a7/anim_3001.tph~ // fix missing creature animation definitions for Neothelid
INCLUDE ~eefixpack/files/tph/a7/cre_anim_fixes.tph~ // fix height offsets of health bar positions for creature animations
INCLUDE ~eefixpack/files/tph/animation_ini_fixes.tph~ // errors in animation definitions
INCLUDE ~eefixpack/files/tph/tbd_splprot_kitfix.tph~ // tbd, cam: fixing kit check to be a numerical equality check, not bitwise
INCLUDE ~eefixpack/files/tph/portrait_2da.tph~ // add defaults for portraits not listed in portrait.2da
INCLUDE ~eefixpack/files/tph/infravision.tph~ // correcting animations subject to infravision
INCLUDE ~eefixpack/files/tph/ids_sync.tph~ // sync race.ids, class.ids

// syncing damages.ids header between games (cosmetic)
COPY_EXISTING ~damages.ids~ ~override~
  REPLACE_TEXTUALLY ~^\(0x0001\)~ ~13%LNL%\1~ // bg2ee/iwdee
  
APPEND ~spell.ids~ ~3119 SAFANA_CHARM~ UNLESS ~^3119[ %TAB%]+SAFANA_CHARM[ %TAB%%MNL%%LNL%]+~ // syncing spell.ids

/*
luke
**splprot.2da**
- the newly added (v2.6) header descriptions are incorrect for the alignment rows
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "splprot.2da" "override"
    COUNT_2DA_COLS "cols"
    SET_2DA_ENTRY 33 0 "%cols%" "33_ALIGNMENTBITS=GOOD" // NOT BIT1 = Not NEUTRAL or EVIL
    SET_2DA_ENTRY 34 0 "%cols%" "34_ALIGNMENTBITS!=GOOD" // HAS BIT1 = Is NEUTRAL or EVIL
    SET_2DA_ENTRY 35 0 "%cols%" "35_ALIGNMENTBITS=NEUTRAL" // NOT BIT0 = Not GOOD or EVIL
    SET_2DA_ENTRY 36 0 "%cols%" "36_ALIGNMENTBITS!=NEUTRAL" // HAS BIT0 = Is GOOD or EVIL
    SET_2DA_ENTRY 37 0 "%cols%" "37_ALIGNMENTBITS=EVIL"
    SET_2DA_ENTRY 38 0 "%cols%" "38_ALIGNMENTBITS!=EVIL"
    SET_2DA_ENTRY 59 0 "%cols%" "59_ALIGNMENTBITS=LAWFUL" // NOT BIT5 = Not CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 60 0 "%cols%" "60_ALIGNMENTBITS!=LAWFUL" // HAS BIT5 = Is CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 61 0 "%cols%" "61_ALIGNMENTBITS=CHAOTIC"
    SET_2DA_ENTRY 62 0 "%cols%" "62_ALIGNMENTBITS!=CHAOTIC"
    SET_2DA_ENTRY 118 0 "%cols%" "118_ALIGNMENTBITS!=n" // Clearly state it is a bitwise check
    // Formatting
    PRETTY_PRINT_2DA
  BUT_ONLY_IF_IT_CHANGES
END

INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves

//tbd, cam
// battleguards starting in tob lack a weapon due to a typo
COPY_EXISTING ~25stweap.2da~ ~override~
  REPLACE_TEXTUALLY ~[ %TAB%]ham07[ %TAB%]~ ~ hamm07 ~
  PRETTY_PRINT_2DA

// tbd, cam
// sync bgee/bg2ee spell.ids entries with extra sod spells
ACTION_IF ((!FILE_EXISTS_IN_GAME ~sppr116.spl~) OR (!FILE_EXISTS_IN_GAME ~spwi126.spl~) OR(!FILE_EXISTS_IN_GAME ~spwi228.spl~)) BEGIN
  INCLUDE ~eefixpack/files/tph/tbd_missing_spells.tph~
END

APPEND ~tooltip.2da~ ~ohdsw03 15529 13053 -1~ // add tooltips for brass blade

// tbd, graion: more consistent op5 setup, see header
INCLUDE ~eefixpack/files/tph/tbd_ea_reallycharmed.tph~

// ensure that ids tables are properly sorted
COPY_EXISTING_REGEXP ~^.+\.ids$~ ~override~
  LPF sort_ids END
BUT_ONLY

CLEAR_IDS_MAP // reload ids, if we end up having any changes

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

COPY ~eefixpack/files/bam/ixbow04.bam~  ~override~ // tbd, cam: fix light crossbow BAM
     ~eefixpack/files/2da/clabmo02.2da~ ~override~ // tbd, cam: borked kit ability table, dark moon monk
     ~eefixpack/files/2da/clabmo03.2da~ ~override~ // tbd, cam: borked kit ability table, sun soul monk
     ~eefixpack/files/2da/clabrn02.2da~ ~override~ // tbd, cam: borked kit ability table, archer
     ~eefixpack/files/2da/spwi417.2da~  ~override~ // tbd, cam: allow enchant weapon to be cast via contingency
     ~eefixpack/files/bam/mdem~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, demogorgon
     ~eefixpack/files/bam/mdr1~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon red
     ~eefixpack/files/bam/mdr2~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon black
     ~eefixpack/files/bam/mdr3~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon silver
     ~eefixpack/files/bam/mwyv~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, wyvern big - also add shadows
     ~eefixpack/files/eff/balth01b.eff~ ~override~ // tbd, cam: balthazar's solar stance! should have fire-damage melee hit effects that also drain hp
     ~eefixpack/files/bam/cmnk1inv.bam~ ~override~ // monk (tutorial, not pc) paperdoll
     ~eefixpack/files/vvc/spsetsh.vvc~  ~override~ // tbd, cam: fixing unblended fire animation for Hexxat's sunlight damage
     ~eefixpack/files/vvc/spnpoisi.vvc~ ~override~ // tbd, cam: fixing bad blend, placement for tear eye graphics in ar2900
     ~eefixpack/files/vvc/spccmdsi.vvc~ ~override~ // tbd, cam: fixing unblended portal animation for hexxat tomb cutscene in ar0800 (ohhopen)
     ~eefixpack/files/bam/sppr314d.bam~ ~override~ // new unholy blight portrait icon
     ~eefixpack/files/wav/repup.wav~    ~override~ // restoring 'reputation up' sound from bgee
     ~eefixpack/files/bam/mvaf~         ~override~ // sam.: removing shadows from vampires, female
     ~eefixpack/files/bam/mvam~         ~override~ // sam.: removing shadows from vampires, male
     ~eefixpack/files/bam/nboh~         ~override~ // sam.: removing shadows from vampires, bodhi
     ~eefixpack/files/bam/ohbcnosh.bam~ ~override~ // broken noser uniform BAM
     ~eefixpack/files/bam/spwi516a.bam~ ~override~ // fix uncolored BAM for conjure lesser fire elemental scroll
     ~eefixpack/files/bam/spsdbur.bam~  ~override~ // fixed silver dragon breath
     ~eefixpack/files/bam/sppr203c.bam~ ~override~ // fixed chant icon (spellbook)

// fixing shadows on item icons
COPY ~eefixpack/files/bam/item_shadows/bg2ee~           ~override~
     ~eefixpack/files/bam/item_shadows/bg2ee_iwdee~     ~override~
     ~eefixpack/files/bam/item_shadows/bgee_bg2ee~      ~override~
     ~eefixpack/files/bam/item_shadows/sod_bg2ee~       ~override~
     ~eefixpack/files/bam/item_shadows/sod_bg2ee_iwdee~ ~override~
     ~eefixpack/files/bam/item_shadows/universal~       ~override~

INCLUDE ~eefixpack/files/tph/spell_bams.tph~ // updated spell BAMs

// tbd, sam.
// merging multi-part area animations to eliminate issues
ACTION_FOR_EACH file IN
  1400t001 1400t002 1400t003 1400t004 1400t005 am0602c am0602d am0602k1 am0602k2 am0602l1 am0602l2
  am0801b1 am0801b2 am0902l am0902l1 am0902l2 am0902l3 am0902l4 am2000a am2000a1 am2000a2 am2000a3
  am2000a4 am2000b am2000b1 am2000b2 am2000b3 am2000b4 am2805b am2805c am414e11 am414e12
BEGIN
  COPY ~eefixpack/files/bam/area_anim/%file%.bam~ ~override~
END

// installing BAM V2 resources
INCLUDE ~eefixpack/files/tph/install_bam_v2.tph~
ACTION_FOR_EACH bam_resref IN cshld05 BEGIN
  LAF install_bam_v2 STR_VAR bam_resref END
END

// tbd, cam (from jmerry)
// curing poison should also cure insta-death from green slimes
//INCLUDE ~eefixpack/files/tph/tbd_green_slime.tph~ now part of 'cure' fixes

// tbd, cam (from jmerry)
// vernus can't be raised
INCLUDE ~eefixpack/files/tph/tbd_vernus_resurrection.tph~

//tbd, cam
// wilson fixes: strength exploit, AC fixes, regen fixes
INCLUDE ~eefixpack/files/tph/tbd_wilson.tph~

// bgcs-3460, cam
// death tyrant animation fixes
INCLUDE ~eefixpack/files/tph/b3460_death_tyrant.tph~ // bgcs-3460, cam: death tyrant animation fixes

// bgcs-3397, cam
// revenant animation fixes
INCLUDE ~eefixpack/files/tph/b3397_revenant.tph~ // bgcs-3397, cam: revenant animation fixes

// tbd, cam
// make troll animations make sense: https://www.gibberlings3.net/forums/topic/35642-troll-animations/
INCLUDE ~eefixpack/files/tph/tbd_bg2_troll_anims.tph~

// tbd, cam
// potion of icedust doesn't work since it uses an enemy-only projectile for a buff
INCLUDE ~eefixpack/files/tph/tbd_icedust.tph~ // tbd, cam: potion of icedust doesn't work since it uses an enemy-only projectile for a party-only buff

/////                                                  \\\\\
///// mechanics fixes / overhauls                      \\\\\
/////                                                  \\\\\

// kjeron
WITH_SCOPE BEGIN
  WITH_TRA "eefixpack/languages/en_us/luke/polymorph_overhaul.tra" "eefixpack/languages/%LANGUAGE%/luke/polymorph_overhaul.tra" BEGIN
    INCLUDE "eefixpack/files/tph/luke/polymorph_overhaul.tph"
    LAF "POLYMORPH_OVERHAUL" END
  END
END

/*
luke
"7eyes.2da" vs. SPL/ITM files
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/7eyes/7eyes.tph"
  LAUNCH_ACTION_FUNCTION "7EYESification" END
END

/*
luke
**Wing Buffet vs. MR**
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/wing_buffet_vs_mr.tph"
  LAUNCH_ACTION_FUNCTION "WING_BUFFET_VS_MR" END
END

// argent77
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/a7/improved_troll_regeneration.tph"
END

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes
COMPILE ~eefixpack/files/d/temple.d~ // temple changes (see also script changes in temple.tph)

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/temple.tph~ // temple changes for ar0900/1/2/4.bcs (see also dialogue changes in temple.d)
INCLUDE ~eefixpack/files/tph/hexxat_death.tph~ // better death scripting for hexxat

// reduce chance for love talk interruption
COPY_EXISTING ~aeri25.bcs~   ~override~
              ~aerie25d.bcs~ ~override~
              ~aerie.bcs~    ~override~
              ~aeried.bcs~   ~override~
              ~anom25.bcs~   ~override~
              ~anomen.bcs~   ~override~
              ~anomend.bcs~  ~override~
              ~dorn.bcs~     ~override~
              ~dornd.bcs~    ~override~
              ~hexxa25.bcs~  ~override~
              ~hexxat.bcs~   ~override~
              ~hexxatd.bcs~  ~override~
              ~jahe25.bcs~   ~override~
              ~jaheira.bcs~  ~override~
              ~jaheirad.bcs~ ~override~
              ~neer25.bcs~   ~override~
              ~neera.bcs~    ~override~
              ~rasa25.bcs~   ~override~
              ~rasaad.bcs~   ~override~
              ~rasaadd.bcs~  ~override~
              ~vico25.bcs~   ~override~
              ~vicon25d.bcs~ ~override~
              ~vicond.bcs~   ~override~
              ~viconia.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY // should i have done this as one regexp?
      ~\(\(SetGlobal("\(Exp\)?LoveTalk","LOCALS",[0-9]+)[ %TAB%%LNL%%MNL%]+\)?\(IncrementGlobal("\(Exp\)?LoveTalk","LOCALS",1)[ %TAB%%LNL%%MNL%]+\)?\(SetGlobal("talkedYoc","LOCALS",1)[ %TAB%%LNL%%MNL%]+\)?\(RealSetGlobalTimer("[A-Za-z]+Romance","GLOBAL",[0-9A-Z_]+)[ %TAB%%LNL%%MNL%]+\)?\(IncrementGlobal("[A-Za-z]+Lovetalks","GLOBAL",1)[ %TAB%%LNL%%MNL%]+\)?PlaySong([0-9]+)[ %TAB%%LNL%%MNL%]+\(IncrementGlobal("[A-Za-z]+Lovetalks","GLOBAL",1)[ %TAB%%LNL%%MNL%]+\)?\(Interact\|\(Start\)?Dialog\(ue\)?\(NoSet\)?\)(Player1)\)~
      ~SetInterrupt(FALSE) \1 SetInterrupt(TRUE)~ // no, no I should not have.
  END
  BUT_ONLY
  
EXTEND_BOTTOM ~ar0602.bcs~ ~eefixpack/files/baf/ar0602_aataqah.baf~ // offloading dlg actions (see d core fixes)

// tbd, cam
// ranger stronghold fails if you're in Umar Hills when Delon spawn timer expires; see also delon.dlg
COPY_EXISTING ~ar1100.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(NumDeadLT("rogron",6)\)~ ~\1 Global("CDDelonSpoke","GLOBAL",1)~
  END
  BUT_ONLY

// tbd, cam  (from MiH)
// open all shadow doors if shade lord is defeated
COPY_EXISTING ~ar1401.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(TriggerActivation("Tran1404",FALSE)\)~ ~\1 OpenDoor("ShadowDoor01") OpenDoor("ShadowDoor02") OpenDoor("ShadowDoor03")~
  END
  BUT_ONLY

// trying to make the exit statue from spellhold dungeon level 1 more reliable - ppridd.dlg and ar1512.bcs
EXTEND_BOTTOM ~ar1512.bcs~ ~eefixpack/files/baf/ar1512_opendoor.baf~

// tbd, cam (from MiH)
// priest spawns inside slime
COPY_EXISTING ~ar2200.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CreateCreature("dagmag01",\[[0-9]+\.[0-9]+\],[0-9A-Z]+)~ ~CreateCreature("dagmag01",[4401.3527],NW)~
  END
  BUT_ONLY

// wk spawn points - defer spawn point activion to area script (see also ar3001.are)
EXTEND_BOTTOM ~ar3001.bcs~ ~eefixpack/files/baf/ar3001.baf~

// tbd, cam
// siege camp reinforcements checking wrong blocks
COPY_EXISTING ~ar5203.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~GlobalLT("EXTRACOUNT2","MYAREA",1)[ %TAB%%LNL%%MNL%%WNL%]+\(ActionListEmpty()[ %TAB%%LNL%%MNL%%WNL%]+InActiveArea(Myself)[ %TAB%%LNL%%MNL%%WNL%]+THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE #30[ %TAB%%LNL%%MNL%%WNL%]+IncrementGlobal("MaxSpawn","AR5203",1)[ %TAB%%LNL%%MNL%%WNL%]+CreateCreatureObjectOffScreen("YSSOLD04",Player1,0,0,0)[ %TAB%%LNL%%MNL%%WNL%]+IncrementGlobal("EXTRACOUNT3","MYAREA",1)\)~
                      ~GlobalLT("EXTRACOUNT3","MYAREA",1) \1~
  END
  BUT_ONLY

// one area in sendai's enclave lacks the 'drow items safe' bit of scripting
EXTEND_BOTTOM ~ar6104.bcs~ ~eefixpack/files/baf/ar6104_drow.baf~

// holy symbols for not-the-PC shouldn't rely on the PC's choice of temple
COPY_EXISTING ~baldur.bcs~   ~override~
              ~baldur25.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(!Kit(Player[2-6],GODLATHANDER)\)[ %TAB%%LNL%%MNL%%WNL%]+OR(5)[ %TAB%%LNL%%MNL%%WNL%]+GlobalGT("CDWorkingForHelm","GLOBAL",0)~
      ~\1 OR(4)~
    REPLACE_TEXTUALLY ~\(!Kit(Player[2-6],GODHELM)\)[ %TAB%%LNL%%MNL%%WNL%]+OR(3)[ %TAB%%LNL%%MNL%%WNL%]+GlobalGT("CDWorkingForLathander","GLOBAL",0)~
      ~\1 OR(2)~
  END

// shaman summon script errors - fix for bgee, bg2ee, iwdee
COPY_EXISTING ~bdshunsu.bcs~ ~override~ // chaman summun AI script
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!\(Detect(\[PC\.0\.0\.SHAMAN\])[ %TAB%%LNL%%MNL%]+CombatCounter(0)\)~ // have to include extra line to avoid other blocks
    	~\1~ // should be detect, not !detect (sod only)
    REPLACE_TEXTUALLY ~\(MoveToObject(\[PC\.0\.0\.SHAMAN])\)~ ~\1 Continue()~ // don't block item bonuses with infinite Move commands
  END   

// consolidate bernard's store (see also bernard2.sto, bernard.dlg)
EXTEND_BOTTOM ~bernard.bcs~ ~eefixpack/files/baf/bernard.baf~

// prevent ler from initiating dialogue once chapter is advanced
COPY_EXISTING ~bystand1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("CutScene01","AR0700",5)\)~ ~\1 GlobalLT("Chapter","GLOBAL",2)~
  END

// cowled wizard: stop hitting yourself - CWs can end up targeting allies
COPY_EXISTING ~cowenf01.bcs~ ~override~ 
              ~cowenf02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Nearest(\[ENEMY\])~ ~NearestEnemyOf(Myself)~ // catches SecondNearest, ThirdNearest, &c.
    REPLACE_TEXTUALLY ~\[0\.0\.0\.MAGE\]~ ~NearestEnemyOfType([0.0.0.MAGE_ALL])~ // could target fellow CWs
    REPLACE_TEXTUALLY ~StateCheck(\[0\.0\.0\.MAGE\])~ ~StateCheck(LastSeenBy(Myself)~ // could change target to fellwo CWs
    REPLACE_TEXTUALLY ~\[\(0\|ENEMY\)\.0\.0\.0\.0\.SUMMONED\]~ ~[GOODCUTOFF.0.0.0.0.SUMMONED]~ // don't target friendly summons
    REPLACE_TEXTUALLY ~\(See(\[ENEMY\])\)~ ~\1 !InMyGroup(LastSeenBy(Myself))~ // if targeting ENEMY, make sure it's not a fellow CW
    REPLACE_TEXTUALLY ~\(!InMyGroup(LastSeenBy(Myself))\)[ %TAB%%LNL%%MNL%]+!InMyGroup(LastSeenBy(Myself))~ ~\1~ // in case previous R_T doubles this trigger
  END 
  BUT_ONLY

// tbd, cam
// when leaving Brynnlaw with Saemon, the Githyanki catch you "on the fourth morn" but no time actually passes
COPY_EXISTING ~cut41q.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~TextScreen("SCRTXT05")~
      ~Wait(1) DayNight(MIDNIGHT) SmallWait(1) DayNight(MIDNIGHT) SmallWait(1) DayNight(MIDNIGHT) Wait(1) // three days pass: "on the fouth morn"
       Rest() ActionOverride(Player2,Rest()) ActionOverride(Player3,Rest()) ActionOverride(Player4,Rest()) ActionOverride(Player5,Rest()) ActionOverride(Player6,Rest()) // rest party
       DayNight(10) TextScreen("SCRTXT05")~ // delay a few more hours, since the githyanki is spotted "on the fouth morn", but catches you a bit later
  END
  BUT_ONLY

// after defeating Jon for good, Ellesime should initiate dialogue even if player invisible
COPY_EXISTING ~cut59a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~EndCutSceneMode()~ ~Wait(2) ActionOverride("suelle2",StartDialogueNoSet([PC]))~
  END
  BUT_ONLY  

// ToB bonus equipment spilling out of inventory should also be copied to the expansion
COPY_EXISTING ~cut100a.bcs~ ~override~
              ~cut100b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(MoveToExpansion()\)~ ~\1 CopyGroundPilesTo("ar4000",[1116.1805])~
  END
  BUT_ONLY

// bodari's subcontract takes multiple days, have party rested
COPY_EXISTING ~cut244b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ApplySpell(Player1,HEAL_NO_VISUAL)\)~
      ~\1 Rest() ActionOverride(Player2,Rest()) ActionOverride(Player3,Rest()) ActionOverride(Player4,Rest()) ActionOverride(Player5,Rest()) ActionOverride(Player6,Rest())~
  END

// tbd, cam
// variable that tracks if you know Dorn always get set when sorting out journal entries
COPY_EXISTING ~dorn.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("OHD_KNOWS_DORN","LOCALS",1)[ %TAB%%LNL%%MNL%%WNL%]+\(SetGlobal("OHD_JOURNAL_BEGIN","LOCALS",2)\)~ ~\1~
  END
  BUT_ONLY

// tbd, cam
// firkraag should despawn at the copper coronet once you engage in the windpear events
EXTEND_TOP ~firkra01.bcs~ ~eefixpack/files/baf/firkra01.baf~

// tbd, cam
// two CC guards have unbounded scripts blocks that loop
COPY_EXISTING ~guard1a.bcs~ ~override~ // unbounded guard scripts in CC
              ~guard1b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Allegiance(Myself,NEUTRAL)\)~      ~\1 Global("f4imoved","LOCALS",0)~
    REPLACE_TEXTUALLY ~\(MoveToPoint(\[[0-9]+\.[0-9]+\])\)~ ~\1 SetGlobal("f4imoved","LOCALS",1)~
  END
  BUT_ONLY
  
// neera's underdark comment only makes sense if party skipped sahuagin city (hexxat.bcs, neera.bcs, neeraj.dlg)
COPY_EXISTING ~hexxat.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY 
    ~\(Global("OH_UnderdarkComments","GLOBAL",1)[ %TAB%%LNL%%MNL%]+I[fs]ValidForPartyDialog\(ue\)?(Myself)[ %TAB%%LNL%%MNL%]+Delay(2)[ %TAB%%LNL%%MNL%]+!I[fs]ValidForPartyDialog\(ue\)?("dorn")[ %TAB%%LNL%%MNL%]+!I[fs]ValidForPartyDialog\(ue\)?("rasaad")[ %TAB%%LNL%%MNL%]+\)!I[fs]ValidForPartyDialog\(ue\)?("neera")~ 
    ~\1 OR(2) !IfValidForPartyDialog("neera") !Global("SahauginTravel","GLOBAL",0)~
  END  

// kangaxx-as-lich should always talk when he appears
EXTEND_BOTTOM ~hlkang.bcs~ ~eefixpack/files/baf/hlkang.baf~

// tbd, cam
// love talk sets trigger for jaheira's bandits to spawn, but shouldn't occur if romance ends before timer is up
COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("JaheiraBanditPlot","GLOBAL",1)\)~ ~\1 OR(2) Global("JaheiraRomanceActive","GLOBAL",1) Global("JaheiraRomanceActive","GLOBAL",2)~
  END
  BUT_ONLY

// neera's underdark comment only makes sense if party skipped sahuagin city (hexxat.bcs, neera.bcs, neeraj.dlg)
COPY_EXISTING ~neera.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\(Global("OH_UnderdarkComments","GLOBAL",1)\)~ ~\1 Global("SahauginTravel","GLOBAL",0)~
  END  

// tbd, cam
// container script doesn't work since it uses LOCALS; in this case the block should be disabled (reward given via dialogue, item given here is placeholder)
COPY_EXISTING ~ohbpot.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("ohb_makesoup","LOCALS"~ ~Global("ohb_makesoup","MYAREA"~ // fix scope...
    REPLACE_TEXTUALLY ~\(Global("OHB_COOK_MUSHROOM","GLOBAL",3)\)~ ~False() \1~         // and then disable
  END
  BUT_ONLY

// tbd, cam (from k4thos/eet)
// slng03 changes to slng05 from bg > bg2; caught everywhere else except direct import
COPY_EXISTING ~ohimport.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(RESPONSE #100[ %TAB%%LNL%%MNL%%WNL%]+\)\(SetGlobal("OH_IMPORT02","GLOBAL",8)[ %TAB%%LNL%%MNL%%WNL%]+SetInterrupt(TRUE)[ %TAB%%LNL%%MNL%%WNL%]+END\)~
      ~\1 SetInterrupt(FALSE) DestroyItem("SLNG03") CreateItem("SLNG05",0,0,0) \2~
  END
  BUT_ONLY

// ie-5948, cam
// Generic cleric AI script can hang
COPY_EXISTING ~pries14a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(IF[ %TAB%%LNL%%MNL%%WNL%]See(NearestEnemyOf(Myself))[ %TAB%%LNL%%MNL%%WNL%]+HaveSpell(CLERIC_CONFUSION)[ %TAB%%LNL%%MNL%%WNL%]+THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE #100[ %TAB%%LNL%%MNL%%WNL%]\)\(END\)~
      ~\1 Spell(NearestEnemyOf(Myself),CLERIC_CONFUSION) \2~
END
BUT_ONLY


// ie-5281, cam
// bp2: Golem fight is well-nigh impossible on Legacy of Bhaal difficulty
COPY_EXISTING ~ohb_t302.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~LevelGT(Myself,10)~ ~HasItem("immune1",Myself)~
  END
  BUT_ONLY

// tbd, cam (from jmerry)
// area script can loop
COPY_EXISTING ~oh7200.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(AmbientActivate("Hidcont",TRUE)\)~ ~\1 SetGlobal("OHH_wiztrap","OH7200",2)~
  END
  BUT_ONLY

// jmerry: several items for BP2 stores weren't being added due to AddStoreItem quirks
COPY_EXISTING ~ohbhub.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~TriggerActivation("ohb_messhall2",TRUE)~ ~~ // remove reference to non-existent trigger while we're here
    REPLACE_TEXTUALLY ~AddStoreItem("OHBSDWM","STAF12",10,1)~ ~AddStoreItem("OHBSDWM","STAF12",1,1)~
    REPLACE_TEXTUALLY ~AddStoreItem("OHBIMIM","RODS03",5,1)~  ~AddStoreItem("OHBIMIM","RODS03",10,1)~
    REPLACE_TEXTUALLY ~AddStoreItem("OHBIMIM","RODS06",5,1)~  ~AddStoreItem("OHBIMIM","RODS06",10,1)~
    REPLACE_TEXTUALLY ~AddStoreItem("OHBIMIM","AMUL17",10,1)~ ~AddStoreItem("OHBIMIM","AMUL17",50,1)~
    REPLACE_TEXTUALLY ~AddStoreItem("OHBHUEGR","HELM30",1,1)~ ~AddStoreItem("OHBHUEGR","HELM30",3,1)~
    REPLACE_TEXTUALLY ~AddStoreItem("OHBHUEGR","CHAN20",1,1)~ ~AddStoreItem("OHBHUEGR","CHAN20",3,1)~
    REPLACE_TEXTUALLY ~AddStoreItem("OHBIMIM","BOOT12",1,1)~  ~AddStoreItem("OHBIMIM","BOOT12",2,1)~
  END
  BUT_ONLY

// if renfeld gets rendered incapable, can't init dialogue post-mugging
COPY_EXISTING ~renfeld.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\(Dead("rethug01")\)~ ~!StateCheck(Myself,CD_STATE_NOTVALID) \1~
    APPEND_FILE ~eefixpack/files/baf/renfeld_dispel.baf~
  END   

// fear via 'Eyes of the Beholder' item should also work to scare viekang in ToB
COPY_EXISTING ~sarvie01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(12)~ ~OR(13) SpellCastOnMeRES("OHREYEB3",[ANYONE])~
  END
  BUT_ONLY

// tbd, cam (two issues here, see bysohim in main d compile)
// Yoshimo Should Have Follow-Up Banters with the PC
EXTEND_BOTTOM ~yoshimo.bcs~ ~eefixpack/files/baf/yoshimo_banter.baf~ // add script prompt for third banter

// Fixes an exploit that would allow the party to bypass the Oasis when travelling to Amkethran
// via Deepstone Clanhold (Rasaad's quest) or the Clearing (Neera's quest).
// https://www.gibberlings3.net/forums/topic/34875-bug-report-ee-content-bugs/?do=findComment&comment=339326
ACTION_IF (NOT FILE_EXISTS_IN_GAME ~AR6300.BCS~) BEGIN
  COPY - ~.../fp-inline/blank~ ~.../inlined/eefixpack/AR6300.BAF~
  COMPILE ~.../inlined/eefixpack/AR6300.BAF~
END
EXTEND_TOP ~AR6300.BCS~ ~eefixpack/files/baf/ar6300_unlock_ar5500.baf~

INCLUDE ~eefixpack/files/tph/ending_cutscenes.tph~ // tightening end of cutscenes

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_are_vertices_bg2ee.tph~ // tbd, cam (from MiH): fix vertex issues for doors and regions

INCLUDE ~eefixpack/files/tph/assign_area_scripts.tph~ // assign/correct area scripts to areas


// tbd, cam
// actors on impassable terrain: https://www.gibberlings3.net/forums/topic/34875-bug-report-ee-content-bugs/page/9/#comment-315538
LAF cd_move_actor INT_VAR old_x =  398 old_y =  527 new_x =  448 new_y =  537 STR_VAR area = ar0305 END // Shadow Thief (actor #4) at position [398.527] is on impassable terrain
LAF cd_move_actor INT_VAR old_x = 1060 old_y = 3033 new_x = 1060 new_y = 3052 STR_VAR area = ar2000 END // Kveroslava (actor #88) at position [1060.3033] is on impassable terrain
LAF cd_move_actor INT_VAR old_x =  878 old_y =  882 new_x =  916 new_y =  920 STR_VAR area = ar5202 END // Skeleton Cleric (actor #1) at position [878.882] is on impassable terrain
LAF cd_move_actor INT_VAR old_x =  983 old_y =  523 new_x =  988 new_y =  561 STR_VAR area = ar6005 END // Frost Salamander (actor #4) at position [983.523] is on impassable terrain
LAF cd_move_actor INT_VAR old_x = 1021 old_y = 2607 new_x =  996 new_y = 2636 STR_VAR area = oh5500 END // Crusader (actor #6) at position [1021.2607] is on impassable terrain
LAF cd_move_actor INT_VAR old_x = 2552 old_y = 2880 new_x = 2586 new_y = 2916 STR_VAR area = oh5500 END // Celestial Hound (actor #21) at position [2552.2880] is on impassable terrain
// LAF cd_move_actor INT_VAR old_x = 1412 old_y =  888 new_x = 1412 new_y =  888 STR_VAR area = oh7100 END // Yi Niu (actor #21) at position [1412.888] is on impassable terrain (disabled, never activated?)

// tbd, cam (from jmerry)
// npcs spawning/moving on impassable terrain (see also baerie.dlg, aeriej.dlg, ar1000.are)
COPY_EXISTING ~ar0800.are~ ~override~ // Graveyard District, Stein
  LPF ALTER_AREA_ACTOR INT_VAR y_coord=1071 STR_VAR actor_name=Stein END
  BUT_ONLY

// completing the norh flip, pt 1 (see cdhorse1.cre, cdhorse2.cre)
  COPY_EXISTING ~ar0903.are~ ~override~
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Atta Girl" cre_file = cdhorse1 END
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Angel"     cre_file = cdhorse2 END

// tbd, cam (from jmerry)
// npcs spawning/moving on impassable terrain (see also baerie.dlg, aeriej.dlg, ar0800.are)
COPY_EXISTING ~ar1000.are~ ~override~ // Government District, Amnish Soldier
  READ_LONG 0x54 ofsActors
  READ_SHORT 0x58 numActors
  FOR (idx = 0; idx < numActors; ++idx) BEGIN
    SET offsetx = ofsActors + (idx * 0x110) + 0x20
    SET offsety = ofsActors + (idx * 0x110) + 0x22
    READ_SHORT offsetx xpos
    READ_SHORT offsety ypos
    PATCH_IF ((xpos = 2796) AND (ypos = 538)) BEGIN
      WRITE_SHORT offsety 558
    END
  END // Multiple actors with same name, long version needed
  BUT_ONLY

// tbd, cam
// make tear door transparent so #3 eye graphic can be seen
COPY_EXISTING ~ar2900.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_no_look = 1 STR_VAR door_name = door01 END
  BUT_ONLY

// wk spawn points - defer spawn point activion to area script (see also ar3001.bcs)
COPY_EXISTING ~ar3001.are~ ~override~
  READ_LONG 0x60 spawn_off
  READ_LONG 0x64 spawn_num
  FOR (index = 0 ; index < spawn_num ; ++index) BEGIN
    WRITE_SHORT (spawn_off + 0x86 + (index * 0xc8)) 0 /// turns off all spawn points
  END
  BUT_ONLY

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_cre_fixes_bg2ee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/alignment_changes_bg2ee.tph~ // bulk alignment changes

INCLUDE ~eefixpack/files/tph/tbd_elemental_summons.tph~ // tbd, cam: elemental prince summons shouldn't grant XP
INCLUDE ~eefixpack/files/tph/tbd_conjure_animals_bearposu.tph~ // tbd, cam: bears summoned by conjure animals should used summoned scripts, not bear scripts
INCLUDE ~eefixpack/files/tph/bg2ee_cre_levels.tph~ // sahuagin hit dice
INCLUDE ~eefixpack/files/tph/cre_spells.tph~ // wraiths, mists, shadows and shades all have leftover memorized and known spells

// applying duergar skin tone to duergar dwarves
COPY_EXISTING ~ar18dwaf.cre~ ~override~ // duergar
              ~duergar3.cre~ ~override~ // duergar
              ~gorje.cre~    ~override~ // gorje hilldark
              ~scdur.cre~    ~override~ // unger hilldark
              ~sewdue01.cre~ ~override~ // duergar sapper
              ~sewdue02.cre~ ~override~ // duergar sapper
              ~sewyag01.cre~ ~override~ // duergar sapper
  WRITE_BYTE 0x2f 83 // "dark blusih gray", used by other duergar
  BUT_ONLY 

// tbd, cam (from MiH)
// give Drizzt proper dual-wielding setup as he has in bg
COPY_EXISTING ~c6drizz.cre~  ~override~
              ~c6drizz2.cre~ ~override~
              ~c6drizz3.cre~ ~override~
              ~drizzt.cre~   ~override~
  LPF DELETE_CRE_ITEM STR_VAR item_to_delete = sw1h16 END // delete twinkle in glove slot
  REPLACE_CRE_ITEM helmnoan #0 #0 #0 NONE HELMET          // add no-animation helmet (replaces Icingdeath)
  REPLACE_CRE_ITEM sw1h16   #0 #0 #0 NONE WEAPON1 EQUIP   // re-add Twinkle as main weapon (replaces drizzts)
  ADD_CRE_ITEM     sw1h15   #0 #0 #0 NONE SHIELD          // re-add icingdeath as offhand weapon
  SET_BG2_PROFICIENCY PROFICIENCYLONGSWORD 2
  SET_BG2_PROFICIENCY PROFICIENCYSCIMITARWAKISASHININJATO 2
  SET_BG2_PROFICIENCY PROFICIENCYDAGGER 2
  SET_BG2_PROFICIENCY PROFICIENCYSHORTBOW 2
  SET_BG2_PROFICIENCY PROFICIENCY2WEAPON 3

// tbd, cam
// pocket plane challenge creatures shouldn't drop stuff
COPY_EXISTING ~chevil01.cre~ ~override~
              ~chevil03.cre~ ~override~
              ~chgood04.cre~ ~override~
              ~chgood05.cre~ ~override~
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = all END
  BUT_ONLY

// ie-3658, cam
// Jassar (and others) shouldn't drop identified magic armor
COPY_EXISTING ~ohbgit01.cre~ ~override~
              ~ohbthr01.cre~ ~override~
              ~ohdcru04.cre~ ~override~
              ~ohdlufpl.cre~ ~override~
              ~ohnvgc.cre~   ~override~
              ~ohrjassa.cre~ ~override~
  REPLACE_CRE_ITEM ~plat23~ #0 #0 #0 ~NONE~ ~ARMOR~ // replace plat19, the special 0-lore armor from wish with identical plate with lore

// tbd, cam
// has all hallmarks of fear immunity except fear immunity itself
COPY_EXISTING ~gith03.cre~   ~override~ // anti-paladin - blocks cdhorror
              ~ohgith03.cre~ ~override~ // anti-paladin - blocks cdhorror
  LPF ALTER_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 101 parameter2 = 24 END // yo dawg, we heard you like immunity to effect, so we immunity to efected your immunity to effect
  BUT_ONLY

// completing the norh flip, pt 2 (see ar0903.are)
  COPY_EXISTING ~horse.cre~ ~override/cdhorse1.cre~
    SAY 0x08 @106
    SAY 0x0c @106

  COPY_EXISTING ~horse.cre~ ~override/cdhorse2.cre~
    SAY 0x08 @107
    SAY 0x0c @107

// immune to confusion, but missing spconfus immunity
COPY_EXISTING ~ohhgmum.cre~  ~override~ // greater mummy - immune to fear - immune to confusion
              ~ohhraffi.cre~ ~override~ // raffiyah - immune to fear - immune to confusion
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 128 opcode = 296 parameter2 = 0 STR_VAR resource = spconfus END
  BUT_ONLY

// tbd, cam
// HD too low for most ankhegs; bgee ones also had pickpocketable shells
INCLUDE ~eefixpack/files/tph/tbd_ankheg.tph~

//tbd, cam
// poison mists can be summoned but can also spawn as normal creatures, so need both a gender: neither and gender: summoned version
INCLUDE ~eefixpack/files/tph/tbd_poison_mists.tph~

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_itm_fixes_bg2ee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/tbd_bg2ee_146.tph~ // tbd, cam: fixes items that cast spells
INCLUDE ~eefixpack/files/tph/tbd_energy_blades.tph~ // tbd, cam: per descript, energy blades should do missile damage, not slashing
INCLUDE ~eefixpack/files/tph/usability_arrow_bolt.tph~ // monks using arrows/bolts

// basilisk petrification should force, you know, a save against petrification
COPY_EXISTING ~basigaze.itm~ ~override~
              ~basilg1.itm~  ~override~
              ~basill1.itm~  ~override~
  LPF ALTER_EFFECT INT_VAR savingthrow = BIT4 END // save vs. spell > petrification

COPY_EXISTING ~blun26.itm~ ~override~ // club of detonation +3 (see blun26fb.spl, blun27fb.spl)
              ~blun27.itm~ ~override~ // club of detonation +5
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR resource = EVAL ~%SOURCE_RES%fb~ END

// mismatched icons between inventory and item ability
COPY_EXISTING ~book06.itm~ ~override~
              ~book07.itm~ ~override~ // fine in IWDEE
              ~book08.itm~ ~override~
  WRITE_ASCIIE 0x3a ~i%SOURCE_RES%~ #8
  BUT_ONLY

// fixing waterdeep books (fix also used for bg2ee)
INCLUDE ~eefixpack/files/tph/waterdeep_books.tph~

// tbd, cam
// set to 400% speed, not set a fixed value of 30
COPY_EXISTING ~boot10.itm~ ~override~ // Boots of Lightning Speed
  LPF ALTER_EFFECT INT_VAR match_opcode = 126 parameter1 = 400 parameter2 = 2 END

// tbd, cam
// Launchers that generate their own ammo override the projectile of loaded ammo
COPY_EXISTING ~bow15.itm~   ~override~ // tansheron's bow
              ~bow19.itm~   ~override~ // shortbow of gesen
              ~compb15.itm~ ~override~ // tansheron's bow (1pp clone)
              ~compb19.itm~ ~override~ // shortbow of gesen (1pp clone)
              ~slng07.itm~  ~override~ // sling of seeking
              ~wasling.itm~ ~override~ // sling of everard
              ~xbow15.itm~  ~override~ // firetooth, base
              ~xbow16.itm~  ~override~ // firetooth, upgraded
  LPF ALTER_HEADER INT_VAR match_type = 4 projectile = 1 END // remove projectile
  BUT_ONLY

// gloves of healing are unusable by cleric, mages, but not cleric-mages
COPY_EXISTING ~brac20.itm~ ~override~
  WRITE_LONG 0x1e (THIS BOR BIT8) // adds c/m flag

// tbd, cam
// lmd via foebane lacks normal immunity for golems & undead
COPY_EXISTING ~comps63.itm~ ~override~ // foebane +5, 1pp cosmetic version
              ~sw1h63.itm~  ~override~ // foebane +5
  LPF CLONE_EFFECT INT_VAR check_globals = 0 match_opcode = 146 opcode = 324 parameter1 = 0 parameter2 = 55 timing = 0 resist_dispel = 0 STR_VAR match_resource = spin104a resource = EVAL ~%SOURCE_RES%~ END

// poison icon shouldn't be dispellable
COPY_EXISTING ~dart05.itm~ ~override~ // asp's nest
  LPF ALTER_EFFECT INT_VAR resist_dispel = 0 END

// tbd, cam (from MiH)
// flindbar should decrease thac0 of target, not increase it
COPY_EXISTING ~flind1.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 54 END // alter_effect can't set negative, so MAX LAZINESS ENGAGED
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 54 target = 2 parameter1 = "-4" duration = 20 savingthrow = BIT3 END
  BUT_ONLY

// tbd, cam
// elven charm immunity can block level drain and other effects
OUTER_SET charm_immunity = IDS_OF_SYMBOL (~splstate~ ~CHARM_IMMUNITY~)
COPY_EXISTING ~gorwom1.itm~  ~override~ // Nalmissra's attack
  LPF DELETE_EFFECT INT_VAR match_opcode = 324 END // delete existing 324s
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 probability1 = 50 END // charm is 50%, but icon is 100%
  PATCH_FOR_EACH op IN 5 215 142 BEGIN
    LPF CLONE_EFFECT  INT_VAR match_opcode = op match_probability1 = 50 STR_VAR insert = last END
    LPF DELETE_EFFECT INT_VAR match_opcode = op match_probability1 = 50 multi_match = 1 END
  END
//  LPF CLONE_EFFECT INT_VAR match_opcode = 5 opcode = 324 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 savebonus = 0 parameter1 = charm_immunity parameter2 = 110 END // charm immunity
  LPF CLONE_EFFECT INT_VAR match_opcode = 5 opcode = 324 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 savebonus = 0 parameter1 = 0 parameter2 = 19 probability1 = 15 END // half-elf immunity
  LPF CLONE_EFFECT INT_VAR match_opcode = 5 opcode = 324 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 savebonus = 0 parameter1 = 0 parameter2 = 15 probability1 = 45 END // elf immunity

// tbd, cam
// blackmist plays two sounds when it blinds targets; one of them is the expirty sound and should be delayed
COPY_EXISTING ~halb06.itm~   ~override~ // Blackmist +4 - causes blind
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 timing = 4 duration = 600 STR_VAR match_resource = eff_e07 END

// prevent hexxat, haer'dalis from using racially-restricted items (see also npbow.itm, npsw01.itm, hlolth.itm)
COPY_EXISTING ~hamm06.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode=319 target=2 power=1 parameter1=4 parameter2=4 special=1100 END // dwarves only

// tbd, cam
// helmet appearance corrections
COPY_EXISTING ~helm32.itm~ ~override~ // Helm of the Rock
  WRITE_ASCII 0x22 ~j2~ #2 // paperdoll appearance
  LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 48 parameter1 = 207 END // change 'helmet wings' to 'minor noble 2 chrome gold'
  LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 52 parameter1 = 199 END // change 'helmet face' to 'black teal'
  LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 53 parameter1 = 199 END // change 'helmet exterior' to 'black teal'
  LPF CLONE_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 48 parameter1 =   0 parameter2 = 49 END // clone 'helmet wings' to 'helmet detail', color 'rust tinted black'
  LPF CLONE_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 48 parameter1 =   0 parameter2 = 50 END // clone 'helmet wings' to 'helmet plume', color 'rust tinted black'

COPY_EXISTING ~helm33.itm~ ~override~ // Gold Horned Helm
  WRITE_ASCII 0x22 ~j3~ #2 // paperdoll appearance
  LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 48 parameter1 = 223 END // change 'helmet wings' to 'drow skin moss green'
  LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 53 parameter1 = 208 END // change 'helmet exterior' to 'faded wheat'
  LPF CLONE_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 53                  parameter2 = 52 END // clone 'helmet exterior' to 'helmet face'

// tbd, cam
// combat feedback used with instant/limited timing and durations
COPY_EXISTING ~helm32.itm~   ~override~ // Helm of the Rock - causes fear - portrait icon 36 - unknown long effect
              ~spin595.spl~  ~override~ // Yellow Dragon Scorching Sand - causes blind - unknown long effect
              ~spin878.spl~  ~override~ // Level Drain - causes blind - causes level drain - unknown long effect
              ~spin893.spl~  ~override~ // Shadow Dragon Breath - causes blind - causes level drain - unknown long effect
              ~spin929.spl~  ~override~ // Mist Ball - causes blind - unknown long effect
              ~spin931.spl~  ~override~ // Sooty Ball - causes blind - unknown long effect
              ~spwi714.spl~  ~override~ // Prismatic Spray - causes blind - causes feeblemind - unknown long effect
              ~spwi815.spl~  ~override~ // Power Word, Blind - causes blind - unknown long effect - plays sound EFF_E06
              ~spwi958.spl~  ~override~ // Power Word, Blind - causes blind - unknown long effect - plays sound EFF_E06
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 timing = 1 duration = 0 END

// prevent hexxat, haer'dalis from using racially-restricted items (see also hamm06.itm, npbow.itm, npsw01.itm)
COPY_EXISTING ~hlolth.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode=319 target=2 power=0 parameter1=153 parameter2=4 special=8330 END // not usable by Tieflings - display this
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode=319 target=2 power=0 parameter1=125 parameter2=4 special=0 END // not usable by Vampires - silent, since vampire isn't really a race

// tbd, cam
// blocks confusion animation, but is subject to confusion
COPY_EXISTING //~dragring.itm~ ~override~ // ring - immune to stun - immune to hold - blocks spmindat - blocks spconfus - probably intentional since animation isn't right for dragons
              ~hslaywpn.itm~ ~override~ // <invalid strref -1> - immune to stun - blocks spconfus - blocks spmindat
              ~ohbbslay.itm~ ~override~ // <invalid strref -1> - immune to stun - blocks spconfus - blocks spmindat
              ~slayerw1.itm~ ~override~ // <invalid strref -1> - immune to stun - blocks spconfus - blocks spmindat
              ~slayerwp.itm~ ~override~ // <invalid strref -1> - immune to stun - blocks spconfus - blocks spmindat
  LPF DELETE_EFFECT INT_VAR match_opcode = 296 STR_VAR match_resource = spconfus END
  BUT_ONLY

// tie slow icon to slow-via-disease effect so it gets cleared by disease cures
COPY_EXISTING ~iotyugh.itm~  ~override~ // (itm) Attack - causes disease - portrait icon 41
              ~ohrslng1.itm~ ~override~ // (itm) Lupine Sling +2 - causes disease - portrait icon 41 [uses slow icon with its disease]
              ~otyugh.itm~   ~override~ // (itm) Attack - causes disease - portrait icon 41
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 41 END // delete slow effect
  LPF ALTER_EFFECT INT_VAR match_opcode = 78 match_parameter2 = 10 special = 41 END // change slow-via-disease to use slow icon instead of disease (other disease effect already uses disease icon)

// sirine1 uses 'feeblemind' icon for an intelligence drain; instead use generic 'ability score drained' for both
COPY_EXISTING ~magiconf.itm~ ~override~ // Ghoul hand - causes confusion - unknown long effect - portrait icon 3
              ~sirine1.itm~  ~override~ // Ghoul hand - causes confusion - unknown long effect - portrait icon 3 - portrait icon 48
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 48 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 19 opcode = 142 parameter1 = 0 parameter2 = 91 END

// book of infinite spells casts true sight, not true seeing
COPY_EXISTING ~misc3a3.itm~   ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR match_resource = sppr505 resource = spwi609 END

// tbd, cam
// uses 'intoxication' icon for a confusion effect
COPY_EXISTING ~misc3m.itm~   ~override~ // Harp of Discord - causes confusion - plays animation SPCONFUS - portrait icon 5 - plays sound EFF_P05
  //LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 5 parameter2 = 3 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 5 END // moved to subspell

// patrol leader helmet should be unusable by thieves and kensais, like other helmets
COPY_EXISTING ~misca6.itm~  ~override~ // patrol leader helmet
  WRITE_LONG 0x1e (THIS BOR BIT22) // adds thief flag
  WRITE_BYTE 0x2f (THIS BOR BIT2)  // adds kensai flag
  BUT_ONLY

INCLUDE ~eefixpack/files/tph/dragomir.tph~ // making cloak of dragomir a critical item to prevent destruction

// prevent hexxat, haer'dalis from using racially-restricted items (see also hamm06.itm, hlolth.itm)
COPY_EXISTING ~npbow.itm~  ~override~
              ~npsw01.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode=319 target=2 power=1 parameter1=5 parameter2=4 special=1101 END // halflings only

// immune to hold, but missing spmindat immunity
COPY_EXISTING ~ohbetrai.itm~ ~override~ // <invalid strref -1> - immune to stun - immune to hold
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 175 opcode = 296 parameter2 = 0 STR_VAR resource = spmindat END
  BUT_ONLY               

COPY_EXISTING ~ohdsw03.itm~ ~override~ // brass blade (see also ohdsw03f.spl)
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 12 savingthrow = 0 END // remove 'bypass MI' flag
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 146 power = 0 timing = 1 duration = 0 STR_VAR resource = ohdsw03f END // fixed cast at level 10, remove 2-sec cast delay
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 0 secondary = 0 END // remove primary/secondary from header

// cowl of the stars should cast MMM at player level; run it through an innate so that it picks up non-mage levels
COPY ~eefixpack/files/spl/innate_intermediary_template.spl~ ~override/cdwi325.spl~
  //LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR resource = spwi325 END // default spell here is spwi325 since this is the first
COPY_EXISTING ~ohrclck1.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR match_resource = spwi325 resource = cdwi325 END

// move planetar dispel-on-hit to after vorpal effect so that death ward can work better (in bgee, bg2ee, iwdee.tph)
COPY_EXISTING ~planetar.itm~ ~override~
  LPF CLONE_EFFECT  INT_VAR match_opcode = 58 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 58 multi_match = 1 END

// clone immunity to webtrav (web) projectile into web1p (single-target web) projectile immunity (in bgee, bg2ee, iwdee.tph)
COPY_EXISTING ~plyspid.itm~  ~override~ // arcane polymorph spider
              ~plyspid2.itm~ ~override~ // avenger sword spider
  LPF CLONE_EFFECT INT_VAR match_opcode = 83 match_parameter2 = 319 parameter2 = 259 END // fortunately projectiles are all same value sin bg/bg2/iwd

// tbd, cam
// elixir of health should cure intoxication, not set it to zero
COPY_EXISTING ~potn17.itm~ ~override~ // Elixir of Health - set drunkenness to 0, just change to 'cure drunk' instead
  LPF ALTER_EFFECT INT_VAR match_opcode = 94 opcode = 164 parameter2 = 0 END // flesh out effects below

// potion of perception should provide bonus to Hide in Shadows
COPY_EXISTING ~potn39.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 59 opcode = 275 STR_VAR insert = below END

// poison resistance blocking con penalty, choking noises
COPY_EXISTING ~potn48.itm~ ~override~ // Vial of Mysterious Liquid - causes poison
  LPF CLONE_EFFECT INT_VAR match_opcode = 324 STR_VAR insert = last END
  LPF CLONE_EFFECT INT_VAR match_opcode = 25 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR multi_match = 2 match_opcode = 324 END
  LPF DELETE_EFFECT INT_VAR multi_match = 1 match_opcode = 25 END

// tbd, cam (from MiH)
// correct ground icon for quiver/case of plenty
COPY_EXISTING ~quiver01.itm~ ~override~ // quiver of plenty +1
              ~quiver02.itm~ ~override~ // case of plenty +1
              ~quiver03.itm~ ~override~ // quiver of plenty +2
              ~quiver04.itm~ ~override~ // case of plenty +2
  WRITE_ASCII 0x44 ~gquiver0~ #8 // ground icon
  BUT_ONLY

// rod of terror's fear affect lasts 5 rounds instead of the stated 4
COPY_EXISTING ~rods05.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 30 duration = 24 END

// tbd, cam
// protection from magic scrolls not dispelling all buffs
COPY_EXISTING ~scrl07.itm~ ~override~ // protection from magic
  FOR (index = 0 ; index < 11 ; ++index) BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 220 parameter1 = 99 parameter2 = index target = 2 timing = 1 END // remove school protections: index
  END
  FOR (index = 0 ; index < 14 ; ++index) BEGIN // only 10 spell schools, but 13 secondary types
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 221 parameter1 = 99 parameter2 = index target = 2 timing = 1 END // remove school protections: index
  END
  BUT_ONLY

// the feeblemind effect already sets INT=3, so purging redundant effects
COPY_EXISTING ~sirine.itm~   ~override~ // Ghoul hand - causes feeblemind - unknown long effect - plays sound EFF_E05 - portrait icon 48
  LPF DELETE_EFFECT INT_VAR match_opcode = 19 END

// tbd, cam
// elemental staves' sound effect should only target the specified creature
OUTER_SET elemental_destroyed = RESOLVE_STR_REF (@100)
COPY_EXISTING ~staf15.itm~ ~override~ // staff of air +2
              ~staf16.itm~ ~override~ // staff of earth +2
              ~staf17.itm~ ~override~ // staff of fire +2
  LPF CLONE_EFFECT INT_VAR match_opcode = 55 opcode = 318 power = 0 parameter2 = 115 timing = 0 savingthrow = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END
  LPF CLONE_EFFECT INT_VAR match_opcode = 55 opcode = 139 parameter1 = elemental_destroyed parameter2 = 0 END // add 'elemental destroyed' message
  LPF ALTER_EFFECT INT_VAR match_opcode = 55 parameter1 = 0 parameter2 = 2 END // now gated by a 318, make 55 universal
  BUT_ONLY

// tbd, cam
// per descript knockback shouldn't work on 'large creatures'
COPY_EXISTING ~staf22.itm~  ~override~ // staff of the ram +6
  LPF CLONE_EFFECT INT_VAR check_globals = 0 match_opcode = 235 opcode = 318 parameter1 = 0 parameter2 = 13 timing = 0 duration = 0 resist_dispel = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ END

INCLUDE ~eefixpack/files/tph/burning_earth.tph~ // fix burning earth bonuses

// tbd, cam
// ilbratha's mirror image can be blocked by wielder's MR
COPY_EXISTING ~sw1h26.itm~ ~override~ // ilbratha
  LPF ALTER_EFFECT INT_VAR check_globals = 0 header_type = 3 resist_dispel = 3 END
  BUT_ONLY

// tbd, cam
// sword of flame and angurvadal apply glows to wrong part of avatar
COPY_EXISTING ~sw1h53.itm~ ~override~ // Sword of Flame +1
              ~sw1h60.itm~ ~override~ // Angurvadal +4
              ~sw1h61.itm~ ~override~ // Angurvadal +5
  LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 9 match_parameter2 = (5 + (20 << 16)) parameter2 = (20 + (20 << 16)) END
  BUT_ONLY

// remove undocumented +2 save bonus from wand of fear
INCLUDE ~eefixpack/files/tph/wand02.tph~

// luke
// Mis-indexed effects
WITH_SCOPE BEGIN
  COPY_EXISTING ~hamm06.itm~ ~override~ // Dwarven Thrower +3
                ~staf01.itm~ ~override~ // Quarterstaff
    LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Longtooth: The Grave Binder**
// - Unusually large dagger, depicted as a Short Sword => make sure `range=1`
WITH_SCOPE BEGIN
  COPY_EXISTING "dagg04.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_ITEM_HEADER~ INT_VAR ~range~ = 1 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Paralytic Bolt**
// - Rename as "Paralytic Bolt +1" (the item's name should state its enchantment level)
// - `resist_dispel` should be `0` (effects should not be dispellable and should ignore MR)
WITH_SCOPE BEGIN
  COPY_EXISTING "sahbolt.itm" "override"
    /* Feature blocks */
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~resist_dispel~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// – Neera's Staff +1 => When the staff strikes a target, there is a 10% chance that either the target or the wielder will take 1 point of fire damage
//// – In particular, on 10% of all hits there's an extra point of fire damage, so Neera gets hit 5% of the time and her target 5%
//// – This isn't implemented properly since the two op12 effects share the same probability range... Also, while we're at it, make sure it's 10% chance and not 11% chance...
WITH_SCOPE BEGIN
  COPY_EXISTING ~stafn1.itm~ ~override~ // Neera's Staff +1
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~multi_match~ = 1 ~match_opcode~ = 12 ~probability1~ = 4 END // 5% chance (target)
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~match_opcode~ = 12 ~match_probability1~ = 10 ~probability2~ = 5 ~probability1~ = 9 END // 10% chance (wielder)
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Chill Touch – better feedback for the player when attacking Golems or Undead
WITH_SCOPE BEGIN
  COPY_EXISTING "chillt.itm" "override"
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 177 END
    LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~insert_point~ = 0 ~type~ = 1 ~target~ = 2 ~opcode~ = 324 ~parameter2~ = 55 STR_VAR ~resource~ = ~%DEST_RES%~ END // Immunity to resource and message
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Shocking Grasp**
- Should not interact with level-based spell protections
- Secondary type should be `COMBINATION`
- Delete expiry sound since the spell can end prematurely
- Make sure `Range: Touch`
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/shocking_grasp.tph"
  LAUNCH_ACTION_FUNCTION "WIZARD_SHOCKING_GRASP" END
END

/*
luke
**Black Blade of Disaster**
- Should not interact with level-based spell protections
- Make sure *"The caster is considered to be proficient to the point of Grand Mastery in this weapon"* always works
  - `op233` needs to be on the normal effects list to be evaluated after chargen proficiency effects and override them!
- Provide a workaround for the following bugs:
  - if you Dual-class when the Black Blade is equipped, the character permanently gains grand mastery in long swords
  - if, for example, you have a warrior with an item that grants dagger proficiency while equipped, they can then take dagger specialization at a level-up, remove the item, and keep the specialization permanently
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/black_blade_of_disaster.tph"
    LAUNCH_ACTION_FUNCTION "WIZARD_BLACK_BLADE_OF_DISASTER" END
  END
END

// luke
// **Searing Orb**
// - should not interact with level-based spell protections
// - recode from scratch so as to use `op326` effects instead of messy `op177` effects
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/searing_orb.tph"
  LAUNCH_ACTION_FUNCTION ~CLERIC_SOL_SEARING_ORB~ END
END

/*
luke
**Magical Weapon Slot**
(Based on the previous tweak to the Black Blade of Disaster)
- Make sure natural creature weapons / touch attacks do not benefit from Fighting Styles
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/magical_weapon_slot.tph"
    LAUNCH_ACTION_FUNCTION "MAGICAL_WEAPON_SLOT" END
  END
END

/*
luke
**Wand of Glitterdust**
- Make sure all effects are dispellable/do not bypass Magic Resistance (so as to match the corresponding spell)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "ohdwand1.itm" "override"
    LPF "ALTER_EFFECT" INT_VAR "check_globals" = 0 "resist_dispel" = BIT0 END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Kuo-toan Bolt**
- Make sure it ignores Magic Resistance
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "kuobolt3.itm" "override"
    LPF "ALTER_EFFECT" INT_VAR "check_globals" = 0 "resist_dispel" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// tbd, cam
// fixing paperdoll animations
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_paperdoll_animations BEGIN
  misc9q   => sc // Habib's Mighty Scimitar uses a katana paperdoll animation instead of scimitar
  rodsword => fs // The Flaming Long Sword +1 from the Rod of Lordly Might (rodsword) should use the flaming sword animation
  sw1h66   => ss // Yamato +4 uses a scimitar animation instead of short sword (like other wakizashi)
  sw1h67   => s1 // Usuno's Blade uses a scimitar animation instead of long sword (like other ninja-to)
  sw1h68a  => s2 // The undroppable copy of the Spectral brand has no animation
END

ACTION_PHP_EACH cd_paperdoll_animations AS item => anim BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    WRITE_ASCIIE 0x22 "%anim%" #2

END

// tbd, graion
// the Potion of Extra Healing used in the tutorial uses BG1EE string indices instead of oBG2 ones
COPY_EXISTING ~ttpot.itm~ ~override~
  WRITE_LONG 0xC 18893
  WRITE_LONG 0x50 16264
  WRITE_LONG 0x54 18896

/*
+--------------------------------------------------------------------+
| [Luke]                                                             |
+--------------------------------------------------------------------+
| - Rancor +1 (Dorn)                                                 |
|   - in this case, we need a two-stage subspell (so as to make sure |
|     the wielder is indeed Dorn)                                    |
| - Crimson Dawn +2                                                  |
| - Dervish Crescent +2                                              |
+--------------------------------------------------------------------+
| It is the *sword* that should make the kill, not something else    |
+--------------------------------------------------------------------+
*/

WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "gt_fixpack_array"
  ACTION_DEFINE_ASSOCIATIVE_ARRAY "gt_fixpack_array" BEGIN
    "sw2hd1" => "ohdsw1" // Rancor +1 (Dorn)
    "bdsw1h01" => "bdcrimsn" // Crimson Dawn +2 (sod)
    "bdsw1h08" => "bdsw1h08" // Dervish Crescent +2 (sod)
  END
  ACTION_PHP_EACH "gt_fixpack_array" AS "itm" => "spl" BEGIN
    COPY_EXISTING "%itm%.itm" "override"
      PATCH_MATCH "%itm%" WITH
        "sw2hd1" BEGIN
          INNER_ACTION BEGIN
            CREATE ~spl~ ~ohdsw4~
              /* Header */
              WRITE_LONG NAME1 "-1"
              WRITE_LONG NAME2 ~-1~
              WRITE_LONG UNIDENTIFIED_DESC "-1"
              WRITE_LONG DESC ~-1~
              WRITE_LONG 0x18 (BIT14 BOR BIT25)
              WRITE_LONG 0x34 1 // Level
              WRITE_LONG 0x64 0x72 // Extended Header offset
              WRITE_SHORT 0x68 1 // Extended Header count
              WRITE_LONG 0x6a 0x9a // Feature Block Table offset
              INSERT_BYTES 0x72 0x28
              /* Extended Header */
              WRITE_BYTE 0x72 3 // header type
              WRITE_SHORT 0x80 32767 // Ability range
              WRITE_SHORT 0x82 1 // Minimum level
              WRITE_SHORT 0x98 IDS_OF_SYMBOL ("MISSILE" "None") // Projectile
              /* Feature blocks */
              LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 146 "target" = 9 "parameter2" = 1 STR_VAR "resource" = "%spl%" END // cast spell
          END
          LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~type~ = 1 "opcode" = 326 "target" = 2 "parameter2" = 103 "parameter1" = 3 STR_VAR "resource" = "ohdsw4" END // apply effects list
        END
        DEFAULT
          LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~type~ = 1 "opcode" = 326 "target" = 2 "parameter2" = 103 "parameter1" = 3 STR_VAR "resource" = "%spl%" END // apply effects list
      END
      LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "check_headers" = 0 "match_opcode" = 232 STR_VAR "match_resource" = "%spl%" END // no longer needed
    BUT_ONLY_IF_IT_CHANGES IF_EXISTS
  END
END

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_spl_fixes_bg2ee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/bg_bg2_common_spell_fixes.tph~     // spell fixes in common between bg, bg2
INCLUDE ~eefixpack/files/tph/bg2_iwd_common_spell_fixes.tph~    // spell fixes in common between bg2, iwd
INCLUDE ~eefixpack/files/tph/bg_bg2_iwd_common_spell_fixes.tph~ // spell fixes in common between bg, bg2, and iwd

INCLUDE ~eefixpack/files/tph/tbd_324_traps_bg2ee.tph~ // tbd, cam - game can crash if a spell-via-trap uses 318/324 vs. a spell without a name

// tbd, cam
// misindexed effects
COPY_EXISTING ~ohbwi302.spl~ ~override~
              ~ohbwi304.spl~ ~override~
  LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  //LPF cd_fx_unmisindex END
  BUT_ONLY
  
//tbd, cam
// inconsistent targeting (unused, fixed anyway)
// also extend to level 50
COPY_EXISTING ~bhaal1a.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR target = 2 END // some op164s are target party (3)
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 0 level_cap = 50 RET abil_delta END // no duration change, just need addt'l headers
  SET multiply = 2 // mass cure cures 1d8 + 1/level HP; bhaal1a heals 2d8 + 2/level
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = (abil_num - abil_delta) ; index < abil_num ; ++index) BEGIN // loop through new abilities only
    READ_SHORT  (abil_off + 0x10 + (index * 0x28)) level       // just read the level
    READ_SHORT  (abil_off + 0x1e + (index * 0x28)) abil_fx_num
    READ_SHORT  (abil_off + 0x20 + (index * 0x28)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off +        ((index2 + abil_fx_idx) * 0x30)) op
      PATCH_IF op = 17 BEGIN // cur hp bonus
        WRITE_LONG (fx_off + 0x04 + ((index2 + abil_fx_idx) * 0x30)) (level * multiply)
      END
    END     
  END
  BUT_ONLY

COPY_EXISTING ~bhaal1b.spl~ ~override~ // Regeneration (unused, fixed anyway)
  LPF CD_TRIM-O-MATIC INT_VAR level_cap = 1 END // trim down to one header
  LPF CD_EXTEND-O-MATIC INT_VAR min_lev_alt = 14 dur_special = 1 step_size = 2 step_dur = 6 level_cap = 30 END // then build it back up with proper progression

OUTER_FOR (index = 1 ; index < 6 ; ++index) BEGIN // for bhaal3b (unused, fixed anyway)
  COPY_EXISTING ~bdpweapn.eff~ ~override/bh3b%index%.eff~
    WRITE_ASCIIE 0x30 ~%DEST_RES%~ #8  
END

// tbd, cam
// Hexxat's ability is supposed to summon 1d4 creatures, does something wacky intstead
// also extend duration out to l50
COPY_EXISTING ~ohhsumm.spl~ ~override~ // children of the night
  LPF DELETE_EFFECT INT_VAR multi_match = 1                                             STR_VAR match_resource = ohhrat END // change 1d4+1 rats to 1d4
  LPF ALTER_EFFECT  INT_VAR                                          probability1 = 100 STR_VAR match_resource = ohhrat END // uncorrect rat probs so I can run the next three unimpeded
  LPF ALTER_EFFECT  INT_VAR multi_match = 1                          probability1 = 24                                  END // change summon #1 to 25%
  LPF ALTER_EFFECT  INT_VAR multi_match = 1 match_probability1 = 100 probability1 = 49                                  END // change summon #2 to 50%
  LPF ALTER_EFFECT  INT_VAR multi_match = 1 match_probability1 = 100 probability1 = 74                                  END // change summon #3 to 75%
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 6 level_cap = 50 END // 1 round/level
  BUT_ONLY

// tbd, cam
// shadow twin duration should be two turns, not one
COPY_EXISTING ~spcl936.spl~ ~override~ // shadow twin
  LPF ALTER_EFFECT INT_VAR match_duration = 60 duration = 120 END
  BUT_ONLY

// tbd, cam
// damage on minsc's berserk expiry should precede the bonus HP expiring, like it does in bgee
COPY_EXISTING ~spin117.spl~ ~override~ // minsc's berserk
  LPF ALTER_EFFECT INT_VAR match_duration = 119 duration = 120 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 duration = 119 END
  BUT_ONLY

// find traps part of moon dog sight doesn't work (projectile doesn't work for op150)
COPY_EXISTING ~spin696.spl~  ~override~ // moon dog sight
  LPF ALTER_EFFECT INT_VAR match_opcode = 150 opcode = 146 target = 1 timing = 1 duration = 0 STR_VAR resource = sppr205d END // change to cast find traps subspell

// tbd, cam
// wrong duration for portrait icon
//COPY_EXISTING ~spin807.spl~  ~override~ // Slayer Fear - causes fear - portrait icon 36
  //LPF ALTER_EFFECT INT_VAR match_duration = 120 duration = 12 END
WITH_SCOPE BEGIN
  COPY_EXISTING - ~spin807.spl~  ~override~ // Slayer Fear - causes fear - portrait icon 36
    GET_OFFSET_ARRAY ab_arr SPL_V10_HEADERS
    PHP_EACH ab_arr AS ab_ind => ab_off BEGIN
      GET_OFFSET_ARRAY2 fx_arr ab_off SPL_V10_HEAD_EFFECTS
      PHP_EACH fx_arr AS fx_ind => fx_off BEGIN
        PATCH_MATCH SHORT_AT fx_off WITH
          326 WHEN SLONG_AT (fx_off + 0x4) = IDS_OF_SYMBOL ("splstate" "PANIC_IMMUNITY") AND LONG_AT (fx_off + 0x8) = 111 BEGIN
            READ_ASCII (fx_off + 0x14) "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY
  COPY_EXISTING "%subspell%.spl" "override"
    LPF ALTER_EFFECT INT_VAR match_duration = 120 duration = 12 END
  BUT_ONLY
END

COPY_EXISTING ~spin891.spl~ ~override~ // make moon dog howl only affect enemies
  LPF ALTER_HEADER INT_VAR projectile = 159 END // inarea > inareanp

// making custom fireballs for clubs of detonation, brass blade  
COPY_EXISTING ~spwi304.spl~ ~override/ohdsw03f.spl~ // 10d6 fireball (arcane, for brass blade, ohdsw03.itm)
  LPF CD_TRIM-O-MATIC INT_VAR level_cap = 1 END 
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicenumber = 10 END   
  
COPY_EXISTING ~ohdsw03f.spl~ ~override/blun27fb.spl~ // 10d6 fireball (innate, for club of detonation +5, blun27.itm)
  WRITE_SHORT 0x1c 4 // innate   
  
COPY_EXISTING ~blun27fb.spl~ ~override/blun26fb.spl~ // 5d6 fireball (innate, for club of detonation +3, blun26.itm)
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicenumber = 5 END   

COPY_EXISTING ~spwi613.spl~ ~override~ // improved haste
  READ_SHORT 0x68 abil_num // op206 vs. spcl521 is always 90 seconds - doesn't scale correctly with other effects
  FOR (index = abil_num ; index > 1 ; --index) BEGIN // skip header 0 since it's correct
    LPF ALTER_EFFECT INT_VAR header = (index - 1) match_duration = 90 duration = (((33 - abil_num) + index) * 6) END 
  END   

// tbd, cam (from jmerry)
// self-stack protection only targets self, not party, like the rest of the effects
COPY_EXISTING ~spwish12.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 target = 3 END
  BUT_ONLY // Now removes hardiness effects from the same targets it adds them to

// luke
// Mis-indexed effects
WITH_SCOPE BEGIN
  COPY_EXISTING ~ohbwi302.spl~ ~override~ // Remove Magic
                ~ohbwi304.spl~ ~override~ // Fireball
    LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Improved Haste
// 1) make sure all effects bypass Magic Resistance
// 2) add missing protection against Ranger Haste
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_IMPROVED_HASTE%.spl" "override" // spwi613
                "%DM_IMPROVED_HASTE%.spl" "override" // spin655
                "spwish36.spl" "override"
                "spwish37.spl" "override"
                "spwish40.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~resist_dispel~ = (BIT0 + BIT1) END
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "206" "resource" = "spra301" RET "count" END
        LPF ~CLONE_EFFECT~ INT_VAR ~silent~ = 1 ~match_opcode~ = 206 ~check_headers~ = (~%count%~ == 0 ? 1 : 0) ~multi_match~ = 1 ~header~ = ~%hdr%~ STR_VAR "resource" = "spra301" END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Energy Drain – provide better feedback for the player
// cam note: could be in joint bg/bg2 fixes except that it needs  different string
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_ENERGY_DRAIN%.spl" "override" // spwi914
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ INT_VAR ~match_opcode~ = 216 ~opcode~ = 139 ~parameter1~ = 40968 STR_VAR ~insert~ = ~below~ END // Display string
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Barkskin**
// - Instantaneous effects should have a duration of `0` (this is probably irrelevant, but just in case...)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_BARKSKIN%.spl~ ~override~ // sppr202
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_timing" = 1 "duration" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Creeping Doom**
// - remove duplicate `op233` effect(s)
// - subspell's `Primary/Secondary type` should match parent `SPL` file
// - subspell's effects should bypass Magic Resistance (since MR is already checked on the parent file)
// - do not use the same projectile as "Insect Plague" (since that is supposed to affect up to 6 creatures)
WITH_SCOPE BEGIN
  WITH_SCOPE BEGIN
    COPY_EXISTING "flpr717a.spl" "override"
      /* Header */
      WRITE_BYTE 0x25 6 // INVOKER
      WRITE_BYTE 0x27 10 // OFFENSIVEDAMAGE
      /* Feature blocks */
      LPF "ALTER_EFFECT" INT_VAR "resist_dispel" = (BIT0 + BIT1) END
    BUT_ONLY_IF_IT_CHANGES
  END
  WITH_SCOPE BEGIN
    COPY_EXISTING "%CLERIC_CREEPING_DOOM%.spl" "override" // sppr717
      /* Extended Header */
      LPF "ALTER_SPELL_HEADER" INT_VAR "projectile" = IDS_OF_SYMBOL ("MISSILE" "Chain_Insect") END
      PATCH_WITH_SCOPE BEGIN
        SET "parameter2" = IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE")
        GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
        PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
          LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" RET "count" END
          LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_globals~ = 0 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%ab_ind%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE") END
        END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END

// tbd, cam
// dragon breath fixes: generalist mages shouldn't get +2 to saves; also blank secondary type (see bulk fixes); other fixes for blue dragon breath
COPY_EXISTING ~spin597.spl~  ~override~ // blue dragon breath needs additional fixes
  WRITE_ASCII 0x10 ~~ #8 // blank casting sound
  WRITE_SHORT 0x1c 4     // innate spell type
  WRITE_LONG  0x1e 0     // blanks exclusion flags
  WRITE_SHORT 0x22 0     // blanks casting animation
  WRITE_LONG  0x34 1     // set spell level 1
  LPF ALTER_EFFECT INT_VAR power = 0 resist_dispel = 0 END

/*
luke
**Wand of Glitterdust**
- Make sure all effects are dispellable/do not bypass Magic Resistance (so as to match the corresponding spell)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING - "ohdwand1.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "BLIND_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "ALTER_EFFECT" INT_VAR "check_globals" = 0 "resist_dispel" = BIT0 BOR BIT1 END // MR already checked on the parent file
  END
END

/*
luke
**The Captive Audience**
- Make sure duration is 12 hours (so as to match item description)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING - "misc2p.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "CHARM_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "ALTER_EFFECT" INT_VAR "match_duration" = 120 "duration" = 3600 END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Harp of Discord**
- Make sure it uses the Confused icon
  - this is a "eefixpack\files\tph\luke\7eyes\7eyes.tph" follow-up
*/
WITH_SCOPE BEGIN
  COPY_EXISTING - "misc3m.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "CONFUSION_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 128 "opcode" = 142 "parameter1" = 0 "parameter2" = 3 "special" = 0 STR_VAR "resource" = "" "insert" = "below" END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Kuo-toan Bolt**
- Make sure it ignores Magic Resistance
- It is supposed to cause paralysis, not web
*/
WITH_SCOPE BEGIN
  COPY_EXISTING - "kuobolt3.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "PARALYZE_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 157 END // web overlay
    LPF "ALTER_EFFECT" INT_VAR "check_globals" = 0 "resist_dispel" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Horn of Silence**
- Make sure all effects are dispellable / bypass Magic Resistance (so as to match the corresponding spell)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING - "misc3l.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "SILENCE_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "ALTER_EFFECT" INT_VAR "check_globals" = 0 "resist_dispel" = BIT0 BOR BIT1 END // MR already checked on the parent file
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**False Dawn**
- make sure it properly affects undead (undead are normally immune to confusion, so this spell is not working as expected...)
  - if EEex is not installed, abuse op182 to bypass/ignore op101 ("https://www.gibberlings3.net/forums/topic/35616-effect-immunities-on-the-ee-engine-aka-taking-full-advantage-of-the-ee-fixpack/page/2/#comment-313805")
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack\files\tph\luke\false_dawn.tph"
  //LAF "CLERIC_FALSE_DAWN" END
END

/*
luke
**Oil of Speed (cursed)**
- should display the feeblemind icon
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "potn23.itm" "override"
    LPF "DELETE_EFFECT" INT_VAR "match_opcode" = 142 "match_parameter2" = 3 END // Icon: Confused
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "FEEBLEMIND_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
    INNER_ACTION BEGIN
      COPY_EXISTING "%subspell%.spl" "override"
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 76 "opcode" = 142 "parameter1" = 0 "parameter2" = 48 "special" = 0 STR_VAR "resource" = "" "insert" = "below" END // portrait icon
      BUT_ONLY_IF_IT_CHANGES
    END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Moon Dog Howl**
- "eefixpack\files\tph\luke\7eyes\7eyes.tph" follow-up
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "spin891a.spl" "override" // panic subspell
    LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 177 "opcode" = 24 "parameter1" = 0 "parameter2" = 0 STR_VAR "match_resource" = "moondog" "resource" = "" END
    LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 177 "opcode" = 142 "parameter1" = 0 "parameter2" = 36 STR_VAR "match_resource" = "moond2" "resource" = "" END
    LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 177 "opcode" = 139 "parameter1" = 14007 "parameter2" = 0 STR_VAR "match_resource" = "moond1" "resource" = "" END
    LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 177 "opcode" = 215 "parameter1" = 0 "parameter2" = 1 STR_VAR "match_resource" = "moond4" "resource" = "cdhorror" END
    LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 24 "opcode" = 318 "parameter1" = IDS_OF_SYMBOL ("align" "MASK_EVIL") "parameter2" = 118 "timing" = 0 "duration" = 0 "special" = 0 STR_VAR "resource" = "%DEST_RES%" END
    LPF "DELETE_EFFECT" INT_VAR "match_target" = 3 END
  BUT_ONLY_IF_IT_CHANGES
END

/////                                                  \\\\\
///// projectile fixes                                 \\\\\
/////                                                  \\\\\

// fixed dragon breath projectiles
COPY_EXISTING ~dragblck.pro~ ~override~
              ~draggree.pro~ ~override~
              ~dragred.pro~  ~override~
              ~dragsilv.pro~ ~override~
  WRITE_SHORT 0x210 8

// grease projectiles created in spell section since vars needed

// luke
// Ice Storm should last 4 rounds (not 3)
WITH_SCOPE BEGIN
  COPY_EXISTING ~icestorm.pro~ ~override~
    WRITE_BYTE 0x216 4 // # repetitions
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Insect Plague**
// - make sure it affects up to 6 creatures (as per spell description)
WITH_SCOPE BEGIN
  COPY_EXISTING ~insec2.pro~ ~override~
    WRITE_SHORT 0x200 (THIS BOR BIT15) // Single target
    WRITE_SHORT 0x244 6 // # dice for multiple targets
    WRITE_SHORT 0x246 1 // Dice size for multiple targets
  BUT_ONLY_IF_IT_CHANGES
END

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

// consolidate bernard's store (see also bernard.bcs, bernard.dlg)
COPY_EXISTING ~bernard.sto~ ~override/bernard2.sto~

COPY_EXISTING ~type2.sto~ ~override~ //Adventurers' Mart (unused)
  REMOVE_STORE_ITEM ~scrl7a~ //Invalid item
  BUT_ONLY

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\

// illusionary creatures shouldn't save when dispelled
INCLUDE ~eefixpack/files/tph/illusionary_die.tph~

COPY ~music/vb.mus~ ~music/vb.mus~
  REPLACE_TEXTUALLY ~\(_H2[ %TAB%]+VB[ %TAB%]+\)_B2~ ~\1_B3~

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/scripting_states.tph~ // cleans up scripting states

// prevent spell called via op232 from interrupting caster sounds
INCLUDE ~eefixpack/files/tph/noisy_fireshields.tph~

INCLUDE ~eefixpack/files/tph/free_action.tph~ // free action swap from 126 > 337

INCLUDE ~eefixpack/files/tph/tbd_bg2ee_probabilities.tph~ // tbd, cam: probability fixes
INCLUDE "%MOD_FOLDER%/files/tph/dw_fixes.tph" // tbd, davidw:  318/324 immunity, 109/175/185 disentanglement, and more

// the 324 vs death immunity is being inserted between the 'destroyed' message and the actual destruction
// or, in a few cases, the order was just wrong
COPY_EXISTING ~ax1h10.itm~   ~override~ // azuredge
              ~blun12.itm~   ~override~ // mace o' disruption +1
              ~blun25.itm~   ~override~ // mace o' disruption +2
              ~dwfpdeva.spl~ ~override~ // used by deva.itm for, you guessed it, disruption
              ~dwfpohdv.spl~ ~override~ // used by ohbdeva.itm for, you guessed it, disruption
              ~hamm10.itm~   ~override~ // runehammer +4
              ~hamm11.itm~   ~override~ // runehammer +5
  LPF CLONE_EFFECT INT_VAR                  match_opcode = 177 STR_VAR match_resource = mesdie insert = last END // copy mesdie, die to last effects
  LPF CLONE_EFFECT INT_VAR                  match_opcode = 177 STR_VAR match_resource = die    insert = last END
  LPF DELETE_EFFECT INT_VAR multi_match = 1 match_opcode = 177 STR_VAR match_resource = mesdie               END // then delete the source of the copied effects
  LPF DELETE_EFFECT INT_VAR multi_match = 1 match_opcode = 177 STR_VAR match_resource = die                  END

WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/redundant_effects.tph"
  LAF "remove_redundant_effects" END
END

INCLUDE ~eefixpack/files/lib/cd_effect_batches_functions.tpa~         // function for effect batches
INCLUDE ~eefixpack/files/lib/cd_effect_batches_arrays_bg_bg2_iwd.tpa~ // array definitions for effect batches
INCLUDE ~eefixpack/files/tph/tbd_vfx_removal_bg2.tph~                 // use effect batches to remove vfx from effects which have been removed