/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

// string fixes, processed automatically if tra file exists
LAUNCH_ACTION_FUNCTION cd_string_set_from_tra
  STR_VAR input_tra  = EVAL ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tra~ // tra file to process
          output_tph = EVAL ~weidu_external/eefixpack/fixes_%game%.tph~ // tph being built; use weidu_external for this
END

/////                                                  \\\\\
///// ids/2da fixes                                    \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/a7/anim_3001.tph~ // fix missing creature animation definitions for Neothelid

/*
luke
**splprot.2da**
- the newly added (v2.6) header descriptions are incorrect for the alignment rows
*/

WITH_SCOPE BEGIN
  COPY_EXISTING "splprot.2da" "override"
    COUNT_2DA_COLS "cols"
    SET_2DA_ENTRY 33 0 "%cols%" "33_ALIGNMENTBITS=GOOD" // NOT BIT1 = Not NEUTRAL or EVIL
    SET_2DA_ENTRY 34 0 "%cols%" "34_ALIGNMENTBITS!=GOOD" // HAS BIT1 = Is NEUTRAL or EVIL
    SET_2DA_ENTRY 35 0 "%cols%" "35_ALIGNMENTBITS=NEUTRAL" // NOT BIT0 = Not GOOD or EVIL
    SET_2DA_ENTRY 36 0 "%cols%" "36_ALIGNMENTBITS!=NEUTRAL" // HAS BIT0 = Is GOOD or EVIL
    SET_2DA_ENTRY 37 0 "%cols%" "37_ALIGNMENTBITS=EVIL"
    SET_2DA_ENTRY 38 0 "%cols%" "38_ALIGNMENTBITS!=EVIL"
    SET_2DA_ENTRY 59 0 "%cols%" "59_ALIGNMENTBITS=LAWFUL" // NOT BIT5 = Not CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 60 0 "%cols%" "60_ALIGNMENTBITS!=LAWFUL" // HAS BIT5 = Is CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 61 0 "%cols%" "61_ALIGNMENTBITS=CHAOTIC"
    SET_2DA_ENTRY 62 0 "%cols%" "62_ALIGNMENTBITS!=CHAOTIC"
    SET_2DA_ENTRY 118 0 "%cols%" "118_ALIGNMENTBITS!=n" // Clearly state it is a bitwise check
    // Formatting
    PRETTY_PRINT_2DA
  BUT_ONLY_IF_IT_CHANGES
END

// tbd, davidw
// 7 eyes (eye of the sword shouldn't protect against stunning damage, which is only used for the hp backlash of the Enrage ability)
COPY_EXISTING ~7eyes.2da~ ~override~
  REPLACE_TEXTUALLY "12\*0x8000000" "*"
  PRETTY_PRINT_2DA



INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves



// tbd, cam
// regression: add full oiwd soundsets for iwdee now that engine fully supports them
ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/sounds/charstr.2da~ BEGIN
  INCLUDE ~eefixpack/files/tph/iwdee_soundsets.tph~
END
 
// CLEAR_IDS_MAP // reload ids, if we end up having any changes

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

COPY_EXISTING ~eefixpack/files/bam/ixbow04.bam~  ~override~ // tbd, cam: fix light crossbow BAM
              ~eefixpack/files/bam/ixbow09.bam~  ~override~ // tbd, cam: fix HQ light crossbow BAM
              ~eefixpack/files/wav/#choke.wav~   ~override~ // ie-6009, cam: missing sound for talonite poison
              ~eefixpack/files/2da/clabmo02.2da~ ~override~ // tbd, cam: borked kit ability table, dark moon monk
              ~eefixpack/files/2da/clabmo03.2da~ ~override~ // tbd, cam: borked kit ability table, sun soul monk
              ~eefixpack/files/bam/mdem~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, demogorgon
              ~eefixpack/files/bam/mdr1~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon red
              ~eefixpack/files/bam/mdr2~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon black
              ~eefixpack/files/bam/mdr3~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon silver
              ~eefixpack/files/bam/mwdr~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon white
              ~eefixpack/files/bam/mwyv~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, wyvern big

// tbd, sam.
// merging multi-part area animations to eliminate issues
ACTION_FOR_EACH file IN
  1000ms1 1105ms1 1201ms2 1400t001 1400t002 1400t003 1400t004 1400t005 3001ms1 3601ms1 
  6013ms1 am0602c am0602d am0602k1 am0602k2 am0602l1 am0602l2 am0801b1 am0801b2 am0902l 
  am0902l1 am0902l2 am0902l3 am0902l4 am2000a am2000a1 am2000a2 am2000a3 am2000a4 am2000b 
  am2000b1 am2000b2 am2000b3 am2000b4 am2805b am2805c am414e11 am414e12 
BEGIN  
  COPY ~eefixpack/files/bam/area_anim/%file%.bam~ ~override~
END  

// there's another section of INCLUDEs at the bottom if it should be late in the process
INCLUDE ~eefixpack/files/tph/5943_mold_touch.tph~      // ie-5943 , cam: lots of mold touch fixes
INCLUDE ~eefixpack/files/tph/6018_lizardmen.tph~       // ie-6018 , cam: Summoned lizardmen lack visible weapons
INCLUDE ~eefixpack/files/tph/6020_bullets.tph~         // ie-6020 , cam: Bullets +1 and Bullets +2 use the same projectile
INCLUDE ~eefixpack/files/tph/6036_clostsbfbhnaeig.tph~ // ie-6036 , cam: Cam's List o' Stuff That Should Be Fixed But Has No Actual Effect In Game

/////                                                  \\\\\
///// mechanics fixes / overhauls                      \\\\\
/////                                                  \\\\\

// kjeron
WITH_SCOPE BEGIN
  WITH_TRA "eefixpack/languages/en_us/luke/polymorph_overhaul.tra" "eefixpack/languages/%LANGUAGE%/luke/polymorph_overhaul.tra" BEGIN
    INCLUDE "eefixpack/files/tph/luke/polymorph_overhaul.tph"
    LAF "POLYMORPH_OVERHAUL" END
  END
END

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\
  
// tbd, cam
// player ai checks if magical weapon present before casting shillelagh, but checks old resources
COPY ~scripts/cleric1.bs~ ~scripts/cleric1.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!HasItem("SGRASP",Myself)~ // shocking grasp just does direct damage in iwdee, no item
      ~!HasItem("bclaw",Myself) !HasItem("moonbla",Myself) !HasItem("smcudge",Myself)~ // change instead to druid spells: beast claw, moonblade, star metal cudgel
    REPLACE_TEXTUALLY ~!HasItem("shille",Myself)~ ~!HasItem("shillel",Myself)~ // wrong resref for shillelagh
    REPLACE_TEXTUALLY ~!HasItem("SHAMMR",Myself)~ ~!HasItem("shamme1",Myself) !HasItem("shamme2",Myself) !HasItem("shamme3",Myself)~ // wrong resrefs for spiritual hammer
    REPLACE_TEXTUALLY ~!HasItem("chillt",Myself)~ ~!HasItem("ctouch",Myself)~ // wrong resref for chill touch
  END
  BUT_ONLY
  
  
  
// tbd, cam
// checking for ambients which don't exist
COPY_EXISTING ~eepoqct1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AmbientActivate("1105MS1[0-7]",FALSE)~ ~~ 
  END
  BUT_ONLY
  
  
  
// ie-6015, cam
// Jorn should heal all fellow barbarians (allies are BARBARIAN_1 to 8, script checks 0-7)
COPY_EXISTING ~iljorn.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~BARBARIAN_0~ ~BARBARIAN_8~ 
  END
  BUT_ONLY
  
  
  
// ie-6014, cam
// Loot should have charges: items from random treasure table with differing charges need to be re-added with charges
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_rndtres_charges BEGIN
  wandtrp, 5, "Uligar"           => ar2004 // orctres
  arowtrn, 8, "Pileo'Stuff"      => ar4003 // de3tres
  trnbolt, 8, "Pileo'Stuff"      => ar4003 // de3tres
  boneam,  1, "SerratedSkeleton" => ar5003 // sh1tres
END

ACTION_PHP_EACH cd_rndtres_charges AS params => area BEGIN
  EXTEND_BOTTOM ~%area%.bcs~ ~eefixpack/files/baf/rndtres_charges.baf~ EVALUATE_BUFFER
END
  
  
  
// ie-6006, cam
// Talonite Priests don't heal one another
COPY_EXISTING ~d2talon3.bcs~ ~override~
              ~d2talon4.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~TalonitePriest[14]~    ~Talonite~
    REPLACE_TEXTUALLY ~TalonitePriestess[23]~ ~Talonite~
  END
  BUT_ONLY 

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\
  
// ie-6016, cam
// Untrapped doors shouldn't show as trapped
COPY_EXISTING ~ar7000.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_detectable = 0 STR_VAR door_name = AR7000Door1 END
  BUT_ONLY  
  
COPY_EXISTING ~ar7002.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_detectable = 0 STR_VAR door_name = AR7002Door1 END
  BUT_ONLY
  
  
  
// ie-6017, cam
// Wrong random treasures in HoW
COPY_EXISTING ~ar9200.are~ ~override~ // swap back the old extres4 dupe fix
  LPF REPLACE_AREA_ITEM INT_VAR charges1 = 3 charges2 = 0 charges3 = 0 STR_VAR old_item = rndtre75 new_item = rndtre78 END // 3/0/0 charges for ogien/cloakin
  BUT_ONLY
  
COPY_EXISTING ~ar9601.are~ ~override~ // swap back the old extres4 dupe fix
  LPF REPLACE_AREA_ITEM INT_VAR charges1 = 1 charges2 = 1 charges3 = 0 STR_VAR old_item = rndtre78 new_item = rndtre75 END // 1/1/0 charges for beltbon/helmsh/stomper  
  BUT_ONLY
  
  
  
// ie-6013, cam
// Harpies should despawn for lower level parties
COPY_EXISTING ~ar9706.are~ ~override~ // only two harpy fiends should go away at CheckPartyAverageLevel(14,LESS_THAN)
  SET harpy = 0
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x00 + (index * 0x110)) name 
    PATCH_IF ("%name%" STRING_COMPARE_CASE "HrpFnd_0" = 0) BEGIN
      WRITE_ASCIIE (actor_off + 0x00 + (index * 0x110)) ~HrpFnd_%harpy%~
      SET harpy += 1
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\
  
// ie-6035, cam
// Why are constructs carrying gold?
COPY_EXISTING ~biknight.cre~ ~override~ // black ice knight
  WRITE_LONG 0x1C 0 // gold
  BUT_ONLY

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_iwdee_146.tph~ // tbd, cam: fixes items that cast spells
            
// tbd, cam
// upgraded bren muller's crossbow needs feedback that effect was activated (copy bg2 Heartseeker's visuals)
COPY_EXISTING ~cdxbowbm.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode =  61 target = 1 parameter1 = ((0 << 24) + (30 << 16) + (120 << 8)) parameter2 = (25 << 16) timing = 1 END // rgb fade
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 141 target = 1 parameter2 = 24 timing = 1 END // rgb fade
  BUT_ONLY
  
  
            
// ie-5937, cam
// darts of bone shouldn't have a saving throw on their APR boost
COPY_EXISTING ~dobone.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 1 savingthrow = 0 END
  BUT_ONLY
  
  
  
// ie-6026, cam - see also string updates to 14913 and 21627
// Revert changes to Thrym Extract and War Hammer +4: Defender (had wrong effects which were documented as if intended)
COPY_EXISTING ~thrym.itm~ ~override~ // should block effects on failed save
  LPF CLONE_EFFECT INT_VAR match_opcode = 12 opcode = 318 parameter1 = 0 parameter2 = 0 timing = 0 duration = 6 dicesize = 0 dicenumber = 0 STR_VAR insert = below resource = thrym END 
  BUT_ONLY

COPY_EXISTING ~u1ham5b.itm~  ~override~ // war hammer +4: defender: 20% heal
  LPF DELETE_EFFECT INT_VAR check_globals = 0 END // delete random heal, visuals
  BUT_ONLY
  
  
  
// ie-6025, cam
// Items blocking spells that they shouldn't
COPY_EXISTING ~gvalor1.itm~ ~override~ // gauntlets of valor block haste, but should be blocking hold person
  LPF ALTER_EFFECT STR_VAR match_resource = spwi305 resource = spwi306 END 
  BUT_ONLY
  
COPY_EXISTING ~kinetic.itm~ ~override~ // lance of disruption confers immunity to lance of disruption, but doesn't need it (original item bug fixed, this 318 was a stopgap)
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 STR_VAR match_resource = spwi328 END
  BUT_ONLY
  
  
  
// ie-6024, cam - see also strings 14432 and 18095
// Two crossbows have the wrong strength requirement
COPY_EXISTING ~uhxbw2a.itm~ ~override~ // finest heavy crossbow
              ~xbow01.itm~  ~override~ // heavy crossbow
  WRITE_SHORT 0x26 11 // min str
  BUT_ONLY
  
  
  
// ie-6023, cam
// Weapon effect duration fixes
COPY_EXISTING ~dart03.itm~ ~override~ // dart of stunning: color glow expires early
  LPF ALTER_EFFECT INT_VAR match_duration = 38 duration = 42 END
  BUT_ONLY

COPY_EXISTING ~ghoult.itm~ ~override~ // ghoul touch should last six rounds
  LPF ALTER_EFFECT INT_VAR match_duration = 25 duration = 36 END // bump up to six rounds
  BUT_ONLY
  
  
  
// ie-6019, cam
// Portrait icon fixes for items
COPY_EXISTING ~vexed.itm~ ~override~ // vexed armor: has 2x cold resist icon, one should be cursed instead  
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 25 parameter2 = 35 multi_match = 1 END
  BUT_ONLY

COPY_EXISTING ~bootfox.itm~ ~override~ // boots of the fox: swap haste icon for movement speed increase
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 38 parameter2 = 193 END
  BUT_ONLY
  
COPY_EXISTING ~moonbla.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 60 opcode = 142 parameter2 = 105 END
  BUT_ONLY
  
  
  
// ie-6007, cam - see also strings 8663, 8712, 8911, 40639, 25818, and 27373
// Weapon damage corrections
COPY_EXISTING ~clbhand.itm~ ~override~ // composite long bow of the hand
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 3 END // missing +1 from being composite long bow
  BUT_ONLY
  
COPY_EXISTING ~kaybow.itm~ ~override~ // kaylessa's composite long bow of the hand
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 4 END // missing +1 from being composite long bow
  BUT_ONLY
  
COPY_EXISTING ~lxbowbm.itm~  ~override~ // bren muller's crossbow
              ~cdxbowbm.itm~ ~override~ // bren muller's crossbow, upgraded
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 3 END // 1 damage short for +3 lt xbow
  BUT_ONLY
  
COPY_EXISTING ~storm.itm~ ~override~ // storm bow
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 2 END // as a short bow, shouldn't get long bow's +1 damage
  BUT_ONLY
  
COPY_EXISTING ~flailsk.itm~ ~override~ // skullflail
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 1 damage_bonus = 5 END // is +4 weapon, base flail damage is 1d6+1, so damage should be 1d6+5
  BUT_ONLY
  
  
// tbd, cam
// sword of days doesn't block all haste/slow spells
COPY_EXISTING ~days.itm~ ~override~ // round out slow/haste immunity; david's stuff will add the immunity spellstates
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi305 resource = spra301 insert = below END // clone haste to ranger haste
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spin146 insert = below END // clone to beholder slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spin977 insert = below END // clone to golem slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spwish25 insert = below END // clone to wish slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spwm164 insert = below END // clone to wild surge slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 101 parameter2 = 40 STR_VAR match_resource = spwi312 resource = ~~ END // immune to slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 101 parameter2 = 16 STR_VAR match_resource = spwi312 resource = ~~ END // immune to haste
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 267 parameter1= 14023 parameter2 = 0 STR_VAR match_resource = spwi312 resource = ~~ END // immune to string hasted
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 267 match_parameter1= 14023 parameter1 = 14000 END // immune to string slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 267 match_parameter1= 14023 parameter1 = 14668 END // immune to string slowed
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 169 parameter1= 0 parameter2 = 38 STR_VAR match_resource = spwi312 resource = ~~ END // immune to icon haste
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 169 match_parameter2= 38 parameter2 = 41 END // immune to icon slow

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

// ie-6014, cam
// spells schooled as 'generalist' instead of 'none' incorrectly invoke save bonuses/penalties
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_spell_schools BEGIN

  ~#beltyn~  => 7 // generalist > necromancy (beltyn's burning blood)
  ~#bonecir~ => 7 // generalist > necromancy (circle of bones)
  ~#cureslp~ => 0 // generalist > none (unused)
  ~#curestn~ => 0 // generalist > none (unused)
  ~#deathf1~ => 6 // generalist > invoker (death fog, trap)
  ~#deathf2~ => 6 // generalist > invoker (death fog, trap)
  ~#deathf3~ => 6 // generalist > invoker (death fog, trap)
  ~#jedslp~  => 0 // generalist > none (jed passes out)
  ~#rw_17~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_18~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_19~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_33~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_34~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_35~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_49~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_50~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_51~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#rw_ex~   => 4 // generalist > enchanter (old Righteous Wrath of the Faithful)
  ~#shrouda~ => 6 // generalist > invoker (old shroud of flmae)
  ~#shroudb~ => 6 // generalist > invoker (old shroud of flmae)
  ~#sunrund~ => 0 // generalist > none (unk)
  ~#vitarea~ => 2 // generalist > conjurer (old vitriolic sphere)
  ~blindimm~ => 0 // generalist > none (old blindness immunity)
  ~deafimm~  => 0 // generalist > none (old deafness immunity)
  ~drgrbrht~ => 0 // generalist > none (leftover green dragon breath)
  ~firau1d6~ => 0 // generalist > none (salamander aura)
  ~firau3d6~ => 0 // generalist > none (salamander aura)
  ~frostd10~ => 0 // generalist > none (frost salamander aura)
  ~frostd6~  => 0 // generalist > none (frost salamander aura)
  ~gasp~     => 0 // generalist > none (unk)
  ~holdfst~  => 0 // generalist > none (old holdfast arrows)
  ~spcl101~  => 0 // generalist > none (DRUID_SHAPESHIFT_POLAR_BEAR)
  ~spcl107~  => 0 // generalist > none (DRUID_SHAPESHIFT_WINTER_WOLF)
  ~spcl108~  => 0 // generalist > none (DRUID_SHAPESHIFT_BORING_BEETLE)
  ~spcl109~  => 3 // generalist > divination (PALADIN_DETECT_EVIL)
  ~spcl111~  => 1 // generalist > abjuration (PALADIN_CURE_DISEASE)
  ~spcl112~  => 0 // generalist > none (DRUID_SHAPESHIFT_ELEMENTAL_FIRE)
  ~spcl113~  => 0 // generalist > none (DRUID_SHAPESHIFT_ELEMENTAL_EARTH)
  ~spcl114~  => 0 // generalist > none (DRUID_SHAPESHIFT_ELEMENTAL_WATER)
  ~spcl212~  => 1 // generalist > abjuration (PALADIN_PROTECTION_FROM_EVIL)
  ~spcl213~  => 1 // generalist > abjuration (protection from evil, unused)
  ~spcl311~  => 4 // generalist > enchantment (RANGER_CHARM_ANIMAL)
  ~spin114~  => 0 // generalist > none (INNATE_WIND_WALK)
  ~spin115~  => 0 // generalist > none (INNATE_MOURNFUL_WAIL)
  ~spin116~  => 0 // generalist > none (INNATE_DEATH_KNELL)
  ~spin117~  => 0 // generalist > none (INNATE_WAR_CRY)
  ~spin118~  => 0 // generalist > none (INNATE_UNDYING_LAMENT)
  ~spin121~  => 0 // generalist > none (INNATE_SHAPESHIFTS_WEREWOLF)
  ~spin125~  => 0 // generalist > none (INNATE_WEREWOLF_DEATH)
  ~spin126~  => 0 // generalist > none (INNATE_WYLFDENES_DEATH_ANIMATION)
  ~spin127~  => 0 // generalist > none (INNATE_ICE_GOLEM_DISAPPEAR)
  ~spin128~  => 0 // generalist > none (INNATE_FROST_AURA)
  ~spin129~  => 0 // generalist > none (INNATE_SEER_TELEPORT)
  ~spin130~  => 0 // generalist > none (INNATE_BLUE_GLOW)
  ~spin131~  => 0 // generalist > none (INNATE_DRAGON_DEATH_ANIMATION)
  ~spin132~  => 0 // generalist > none (INNATE_DRAGON_WING_BUFFET)
  ~spin133~  => 0 // generalist > none (INNATE_DRAGON_GEM_EFFECTS)
  ~spin134~  => 0 // generalist > none (INNATE_DRAGON_BREATH)
  ~spin135~  => 0 // generalist > none (INNATE_DRAGON_GEM_SUMMONING)
  ~spin136~  => 0 // generalist > none (INNATE_BEHOLDER_UNUSED)
  ~spin138~  => 0 // generalist > none (INNATE_HARPY_WAIL)
  ~spin139~  => 0 // generalist > none (INNATE_HARPY_WAIL_INTERNAL)
  ~spin149~  => 0 // generalist > none (INNATE_POLYMORPH_TO_DOG)
  ~spin162~  => 0 // generalist > none (INNATE_HOBART_TO_RAKSHASA)
  ~spin164~  => 0 // generalist > none (INNATE_MUSTARD_JELLY_VAPOR)
  ~spin166~  => 0 // generalist > none (INNATE_CONTAINER_GLOW)
  ~spin167~  => 0 // generalist > none (INNATE_CONTAINER_GLOW_BAD)
  ~spin168~  => 0 // generalist > none (INNATE_CORNUGON_SUMMON_TRAVEL_ANIMATION)
  ~spin169~  => 0 // generalist > none (INNATE_WINTER_WOLF_BREATH)
  ~spin170~  => 0 // generalist > none (INNATE_REMOVE_TERIKANS_ITEM_ANIMATION)
  ~spin171~  => 0 // generalist > none (INNATE_BELHIFETS_DEATH_ANIMATION)
  ~spin172~  => 0 // generalist > none (INNATE_EVERARDS_PORTAL_CLOSING)
  ~spin176~  => 0 // generalist > none (INNATE_INFERNAL_CONVEYANCE)
  ~spin177~  => 0 // generalist > none (INNATE_POQUELINS_PORTAL_OPENING)
  ~spin180~  => 0 // generalist > none (INNATE_INCENDIARY_CLOUD_IDLING)
  ~spin181~  => 0 // generalist > none (INNATE_INCENDIARY_CLOUD_EXPLOSION)
  ~spin182~  => 0 // generalist > none (INNATE_FLASH_DESTROY_SUMMON_YXUNOMEI)
  ~spin183~  => 0 // generalist > none (INNATE_FLASH_DESTROY_SUMMON_BELHIFET)
  ~spin184~  => 0 // generalist > none (INNATE_FLASH_DISAPPEAR)
  ~spin186~  => 0 // generalist > none (INNATE_PHASE_SPIDER_TELEPORT)
  ~spin187~  => 0 // generalist > none (INNATE_SALAMANDER_AURA_SHIKATA)
  ~spin188~  => 0 // generalist > none (INNATE_MYCONID_SPORES)
  ~spin189~  => 0 // generalist > none (INNATE_IRON_GOLEM_CLOUD)
  ~spin190~  => 0 // generalist > none (INNATE_ZOMBIE_LORD_AURA)
  ~spin192~  => 0 // generalist > none (INNATE_UMBER_HULK_GAZE)
  ~spin193~  => 0 // generalist > none (INNATE_SALAMANDER_AURA_FROST)
  ~spin194~  => 0 // generalist > none (INNATE_SALAMANDER_AURA_FIRE)
  ~sppr603d~ => 6 // generalist > invoker (CLERIC_BLADE_BARRIER subspell)
  ~spwi480~  => 0 // generalist > none (polymorph self > boring beetle)
  ~spwi481~  => 0 // generalist > none (polymorph self > polar bear)
  ~spwi482~  => 0 // generalist > none (polymorph self > winter wolf)
  ~talongf~  => 0 // generalist > none (unused talon of the gloomfrost)
  ~umbergaz~ => 0 // generalist > none (umberhulk gaze)

END

ACTION_PHP_EACH cd_spell_schools AS spell => school BEGIN
  
  COPY_EXISTING ~%spell%.spl~ ~override~
    WRITE_BYTE 0x25 school
    BUT_ONLY 
  
END

INCLUDE ~eefixpack/files/tph/5919_iwdee_spider_spawn.tph~ // ie-5919, cam: spider spawn only summons giant spiders, should also summon phase and sword



// tbd, cam (from jmerry)
// simulacrum/projected images should have thief skills, but no shadow twin
INCLUDE ~eefixpack/files/tph/tbd_simulacrum.tph~


// ie-5938, cam
// Lich Touch should paralyze, not hold
COPY_EXISTING ~ltouch.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 175 opcode = 109 END  
  BUT_ONLY
     



// tbd, davidw
// circle of bone subspell has illegal character in sig
 COPY_EXISTING "#bonecir.spl" override WRITE_ASCII 0x0 "SPL "

// tbd, cam
// crippling strike should extend t0 -10 for assassins
COPY_EXISTING ~backstab.spl~ ~override~
  READ_SHORT 0x68 abil_num_orig
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 420 step_dur = 0 level_cap = 11 END
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = abil_num_orig ; index < abil_num ; ++index) BEGIN
    READ_SHORT (abil_off + 0x1e + (index * 0x28)) abil_fx_num
    READ_SHORT (abil_off + 0x20 + (index * 0x28)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off + 0x00 + ((index2 + abil_fx_idx) * 0x30)) op
      PATCH_IF ((op = 54) OR (op = 73)) BEGIN
        WRITE_LONG (fx_off + 0x04 + ((index2 + abil_fx_idx) * 0x30)) (0 - index) // penalty
        WRITE_LONG (fx_off + 0x0e + ((index2 + abil_fx_idx) * 0x30)) 70          // duration
      END
    END      
  END
  BUT_ONLY  
  
// tbd, cam
// nymphs from 'call woodland beings' lack their confusion ability due to a spell not being ported
APPEND ~spell.ids~ ~3704 NYMPH_CONFUSION~
COPY ~eefixpack/files/spl/spin704.spl~ ~override~
  SPRINT NYMPH_CONFUSION ~spin704~
  
  
  
// tbd, davidw
// The projectile for Bombardier Beetle clouds has a duplicate string
COPY_EXISTING ~%INNATE_BOMBARDIER_BEETLE_CLOUD%.spl~ ~override~ // spin191
  LPF DELETE_EFFECT INT_VAR match_opcode=139 match_parameter1=35568 multi_match=1 END // has two 'stunned' strings
  BUT_ONLY
 
 
 
// tbd, davidw
// entropy shield (remove spurious protection from Breach/Spellstrike/Imprisonment; doesn't protect against Flame Strike)
COPY_EXISTING "%CLERIC_ENTROPY_SHIELD%.spl" ~override~ // sppr615
  LPF DELETE_EFFECT INT_VAR match_opcode=83 match_parameter2=54 END // remove immunity to SPARKLGR projectile
  PATCH_FOR_EACH resource IN sppr503 spimix01 ohbeflam BEGIN // clone one flamestrike immunity to cover other flamestrike variants
    LPF CLONE_EFFECT STR_VAR match_resource=sppr984 resource END      
  END
  BUT_ONLY



// tbd, davidw
// Storm Shell shouldn't stack with itself
COPY_EXISTING ~%CLERIC_STORM_SHELL%.spl~ ~override~ // sppr327
  LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 timing=0 resist_dispel=3 STR_VAR resource="%CLERIC_STORM_SHELL%" END
  LPF ALTER_EFFECT INT_VAR target=1 END // for some subtle reason, ability_target=5 and target=2 doesn't play nice with the 321 trick
  BUT_ONLY



// tbd, davidw 
// Mind Blank is supposed to protect against petrification
COPY_EXISTING ~%WIZARD_MIND_BLANK%.spl~ ~override~ // spwi802
  LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=175 parameter2=134 END // immune to petrification opcode
  LPF CLONE_EFFECT INT_VAR match_opcode=267 match_parameter1=14102 parameter1=14127 END // immune to petrification string
  LPF CLONE_EFFECT INT_VAR match_opcode=169 match_parameter2=3 parameter2=171 END // immune to petrification icon
  BUT_ONLY 
 
 
 
// ie-5939, cam
// Great Shout duplicates combat feedback on a failed save
COPY_EXISTING ~%WIZARD_GREAT_SHOUT%.spl~ ~override~ // spwi806 great shout
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 139 match_target = 2 match_savingthrow = BIT0 END // nuke dupe 'stunned' string
  LPF CLONE_EFFECT INT_VAR match_opcode =  142 match_duration = 12 opcode = 206 parameter1 = 0 parameter2 = 0 duration = 1 STR_VAR resource = EVAL ~%WIZARD_GREAT_SHOUT%~ insert = below END 
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 174 target = 1 power = 8 timing = 1 resist_dispel = 2 STR_VAR resource = ~#ff_m101~ END
  BUT_ONLY
 
 
 
// ie-5940, cam
// Emotion, Fear bypasses save/MR checks for removing Emotion, Courage
COPY_EXISTING ~%WIZARD_EMOTION_FEAR%.spl~ ~override~ // spwi428
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 resist_dispel = 1 savingthrow = BIT0 END



// tbd, cam
// iwdee fix: deafness from (great) shout not including portrait icon
COPY_EXISTING ~%WIZARD_GREAT_SHOUT%.spl~ ~override~ // spwi806
              ~%WIZARD_SHOUT%.spl~       ~override~ // spwi431
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 80 opcode = 142 parameter2 = 112 END // clone deaf into deafness icon



// ie-5941, 1i-6031, cam
// iwdee: not always checking MR correctly  
COPY_EXISTING ~%WIZARD_BELTYNS_BURNING_BLOOD%.spl~ ~override~ // spwi422
  PATCH_FOR_EACH op IN 61 142 174 215 321 333 BEGIN // everything but the portrait icon is wrong except at header 0
  LPF ALTER_EFFECT INT_VAR match_opcode = op resist_dispel = 1 END
  END  



// tbd, cam
// iwdee bug: use 176 for movement speed, 321 needs save  
COPY_EXISTING ~%WIZARD_SUFFOCATE%.spl~ ~override~ // spwi726
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 savingthrow = BIT0 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 126 opcode = 176 END



// tbd, cam
// stun icons provided by stun effect, remove redundant 142  
COPY_EXISTING ~%WIZARD_GREAT_SHOUT%.spl~ ~override~ // spwi806
              ~%WIZARD_ICELANCE%.spl~    ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 55 END 



// ie-5753, cam
// iwdee bug: blood rage can avoid fatigue with a re-cast - clone 321 into 206 then delete it; also block ops 18 and 98
COPY_EXISTING ~%CLERIC_BLOOD_RAGE%.spl~  ~override~ // sppr422
  LPF CLONE_EFFECT  INT_VAR match_opcode = 101 match_parameter2 = 17 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 17 multi_match = 1 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 101 match_parameter2 = 17 parameter2 = 18 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 101 match_parameter2 = 17 parameter2 = 98 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 321 opcode = 206 timing = 0 duration = 120 STR_VAR match_resource = EVAL ~%CLERIC_BLOOD_RAGE%~ insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = EVAL ~%CLERIC_BLOOD_RAGE%~ END



// ie-5944, cam
// Animal Rage needs power level corrections
COPY_EXISTING ~%CLERIC_ANIMAL_RAGE%.spl~  ~override~ // sppr522
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 match_timing = 4 END

COPY_EXISTING ~%CLERIC_ANIMAL_RAGE%b.spl~  ~override~ // sppr522b
  LPF ALTER_EFFECT INT_VAR power = 0 END



// ie-5945, cam
// Static Charge icon should be dispellable like the rest of the spell
COPY_EXISTING ~%CLERIC_STATIC_CHARGE%.spl~  ~override~ // sppr420
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 resist_dispel = 3 END
  
  
  
// tbd, cam
// oiwd fix
COPY_EXISTING ~%CLERIC_WHIRLWIND%.spl~  ~override~ // sppr617
  LPF ALTER_EFFECT INT_VAR match_resist_dispel = 0 resist_dispel = 1 END



// tbd, cam
// iwdee fix: dupe 'unconscious' strings, remove redundant 318 at end
COPY_EXISTING ~%CLERIC_SMASHING_WAVE%.spl~ ~override~ // sppr426
  LPF DELETE_EFFECT INT_VAR match_opcode = 139 match_duration = 12 END 
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter2 = 0 END 



// tbd, cam  
// iwdee fix: weird durations for 318/324s
COPY_EXISTING ~%CLERIC_CLOUD_OF_PESTILENCE%.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 85 duration = 0 END 



// ie-5946, cam
// Wall of Moonlight should not interact with spell protections
COPY_EXISTING ~%CLERIC_WALL_OF_MOONLIGHT%.spl~ ~override~ // sppr428
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
  
COPY_EXISTING ~%CLERIC_WALL_OF_MOONLIGHT%a.spl~ ~override~ // sppr428a
              ~%CLERIC_WALL_OF_MOONLIGHT%b.spl~ ~override~ // sppr428b
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 power = 0 resist_dispel = 0 END



// ie-5947, cam
// Cloudburst should remove Fireshields
COPY_EXISTING ~%CLERIC_CLOUDBURST%.spl~ ~override~ // sppr325
  LPF CLONE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = firau1d6 resource = spwi403 END // fireshield blue
  LPF CLONE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = firau1d6 resource = spwi418 END // fireshield red
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = firau1d2 resource = sppr730d END // immune to aura of flaming death damage



// tbd, cam
// IWDEE bug 321 should be timing 1/dur 0
COPY_EXISTING ~%CLERIC_SPIKE_STONES%.spl~ ~override~ // sppr519
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 timing = 1 duration = 0 END



// tbd, cam
// IWDEE bug 321 should be subject to same MR check as AC effect
COPY_EXISTING ~%CLERIC_ALICORN_LANCE%.spl~ ~override~ // sppr218
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 resist_dispel = 1 END



// tbd, cam
// stun icons provided by stun effect in EE, remove redundant 142  
COPY_EXISTING ~%CLERIC_SMASHING_WAVE%.spl~ ~override~ // sppr426
              ~%CLERIC_WHIRLWIND%.spl~     ~override~ // sppr617
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 55 END 



// ie-5942, cam
// Mass Cause Light Wounds should not be available to druids
COPY_EXISTING ~%CLERIC_MASS_CAUSE_LIGHT_WOUNDS%.spl~ ~override~ // sppr523.spl
  WRITE_LONG 0x1e (THIS BOR BIT31) // adds druid flag
  
  
  
// ie-6033, cam
// Great Roar has inconsistent durations
COPY_EXISTING ~%INNATE_GREAT_ROAR%.spl~ ~override~ // spin119 great roar
  LPF ALTER_EFFECT INT_VAR match_duration = 120 duration = 30 END
  BUT_ONLY
  
  
  
// ie-6032, cam
// Tenser's Transformation shouldn't block innate abilities
COPY_EXISTING ~%WIZARD_TENSERS_TRANSFORMATION%.spl~ ~override~ // spwi603
  LPF ALTER_EFFECT INT_VAR match_opcode = 145 match_parameter2 = 2 parameter2 = 3 END
  BUT_ONLY
  
  
  
// ie-6030, cam
// Stoneskin provides too many skins at all levels
COPY_EXISTING ~%WIZARD_STONE_SKIN%.spl~ ~override~ // spwi408  
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 218 parameter1 = (index + 3) END
  END
  BUT_ONLY
  
  
  
// ie-6029, cam
// Hopelessness not always canceling Hope
COPY_EXISTING ~%CLERIC_SYMBOL_OF_HOPELESSNESS%.spl~ ~override~ // sppr716 Symbol: Hopelessness
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 139 opcode = 321 parameter1 = 0 parameter2 = 0 STR_VAR resource = spwi429 END // remove Emotion: Hope
  BUT_ONLY

COPY_EXISTING ~%INNATE_MOURNFUL_WAIL%.spl~ ~override~ // spin115 Mournful Wail; do separately since it has a 206 at top of stack
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 probability1 = 100 END // play on all stuns
  LPF CLONE_EFFECT INT_VAR match_opcode = 139 opcode = 321 STR_VAR resource = spwi429 END // remove Emotion: Hope
  BUT_ONLY
  
  
  
// ie-6028, cam
// Charm via Mantle of Hell's Furnace does not work as expected or, really, at all
COPY_EXISTING ~spitm99.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 5 parameter2 = 1 END // change to normal hostile charm
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 2 parameter1 = 187 parameter2 = 115 duration = 1 STR_VAR resource = spitm99 END // block class != fire_elemental
  BUT_ONLY
  
  
  
// ie-6027, cam
// HP draining spells shouldn't work on undead and extraplanar creatures
COPY_EXISTING ~%MONK_LAY_ON_HANDS%.spl~ ~override~ // spcl815 lay on hands (monk)
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 2 parameter2 = 55 duration = 1 STR_VAR resource = spcl815 END

COPY_EXISTING ~%MONK_LAY_ON_HANDS%.spl~          ~override~ // spcl815 lay on hands (monk)
              ~%WIZARD_LARLOCH_MINOR_DRAIN%.spl~ ~override~ // spwi119 larloch's minor drain
              ~%WIZARD_VAMPIRIC_TOUCH%.spl~      ~override~ // spwi314 vampiric touch
  PATCH_FOR_EACH race IN 121 139 145 147 153 156 157 158 159 169 BEGIN // demonic mephit elemental genie tiefling (anti)solar (dark)planetar salamander
    LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 55 parameter1 = race parameter2 = 104 END 
  END   
  BUT_ONLY



// luke
// 1) Grease should not interact with spell protections
// 2) Grease (in particular "spwi101b.spl" and "spwi101c.spl") should apply the GREASE stat.
///// As you can see, those two subspells are not applying the aforementioned stat, nor the GREASE spell state. This might confuse both later patches and AI.
///// As a result, I suggest replacing op215 with op158 (param2=1|custom overlay; resource="greaseb" on "spwi101b" and resource="greasec" on "spwi101c").
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_GREASE%b.spl~ ~override~ // spwi101b
                ~%WIZARD_GREASE%c.spl~ ~override~ // spwi101c
    LPF ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
    LPF ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 215 ~opcode~ = 158 END // Play visual effect => Grease overlay
  BUT_ONLY_IF_IT_CHANGES
END



// luke
// Chill Touch should correctly affect UNDEAD without permanently removing their immunity to Panic (op24)
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/wizard_chill_touch.tph"
  LAF "WIZARD_CHILL_TOUCH" END
END



// luke
// Spirit Fire => should not interact with spell protections
// As a result, do not use "bddoom.spl" (since its effects are coded as `power=1`)
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cleric_spirit_fire.tph"
  LAUNCH_ACTION_FUNCTION "CLERIC_SPIRIT_FIRE" END
END



// luke
// Nature's Beauty â€“ permanent blindness (until dispelled)
// 1) Timing Mode 1 will permanently set the creature's STATE_BLIND flag, leaving no removable effect.
// 2) Related: the -4 penalty to AC and THAC0 are apparently tied to the effect (op74), not STATE_BLIND.
// As a result, we suggest to stick with a limited `timing_mode` and set `duration` to a very high random value (max signed value...?).
// Additionally, this spell should only affect HUMANOID (there are a lot of unnatural creatures that are immune to it via op206; also, it is intended, right...?)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_NATURE_BEAUTY%.spl~ ~override~
    LPF "ADD_SPELL_EFFECT" INT_VAR "insert_point" = 0 "opcode" = 324 "target" = 2 "power" = 7 "parameter2" = 6 "resist_dispel" = BIT1 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (GENERAL != HUMANOID)
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 74 "timing" = 0 "duration" = 0x7FFFFFFF END // Blindness
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "match_opcode" = 142 END // op74 naturally provides the "Blind" portrait icon
  BUT_ONLY_IF_IT_CHANGES
END

// tbd, cam
// don't play berserk damage warnings on non-party memebers; see also spin117[ab].eff
INCLUDE ~eefixpack/files/tph/tbd_angry_noises.tph~

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\
            
// ie-6008, cam
// Worldmap travel fixes
COPY_EXISTING ~worldmap.wmp~ ~override~ // inconsistent travel times
  READ_LONG 0x0c mos_off
  READ_LONG (mos_off + 0x20) area_num
  READ_LONG (mos_off + 0x24) area_off
  READ_LONG (mos_off + 0x28) link_off
  FOR (index = 0 ; index < area_num ; ++index) BEGIN
    READ_ASCII (area_off + 0x08 + (index * 0xf0)) area
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar5000" = 0) BEGIN // severed hand
      SET hand = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar4000" = 0) BEGIN // dragon's eye
      SET deye = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar7000" = 0) BEGIN // wyrm's tooth
      SET wyrm = index
    END
  END
  FOR (index = 0 ; index < area_num ; ++index) BEGIN // loop through areas
    FOR (index2 = 0 ; index2 < 4 ; ++index2) BEGIN // loop through four sets of links (n,s,e,w)
      READ_LONG (area_off + 0x50 + (index2 * 0x08) + (index * 0xf0)) link_idx
      READ_LONG (area_off + 0x54 + (index2 * 0x08) + (index * 0xf0)) link_num
      FOR (index3 = 0 ; index3 < link_num ; ++index3) BEGIN // loop through actual links
        READ_LONG (link_off +        ((link_idx + index3) * 0xd8)) target
        PATCH_IF ((index = deye) AND (target = hand)) BEGIN // dragon's eye to severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 12 // travel time
        END ELSE  
        PATCH_IF ((index = wyrm) AND (target = hand)) BEGIN // wyrm's tooth to severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 72 // travel time
        END   
      END
    END
  END
  BUT_ONLY



// tbd, cam
// broken sound reference in lance of disruption projectile
COPY_EXISTING ~idpro313.pro~ ~override~
  WRITE_ASCII 0x18 ~#tra_59~ #8

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/6037_iwdee_probabilities.tph~ // ie-6021 & ie-6037, cam: probability fixes
INCLUDE "%MOD_FOLDER%/files/tph/dw_fixes.tph" // tbd, davidw:  318/324 immunity, 109/175/185 disentanglement, and more