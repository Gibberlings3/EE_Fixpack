/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~ BEGIN
  INCLUDE ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tph~
END

INCLUDE ~eefixpack/files/tph/launcher_damage_types.tph~ // many bows/xbows lack damage type in their description

/////                                                  \\\\\
///// lua fixes                                        \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/debug_area_listings.tph~ // correcting area names/styles in console menu

/////                                                  \\\\\
///// ids/2da/ini fixes                                \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/a7/action_changestoremarkup_fix.tph~ // fix parameter order in the ChangeStoreMarkup() script action
INCLUDE ~eefixpack/files/tph/a7/anim_3001.tph~ // fix missing creature animation definitions for Neothelid
INCLUDE ~eefixpack/files/tph/a7/cre_anim_fixes.tph~ // fix height offsets of health bar positions for creature animations
INCLUDE ~eefixpack/files/tph/animation_ini_fixes.tph~ // errors in animation definitions
INCLUDE ~eefixpack/files/tph/tbd_splprot_kitfix.tph~ // tbd, cam: fixing kit check to be a numerical equality check, not bitwise
INCLUDE ~eefixpack/files/tph/infravision.tph~ // correcting animations subject to infravision
INCLUDE ~eefixpack/files/tph/ids_sync.tph~ // sync race.ids, class.ids

// syncing damages.ids header between games (cosmetic)
COPY_EXISTING ~damages.ids~ ~override~
  REPLACE_TEXTUALLY ~^\(0x0001\)~ ~13%LNL%\1~ // bg2ee/iwdee

APPEND ~object.ids~
~57 EighthNearestEnemyOfType
67 EighthNearestMyGroupOfType
94 LastKilled
95 NearestAllyOf
96 SecondNearestAllyOf
97 ThirdNearestAllyOf
98 FourthNearestAllyOf
99 FifthNearestAllyOf
100 SixthNearestAllyOf
101 SeventhNearestAllyOf
102 EighthNearestAllyOf
103 NinthNearestAllyOf
104 TenthNearestAllyOf
105 FarthestEnemyOf
106 SecondFarthestEnemyOf
107 ThirdFarthestEnemyOf
108 FourthFarthestEnemyOf
109 FifthFarthestEnemyOf
110 SixthFarthestEnemyOf
111 SeventhFarthestEnemyOf
112 EighthFarthestEnemyOf
113 NinthFarthestEnemyOf
114 TenthFarthestEnemyOf~

// tbd, cam
// nymphs from 'call woodland beings' lack their confusion ability due to a spell not being ported
APPEND ~spell.ids~ ~3704 NYMPH_CONFUSION~
COPY ~eefixpack/files/spl/spin704.spl~ ~override~
  SPRINT NYMPH_CONFUSION ~spin704~

// tbd, cam
// fixing missing starting equipment in HoW games for shaman, others
COPY ~eefixpack/files/2da/25stweap.2da~ ~override~

ACTION_IF FILE_EXISTS ~eefixpack/languages/%LANGUAGE%/sounds/cd_ch6a.wav~ BEGIN
  INCLUDE ~eefixpack/files/tph/iwdee_alt_ch6.tph~ // alternatve ch 6 narration
END

/*
luke
**splprot.2da**
- the newly added (v2.6) header descriptions are incorrect for the alignment rows
*/

WITH_SCOPE BEGIN
  COPY_EXISTING "splprot.2da" "override"
    COUNT_2DA_COLS "cols"
    SET_2DA_ENTRY 33 0 "%cols%" "33_ALIGNMENTBITS=GOOD" // NOT BIT1 = Not NEUTRAL or EVIL
    SET_2DA_ENTRY 34 0 "%cols%" "34_ALIGNMENTBITS!=GOOD" // HAS BIT1 = Is NEUTRAL or EVIL
    SET_2DA_ENTRY 35 0 "%cols%" "35_ALIGNMENTBITS=NEUTRAL" // NOT BIT0 = Not GOOD or EVIL
    SET_2DA_ENTRY 36 0 "%cols%" "36_ALIGNMENTBITS!=NEUTRAL" // HAS BIT0 = Is GOOD or EVIL
    SET_2DA_ENTRY 37 0 "%cols%" "37_ALIGNMENTBITS=EVIL"
    SET_2DA_ENTRY 38 0 "%cols%" "38_ALIGNMENTBITS!=EVIL"
    SET_2DA_ENTRY 59 0 "%cols%" "59_ALIGNMENTBITS=LAWFUL" // NOT BIT5 = Not CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 60 0 "%cols%" "60_ALIGNMENTBITS!=LAWFUL" // HAS BIT5 = Is CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 61 0 "%cols%" "61_ALIGNMENTBITS=CHAOTIC"
    SET_2DA_ENTRY 62 0 "%cols%" "62_ALIGNMENTBITS!=CHAOTIC"
    SET_2DA_ENTRY 118 0 "%cols%" "118_ALIGNMENTBITS!=n" // Clearly state it is a bitwise check
    // Formatting
    PRETTY_PRINT_2DA
  BUT_ONLY_IF_IT_CHANGES
END

// tbd, davidw
// 7 eyes (eye of the sword shouldn't protect against stunning damage, which is only used for the hp backlash of the Enrage ability)
// luke (EYEMAGE should not absorb MAGIC damage, as per spell description)
COPY_EXISTING ~7eyes.2da~ ~override~
  REPLACE_TEXTUALLY "12\*0x8000000" "*"
  REPLACE_TEXTUALLY EXACT_MATCH "12*0x400000" "*"
  PRETTY_PRINT_2DA

INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves

// tbd, cam
// regression: add full oiwd soundsets for iwdee now that engine fully supports them
INCLUDE ~eefixpack/files/tph/iwdee_soundsets.tph~

// ar7004 has two entries; one is meant for ar7005
COPY_EXISTING ~tracking.2da~ ~override~
  REPLACE_TEXTUALLY ~AR7004[ %TAB%]+O_24861~ ~AR7005    O_24861~
  BUT_ONLY

 // tbd, graion: more consistent op5 setup, see header
INCLUDE ~eefixpack/files/tph/tbd_ea_reallycharmed.tph~

// ensure that ids tables are properly sorted
COPY_EXISTING_REGEXP ~^.+\.ids$~ ~override~
  LPF sort_ids END
BUT_ONLY

CLEAR_IDS_MAP // reload ids, if we end up having any changes

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

COPY ~eefixpack/files/bam/ixbow04.bam~      ~override~ // tbd, cam: fix light crossbow BAM
     ~eefixpack/files/bam/ixbow09.bam~      ~override~ // tbd, cam: fix HQ light crossbow BAM
     ~eefixpack/files/wav/#choke.wav~       ~override~ // ie-6009, cam: missing sound for talonite poison
     ~eefixpack/files/2da/clabmo02.2da~     ~override~ // tbd, cam: borked kit ability table, dark moon monk
     ~eefixpack/files/2da/clabmo03.2da~     ~override~ // tbd, cam: borked kit ability table, sun soul monk
     ~eefixpack/files/2da/clabrn02.2da~     ~override~ // tbd, cam: table isn't borked, but does lack the last +1 missile bump
     ~eefixpack/files/2da/spwi417.2da~      ~override~ // tbd, cam: allow enchant weapon to be cast via contingency
     ~eefixpack/files/bam/mdem~             ~override~ // tbd, sam.: merging four-frame animations to fix issues, demogorgon
     ~eefixpack/files/bam/mdr1~             ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon red
     ~eefixpack/files/bam/mdr2~             ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon black
     ~eefixpack/files/bam/mdr3~             ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon silver
     ~eefixpack/files/bam/mwdr~             ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon white
     ~eefixpack/files/bam/mwyv~             ~override~ // tbd, sam.: merging four-frame animations to fix issues, wyvern big - also add shadows
     ~eefixpack/files/bam/cmnk1inv.bam~     ~override~ // monk (tutorial, not pc) paperdoll
     ~eefixpack/files/2da/fogarea.2da~      ~override~ // tbd, cam: add basic 2das to enable fog
     ~eefixpack/files/2da/fogpt.2da~        ~override~ // tbd, cam: add basic 2das to enable fog
     ~eefixpack/files/bam/sppr314d.bam~     ~override~ // new unholy blight portrait icon
     ~eefixpack/files/bam/mglc/mglcg1.bam~  ~override~ // fix SC sequence for creature animation 0x7F07 GOLEM_CLAY
     ~eefixpack/files/bam/spwi524d.bam~     ~override~ // fix rle-encoding error
     ~eefixpack/files/bam/mvaf~             ~override~ // sam.: removing shadows from vampires, female
     ~eefixpack/files/bam/mvam~             ~override~ // sam.: removing shadows from vampires, male
     ~eefixpack/files/bam/nboh~             ~override~ // sam.: removing shadows from vampires, bodhi
     ~eefixpack/files/bam/spsdbur.bam~      ~override~ // fixed silver dragon breath

// fixing shadows on item icons
COPY ~eefixpack/files/bam/item_shadows/bg2ee_iwdee~     ~override~
     ~eefixpack/files/bam/item_shadows/bgee_iwdee~      ~override~
     ~eefixpack/files/bam/item_shadows/iwdee~           ~override~
     ~eefixpack/files/bam/item_shadows/sod_bg2ee_iwdee~ ~override~
     ~eefixpack/files/bam/item_shadows/universal~       ~override~

INCLUDE ~eefixpack/files/tph/spell_bams.tph~ // updated spell BAMs

// tbd, sam.
// merging multi-part area animations to eliminate issues
ACTION_FOR_EACH file IN
  1000ms1 1105ms1 1201ms2 1400t001 1400t002 1400t003 1400t004 1400t005 3001ms1 3601ms1
  6013ms1 am0602c am0602d am0602k1 am0602k2 am0602l1 am0602l2 am0801b1 am0801b2 am0902l
  am0902l1 am0902l2 am0902l3 am0902l4 am2000a am2000a1 am2000a2 am2000a3 am2000a4 am2000b
  am2000b1 am2000b2 am2000b3 am2000b4 am2805b am2805c am414e11 am414e12
BEGIN
  COPY ~eefixpack/files/bam/area_anim/%file%.bam~ ~override~
END

// installing BAM V2 resources
INCLUDE ~eefixpack/files/tph/install_bam_v2.tph~
ACTION_FOR_EACH bam_resref IN guiostur storstor BEGIN
  LAF install_bam_v2 STR_VAR bam_resref END
END

// there's another section of INCLUDEs at the bottom if it should be late in the process
INCLUDE ~eefixpack/files/tph/5943_mold_touch.tph~           // ie-5943 , cam: lots of mold touch fixes
INCLUDE ~eefixpack/files/tph/6018_lizardmen.tph~            // ie-6018 , cam: Summoned lizardmen lack visible weapons
INCLUDE ~eefixpack/files/tph/6020_bullets.tph~              // ie-6020 , cam: Bullets +1 and Bullets +2 use the same projectile
INCLUDE ~eefixpack/files/tph/6036_clostsbfbhnaeig.tph~      // ie-6036 , cam: Cam's List o' Stuff That Should Be Fixed But Has No Actual Effect In Game
INCLUDE ~eefixpack/files/tph/b3460_death_tyrant.tph~        // bgcs-3460, cam: death tyrant animation fixes
INCLUDE ~eefixpack/files/tph/b3397_revenant.tph~            // bgcs-3397, cam: revenant animation fixes
INCLUDE ~eefixpack/files/tph/tbd_extraplanar_tieflings.tph~ // tbd, cam: tieflings should not be included in effects that target 'outer' or 'extraplanars'
INCLUDE ~eefixpack/files/tph/tbd_iwdee_chargen.tph~         // tbd, cam: very specific circumstances can crash if starting HoW game

/////                                                  \\\\\
///// mechanics fixes / overhauls                      \\\\\
/////                                                  \\\\\

// kjeron
WITH_SCOPE BEGIN
  WITH_TRA "eefixpack/languages/en_us/luke/polymorph_overhaul.tra" "eefixpack/languages/%LANGUAGE%/luke/polymorph_overhaul.tra" BEGIN
    INCLUDE "eefixpack/files/tph/luke/polymorph_overhaul.tph"
    LAF "POLYMORPH_OVERHAUL" END
  END
END

// tbd, davidw
// circle of bone subspell has illegal character in sig
COPY_EXISTING "#bonecir.spl" override WRITE_ASCII 0x0 "SPL "

/*
luke
"7eyes.2da" vs. SPL/ITM files
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/7eyes/7eyes.tph"
  LAUNCH_ACTION_FUNCTION "7EYESification" END
END

/*
luke
**Wing Buffet vs. MR**
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/wing_buffet_vs_mr.tph"
  LAUNCH_ACTION_FUNCTION "WING_BUFFET_VS_MR" END
END

// argent77
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/a7/improved_troll_regeneration.tph"
END

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_iwd_locals_scripts.tph~ // tbd, cam - region/container scripts can't use LOCALS-scoped variables
INCLUDE ~eefixpack/files/tph/a7/iwd_luremaster.tph~ // luremaster cutscene fixes

// tbd, cam
// player ai checks if magical weapon present before casting shillelagh, but checks old resources
COPY ~scripts/cleric1.bs~ ~scripts/cleric1.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!HasItem("SGRASP",Myself)~ // shocking grasp just does direct damage in iwdee, no item
      ~!HasItem("bclaw",Myself) !HasItem("moonbla",Myself) !HasItem("smcudge",Myself)~ // change instead to druid spells: beast claw, moonblade, star metal cudgel
    REPLACE_TEXTUALLY ~!HasItem("shille",Myself)~ ~!HasItem("shillel",Myself)~ // wrong resref for shillelagh
    REPLACE_TEXTUALLY ~!HasItem("SHAMMR",Myself)~ ~!HasItem("shamme1",Myself) !HasItem("shamme2",Myself) !HasItem("shamme3",Myself)~ // wrong resrefs for spiritual hammer
    REPLACE_TEXTUALLY ~!HasItem("chillt",Myself)~ ~!HasItem("ctouch",Myself)~ // wrong resref for chill touch
  END
  BUT_ONLY

// remove team 5 from Ilmadia's scripting (she's already team 2)
// see also edits to ar8011.are
COPY_EXISTING ~ar8011.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Global("SPRITE_IS_DEADILMADIA","GLOBAL",0)~ ~!Global("SPRITE_IS_DEADMAIDENILMADIA","GLOBAL",0)~
    APPEND_FILE ~eefixpack/files/baf/iwd8011.baf~
  END
  BUT_ONLY

// how finale, checking var that never gets set
COPY_EXISTING ~ar9603.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~"BELHIEFET_DEAD","GLOBAL"~ ~"SPRITE_IS_DEADBELHIEFET","GLOBAL"~ // checking wrong var
  END   

// shaman summon script errors - fix for bgee, bg2ee, iwdee
COPY_EXISTING ~bdshunsu.bcs~ ~override~ // chaman summun AI script
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!\(Detect(\[PC\.0\.0\.SHAMAN\])[ %TAB%%LNL%%MNL%]+CombatCounter(0)\)~ // have to include extra line to avoid other blocks
    	~\1~ // should be detect, not !detect (sod only)
    REPLACE_TEXTUALLY ~\(MoveToObject(\[PC\.0\.0\.SHAMAN])\)~ ~\1 Continue()~ // don't block item bonuses with infinite Move commands
  END   

// tbd, cam
// checking for ambients which don't exist
COPY_EXISTING ~eepoqct1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AmbientActivate("1105MS1[0-7]",FALSE)~ ~~
  END
  BUT_ONLY

// ie-6015, cam
// Jorn should heal all fellow barbarians (allies are BARBARIAN_1 to 8, script checks 0-7)
COPY_EXISTING ~iljorn.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~BARBARIAN_0~ ~BARBARIAN_8~
  END
  BUT_ONLY

// ie-6014, cam
// Loot should have charges: items from random treasure table with differing charges need to be re-added with charges
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_rndtres_charges BEGIN
  wandtrp, 5, "Uligar"           => ar2004 // orctres
  arowtrn, 8, "Pileo'Stuff"      => ar4003 // de3tres
  trnbolt, 8, "Pileo'Stuff"      => ar4003 // de3tres
  boneam,  1, "SerratedSkeleton" => ar5003 // sh1tres
END

ACTION_PHP_EACH cd_rndtres_charges AS params => area BEGIN
  EXTEND_BOTTOM ~%area%.bcs~ ~eefixpack/files/baf/rndtres_charges.baf~ EVALUATE_BUFFER
END

// ie-6006, cam
// Talonite Priests don't heal one another
COPY_EXISTING ~d2talon3.bcs~ ~override~
              ~d2talon4.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~TalonitePriest[14]~    ~Talonite~
    REPLACE_TEXTUALLY ~TalonitePriestess[23]~ ~Talonite~
  END
  BUT_ONLY

// tbd, cam
// potential softlock if party is split when yxunomei closes door as part of her ambush
COPY_EXISTING ~d5yxudor.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"LOCALS"~ ~"ar4005"~ // need area variable, not locals
    REPLACE_TEXTUALLY ~!Global("SPRITE_IS_DEADYXUNOMEI","GLOBAL",0)~ ~CombatCounter(0) OpenState(Myself,FALSE) OR(2) !Global("SPRITE_IS_DEADYXUNOMEI","GLOBAL",0) GlobalTimerExpired("CDYxunDoor","ar4005")~
    REPLACE_TEXTUALLY ~!Global("YXUN_ATTACK","MYAREA",0)~ ~!Global("YXUN_ATTACK","MYAREA",0) OpenState(Myself,TRUE)~
    REPLACE_TEXTUALLY ~[ %TAB%]Lock(Myself)~ ~Lock(Myself) SetGlobalTimer("CDYxunDoor","ar4005",60)~
  END
  BUT_ONLY

INCLUDE ~eefixpack/files/tph/ending_cutscenes.tph~ // tightening end of cutscenes

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

// oIWD uses area actor names as DVs, which means they should be limited to 18 characters (not counting
// spaces) so that SPRITE_IS_DEADfoo won't exceed 32 characters total.
INCLUDE ~eefixpack/files/tph/tbd_iwdee_area_dv_fixes.tpa~

COPY_EXISTING ~ar1015.are~ ~override~ // beetle has redundant script
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = ehbeetle script_override = ~~ END // name not unique, but it's ok since all shouldn't have override script

// tbd, cam
// dupe random treasure, two containers overlay one another
COPY_EXISTING ~ar6006.are~ ~override~
  LPF DELETE_AREA_ITEM STR_VAR item_to_delete = scrl6j END // delete items out of container being removed
  LPF DELETE_AREA_ITEM STR_VAR item_to_delete = potn31 END
  LPF fj_are_structure INT_VAR fj_delete_mode = 3 STR_VAR fj_structure_type = container END // delete container 4
  LPF ALTER_AREA_ITEM STR_VAR match_item = rndtre57 item = scrl6j END // replace dupe random treasure with one of the items from the deleted container
  LPF ADD_AREA_ITEM INT_VAR container_to_add_to = 1 STR_VAR item_to_add = potn31 END // re-add the second deleted item
  BUT_ONLY

// ie-6016, cam
// Untrapped doors shouldn't show as trapped
COPY_EXISTING ~ar7000.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_detectable = 0 STR_VAR door_name = AR7000Door1 END
  BUT_ONLY

COPY_EXISTING ~ar7002.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_detectable = 0 STR_VAR door_name = AR7002Door1 END
  BUT_ONLY

// remove team 5 from Ilmadia's scripting (she's already team 2)
// see also edits to ar8011.bcs
COPY_EXISTING ~ar8011.are~ ~override~
  LPF ALTER_AREA_ACTOR STR_VAR script_default = "" actor_name = "Maiden Ilmadia" END
  BUT_ONLY

// ie-6017, cam
// Wrong random treasures in HoW
COPY_EXISTING ~ar9200.are~ ~override~ // swap back the old extres4 dupe fix
  LPF REPLACE_AREA_ITEM INT_VAR charges1 = 3 charges2 = 0 charges3 = 0 STR_VAR old_item = rndtre75 new_item = rndtre78 END // 3/0/0 charges for ogien/cloakin
  BUT_ONLY

COPY_EXISTING ~ar9601.are~ ~override~ // swap back the old extres4 dupe fix
  LPF REPLACE_AREA_ITEM INT_VAR charges1 = 1 charges2 = 1 charges3 = 0 STR_VAR old_item = rndtre78 new_item = rndtre75 END // 1/1/0 charges for beltbon/helmsh/stomper
  BUT_ONLY

// one greater ice troll and one bergclaw don't have move scripts; also add random walk script to bergclaw to match others
COPY_EXISTING ~ar9600.are~ ~override~ // can't use ALTER_AREA_ACTOR since names not unique, scripts not the same for others
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "dlicetr" = 0) BEGIN
      READ_SHORT (actor_off + 0x20 + (index * 0x110)) x_coord
      READ_SHORT (actor_off + 0x22 + (index * 0x110)) y_coord
      PATCH_IF ((x_coord = 1402) AND (y_coord = 2005)) BEGIN
        WRITE_ASCII (actor_off + 0x58 + (index * 0x110)) ~gnt4move~ #8 // update general script
      END
    END
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "bergclaw" = 0) BEGIN
      READ_SHORT (actor_off + 0x20 + (index * 0x110)) x_coord
      READ_SHORT (actor_off + 0x22 + (index * 0x110)) y_coord
      PATCH_IF ((x_coord = 401) AND (y_coord = 1442)) BEGIN
        WRITE_ASCII (actor_off + 0x58 + (index * 0x110)) ~efrndw03~ #8 // update general script
        WRITE_ASCII (actor_off + 0x68 + (index * 0x110)) ~gnt9move~ #8 // update race script
      END
    END
  END
  BUT_ONLY

// ie-6013, cam
// Harpies should despawn for lower level parties
COPY_EXISTING ~ar9706.are~ ~override~ // only two harpy fiends should go away at CheckPartyAverageLevel(14,LESS_THAN)
  SET harpy = 0
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x00 + (index * 0x110)) name
    PATCH_IF ("%name%" STRING_COMPARE_CASE "HrpFnd_0" = 0) BEGIN
      WRITE_ASCIIE (actor_off + 0x00 + (index * 0x110)) ~HrpFnd_%harpy%~
      SET harpy += 1
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar9714.are~ ~override~ // one boneguard lacks team script
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Boneguard Skeleton 6" script_class = ~gnteam4~ END

// argent77
// Fixes positioning issues with Brother Harken and Sister Incylia in the Fallen Temple
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/a7/iwd_brother_harken_fix.tph"
END

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_cre_fixes_iwdee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/a7/iwd_blast_skeleton.tph~ // blast skeleton should self-destruct more reliably
INCLUDE ~eefixpack/files/tph/tbd_elemental_summons.tph~ // tbd, cam: elemental prince summons shouldn't grant XP

// ie-6035, cam
// Why are constructs carrying gold?
COPY_EXISTING ~biknight.cre~ ~override~ // black ice knight
  WRITE_LONG 0x1C 0 // gold
  BUT_ONLY

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_itm_fixes_iwdee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/tbd_iwdee_146.tph~ // tbd, cam: fixes items that cast spells
INCLUDE ~eefixpack/files/tph/tbd_energy_blades.tph~ // tbd, cam: per descript, energy blades should do missile damage, not slashing
INCLUDE ~eefixpack/files/tph/usability_arrow_bolt.tph~ // monks using arrows/bolts

// mismatched icons between inventory and item ability
COPY_EXISTING ~book06.itm~ ~override~
              ~book08.itm~ ~override~
  WRITE_ASCIIE 0x3a ~i%SOURCE_RES%~ #8
  BUT_ONLY

// ie-6019, cam
// Portrait icon fixes for items
COPY_EXISTING ~bootfox.itm~ ~override~ // boots of the fox: swap haste icon for movement speed increase
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 38 parameter2 = 193 END
  BUT_ONLY
COPY_EXISTING ~moonbla.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 60 opcode = 142 parameter2 = 105 END
  BUT_ONLY
COPY_EXISTING ~vexed.itm~ ~override~ // vexed armor: has 2x cold resist icon, one should be cursed instead
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 25 parameter2 = 35 multi_match = 1 END
  BUT_ONLY

// tbd, cam
// upgraded bren muller's crossbow needs feedback that effect was activated (copy bg2 Heartseeker's visuals)
COPY_EXISTING ~cdxbowbm.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode =  61 target = 1 parameter1 = ((0 << 24) + (30 << 16) + (120 << 8)) parameter2 = (25 << 16) timing = 1 END // rgb fade
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 141 target = 1 parameter2 = 24 timing = 1 END // rgb fade
  BUT_ONLY

// Oil of Second Chances should take a full round to apply, and shouldn't display "gulp"
COPY_EXISTING ~chance.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 opcode = 165 parameter1 = 0 timing = 0 duration = 6 resist_dispel = 0 END

// ie-6007, cam - see also strings 8663, 8712, 8911, 40639, 25818, and 27373
// Weapon damage corrections
COPY_EXISTING ~clbhand.itm~ ~override~ // composite long bow of the hand
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 3 END // missing +1 from being composite long bow
  BUT_ONLY
COPY_EXISTING ~kaybow.itm~ ~override~ // kaylessa's composite long bow of the hand
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 4 END // missing +1 from being composite long bow
  BUT_ONLY
COPY_EXISTING ~lxbowbm.itm~  ~override~ // bren muller's crossbow
              ~cdxbowbm.itm~ ~override~ // bren muller's crossbow, upgraded
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 3 END // 1 damage short for +3 lt xbow
  BUT_ONLY
COPY_EXISTING ~storm.itm~ ~override~ // storm bow
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_bonus = 2 END // as a short bow, shouldn't get long bow's +1 damage
  BUT_ONLY
COPY_EXISTING ~flailsk.itm~ ~override~ // skullflail
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 1 damage_bonus = 5 END // is +4 weapon, base flail damage is 1d6+1, so damage should be 1d6+5
  BUT_ONLY

// swapping stacking protection from 206/318/324 for 321
// see also #cofear.spl, spin132.spl, sppr617.spl, spin161.spl, sppr250.spl, spwi101/b/c.spl
COPY_EXISTING ~cwoslim.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter2 = 0 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 25 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END

// tbd, cam
// item blocks spmindat, but isn't immune to hold
COPY_EXISTING ~cwreve.itm~   ~override~ // attack - blocks spmindat
  LPF DELETE_EFFECT INT_VAR match_opcode = 296 STR_VAR match_resource = spmindat END
  BUT_ONLY

// ie-6023, cam
// Weapon effect duration fixes - target both itm and spl so it can go before or after luke's seven eye fixes
COPY_EXISTING ~dart03.itm~ ~override~ // dart of stunning: color glow expires early
              ~dart03.spl~ ~override~ // dart of stunning: color glow expires early
  LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 38 duration = 42 END
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~ghoult.itm~ ~override~ // ghoul touch should last six rounds
              ~ghoult.spl~ ~override~ // ghoul touch should last six rounds
  LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 25 duration = 36 END // bump up to six rounds
  BUT_ONLY IF_EXISTS

// tbd, cam
// sword of days doesn't block all haste/slow spells
COPY_EXISTING ~days.itm~ ~override~ // round out slow/haste immunity; david's stuff will add the immunity spellstates
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi305 resource = spra301 insert = below END // clone haste to ranger haste
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spin146 insert = below END // clone to beholder slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spin977 insert = below END // clone to golem slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spwish25 insert = below END // clone to wish slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 STR_VAR match_resource = spwi312 resource = spwm164 insert = below END // clone to wild surge slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 101 parameter2 = 40 STR_VAR match_resource = spwi312 resource = ~~ END // immune to slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 101 parameter2 = 16 STR_VAR match_resource = spwi312 resource = ~~ END // immune to haste
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 267 parameter1= 14023 parameter2 = 0 STR_VAR match_resource = spwi312 resource = ~~ END // immune to string hasted
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 267 match_parameter1= 14023 parameter1 = 14000 END // immune to string slow
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 267 match_parameter1= 14023 parameter1 = 14668 END // immune to string slowed
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 318 opcode = 169 parameter1= 0 parameter2 = 38 STR_VAR match_resource = spwi312 resource = ~~ END // immune to icon haste
  LPF CLONE_EFFECT INT_VAR check_headers = 0 match_opcode = 169 match_parameter2= 38 parameter2 = 41 END // immune to icon slow

// ie-5937, cam
// darts of bone shouldn't have a saving throw on their APR boost
COPY_EXISTING ~dobone.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 1 savingthrow = 0 END
  BUT_ONLY

// ie-6025, cam
// Items blocking spells that they shouldn't
COPY_EXISTING ~gvalor1.itm~ ~override~ // gauntlets of valor block haste, but should be blocking hold person
  LPF ALTER_EFFECT STR_VAR match_resource = spwi305 resource = spwi306 END
  BUT_ONLY
COPY_EXISTING ~kinetic.itm~ ~override~ // lance of disruption confers immunity to lance of disruption, but doesn't need it (original item bug fixed, this 318 was a stopgap)
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 STR_VAR match_resource = spwi328 END
  BUT_ONLY

// move planetar dispel-on-hit to after vorpal effect so that death ward can work better (in bgee, bg2ee, iwdee.tph)
COPY_EXISTING ~planetar.itm~ ~override~
  LPF CLONE_EFFECT  INT_VAR match_opcode = 58 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 58 multi_match = 1 END

// clone immunity to webtrav (web) projectile into web1p (single-target web) projectile immunity (in bgee, bg2ee, iwdee.tph)
COPY_EXISTING ~plyspid.itm~  ~override~ // arcane polymorph spider
              ~plyspid2.itm~ ~override~ // avenger sword spider
  LPF CLONE_EFFECT INT_VAR match_opcode = 83 match_parameter2 = 319 parameter2 = 259 END // fortunately projectiles are all same value sin bg/bg2/iwd

// tbd, cam
// multiple permanent effects with durations
COPY_EXISTING ~potn10.itm~  ~override~ // (itm) Potion of Invisibility - causes invisibility - sound(s) #cas_m01 - animations(s) illush - additional effects
  LPF ALTER_EFFECT INT_VAR match_timing = 1 duration = 0 END

// tbd, cam
// elixir of health should cure intoxication, not set it to zero
COPY_EXISTING ~potn17.itm~ ~override~ // Elixir of Health - set drunkenness to 0, just change to 'cure drunk' instead
  LPF ALTER_EFFECT INT_VAR match_opcode = 94 opcode = 164 parameter2 = 0 END // flesh out effects below

// ie-6026, cam - see also string updates to 14913 and 21627
// Revert changes to Thrym Extract and War Hammer +4: Defender (had wrong effects which were documented as if intended)
COPY_EXISTING ~thrym.itm~ ~override~ // should block effects on failed save
  LPF CLONE_EFFECT INT_VAR match_opcode = 12 opcode = 318 parameter1 = 0 parameter2 = 0 timing = 0 duration = 6 dicesize = 0 dicenumber = 0 STR_VAR insert = below resource = thrym END
  BUT_ONLY
COPY_EXISTING ~u1ham5b.itm~  ~override~ // war hammer +4: defender: 20% heal
  LPF DELETE_EFFECT INT_VAR check_globals = 0 END // delete random heal, visuals
  BUT_ONLY

// tbd, cam
// disease icon duration doesn't match disease effect; just delete and attach icon to disease effect
COPY_EXISTING ~udagg4a.itm~  ~override~ // (itm) Chaos Dagger +3 - causes disease - portrait icon(s) 7
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 END // deletes icon
  LPF ALTER_EFFECT INT_VAR match_opcode = 78 special = 7 END // attach icon to disease effect

// ie-6024, cam - see also strings 14432 and 18095
// Two crossbows have the wrong strength requirement
COPY_EXISTING ~uhxbw2a.itm~ ~override~ // finest heavy crossbow
              ~xbow01.itm~  ~override~ // heavy crossbow
  WRITE_SHORT 0x26 11 // min str
  BUT_ONLY

// remove undocumented +2 save bonus from wand of fear
// luke's 7 eyes fix farms the effects into wand02.spl, but save bonus is still on the item
INCLUDE ~eefixpack/files/tph/wand02.tph~

// wand of fire should be using IWD-style Agannazar's Scorcher, not BG2-style
// see also strref 17654
COPY_EXISTING ~wand05.itm~ ~override~
  LPF CLONE_EFFECT  INT_VAR header = 1 match_opcode = 12 match_savingthrow = (BIT3 + BIT11 + BIT24) parameter1 = 0 dicenumber = 3 savingthrow = (BIT11 + BIT24)
    special = 0 STR_VAR insert = first END // primary damage (needs to be moved above evasion check, changed from 6d6+6 save for half to 3d6 no save)
  LPF DELETE_EFFECT INT_VAR header = 1 match_opcode = 12 match_savingthrow = (BIT3 + BIT11 + BIT24) END // delete old primary damage
  LPF ALTER_EFFECT  INT_VAR header = 1 match_opcode = 12 match_savingthrow = (BIT3 + BIT10 + BIT24) parameter1 = 0 dicesize = 8 dicenumber = 2 END // change secondary damage from 6d6+6 to 2d8

// tbd, cam
// feedback with limited timing and/or duration
COPY_EXISTING ~wand08.itm~   ~override~ // (itm) Wand of Sleep - causes sleep - additional effects
              ~wand19.itm~   ~override~ // (itm) Wand of Cursing - causes blind deafness silence - portrait icon(s) 8 34 112 - sound(s) EFF_E06 - additional effects
              ~wand19b.spl~  ~override~ // if run after luke's seven eye fixes, busted string is here instead
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 139 timing = 1 duration = 0 END
  BUT_ONLY IF_EXISTS

// acid storm lasts 15 rds in oIWD, so fix the wand to match
COPY_EXISTING ~wandcor.itm~ ~override~ // Wand of Corrosion
  LPF ALTER_EFFECT INT_VAR parameter1 = 15 parameter2 = 2 END // only effects are 146/48, so change all to cast at caster level 15, instantly

// description says snilloc's does 4d8, which means it should be casting at a fixed level of four
COPY_EXISTING ~wandfre.itm~ ~override~ // Wand of Freezing Death
  LPF ALTER_EFFECT INT_VAR parameter1 = 4 parameter2 = 2 END // only effects are 146/48, so change all to cast at caster level 4, instantly

// Hell's Bane should be usable by thieves, like other broad swords
COPY_EXISTING ~zzc8hb.itm~ ~override~ // Hell's Bane +4
  WRITE_LONG 0x1e THIS & `BIT22
  BUT_ONLY


/*
luke
**Black Blade of Disaster**
- Make sure `primary_type` is `NECROMANCER`
- Make sure `equipped appearance` is `Long sword`
- Make sure all unnatural creatures are immune to its level drain effect
- Should not interact with level-based spell protections and Magic Resistance
- Make sure *"The caster is considered to be proficient to the point of Grand Mastery in this weapon"* always works
  - `op233` needs to be on the normal effects list to be evaluated after chargen proficiency effects and override them!
- Provide a workaround for the following bugs:
  - if you Dual-class when the Black Blade is equipped, the character permanently gains grand mastery in long swords
  - if, for example, you have a warrior with an item that grants dagger proficiency while equipped, they can then take dagger specialization at a level-up, remove the item, and keep the specialization permanently
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/black_blade_of_disaster.tph"
    LAUNCH_ACTION_FUNCTION "WIZARD_BLACK_BLADE_OF_DISASTER" END
  END
END

/*
luke
**Iron Body**
- Move spell features from `SPL` file to `ITM` file
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/iron_body.tph"
  LAUNCH_ACTION_FUNCTION "WIZARD_IRON_BODY" END
END

/*
luke
**Magical Weapon Slot**
(Based on the previous tweak to the Black Blade of Disaster)
- Make sure natural creature weapons / touch attacks do not benefit from Fighting Styles
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/magical_weapon_slot.tph"
    LAUNCH_ACTION_FUNCTION "MAGICAL_WEAPON_SLOT" END
  END
END

/*
luke
**Attack** (used by Slime Zombie)
- Make sure it uses op318 to grant protection from itself (instead of messy op101 + op267 + op173 effects)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "cwszomb.itm" "override"
    LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 267 END // Disable display string
    LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 173 END // Poison resistance bonus
    LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 101 END // Immunity to effect
    LPF CLONE_EFFECT INT_VAR match_opcode = 25 opcode = 321 parameter1 = 0 parameter2 = 2 duration = 0 special = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END
//    LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 318 "target" = 2 "probability1" = 10 STR_VAR "resource" = "%DEST_RES%" END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Three White Doves +3**
- Redone from scratch via op326 (instead of 177s)
- Fixed incorrect probability values
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/three_white_doves.tph"
  LAUNCH_ACTION_FUNCTION "THREE_WHITE_DOVES" END
END

/*
luke
**Talon of the Gloomfrost +4, Holdfast Arrow +1**
- Should display the Entangled icon instead of the Held icon
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "holdfst.itm" "override"
                "talongf.itm" "override"
    LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 142 "match_parameter2" = 13 END // portrait icon -- move to subspell
  BUT_ONLY
END

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bulk_spl_fixes_iwdee.tph~ // bulk general changes

INCLUDE ~eefixpack/files/tph/bg2_iwd_common_spell_fixes.tph~    // spell fixes in common between bg2, iwd
INCLUDE ~eefixpack/files/tph/bg_bg2_iwd_common_spell_fixes.tph~ // spell fixes in common between bg, bg2, and iwd

INCLUDE ~eefixpack/files/tph/5919_iwdee_spider_spawn.tph~ // ie-5919, cam: spider spawn only summons giant spiders, should also summon phase and sword
INCLUDE ~eefixpack/files/tph/tbd_324_traps_iwdee.tph~ // tbd, cam - game can crash if a spell-via-trap uses 318/324 vs. a spell without a name

// swapping stacking protection from 206/318/324 for 321
// see also cwoslim.itm, spin132.spl, sppr617.spl, spin161.spl, sppr250.spl, spwi101/b/c.spl
COPY_EXISTING ~#cofear.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 24 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END

// feedback with limited timing and/or duration
COPY_EXISTING ~#sunrund.spl~ ~override~ // (spl) <Invalid Strref -1> - causes blind - portrait icon(s) 8 - additional effects
              ~spin120.spl~  ~override~ // (spl) Retribution - causes sleep - additional effects
              ~sppr102.spl~  ~override~ // (spl) Command - causes sleep - additional effects
              ~spwi004.spl~  ~override~ // (spl) Stinking Cloud - causes sleep - portrait icon(s) 126 - additional effects
              ~spwi032.spl~  ~override~ // (spl) Color Spray - causes blind pause sleep stun - portrait icon(s) 8 55 - additional effects
              ~spwi105.spl~  ~override~ // (spl) Color Spray - causes blind pause sleep stun - portrait icon(s) 8 - additional effects
              ~spwi213.spl~  ~override~ // (spl) Stinking Cloud - causes sleep - portrait icon(s) 126 - additional effects
              ~spwi711.spl~  ~override~ // (spl) Sphere of Chaos - causes confusion hold sleep - portrait icon(s) 38 - additional effects
              ~spwi806.spl~  ~override~ // (spl) Great Shout - causes deafness pause sleep - portrait icon(s) 112 - additional effects
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 timing = 1 duration = 0 END

// tbd, cam
// crippling strike should extend t0 -10 for assassins
COPY_EXISTING ~backstab.spl~ ~override~
  READ_SHORT 0x68 abil_num_orig
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 420 step_dur = 0 level_cap = 11 END
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = abil_num_orig ; index < abil_num ; ++index) BEGIN
    READ_SHORT (abil_off + 0x1e + (index * 0x28)) abil_fx_num
    READ_SHORT (abil_off + 0x20 + (index * 0x28)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off + 0x00 + ((index2 + abil_fx_idx) * 0x30)) op
      PATCH_IF ((op = 54) OR (op = 73)) BEGIN
        WRITE_LONG (fx_off + 0x04 + ((index2 + abil_fx_idx) * 0x30)) (0 - index) // penalty
        WRITE_LONG (fx_off + 0x0e + ((index2 + abil_fx_idx) * 0x30)) 70          // duration
      END
    END
  END
  BUT_ONLY 

// ie-5938, cam
// Lich Touch should paralyze, not hold
COPY_EXISTING ~ltouch.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 175 opcode = 109 END
  BUT_ONLY

// thac0 progression should be capped at 0, not 1
COPY_EXISTING ~ohtmps1.spl~ ~override~ // holy power
              ~sppr412.spl~ ~override~ // holy power
  READ_SHORT 0x68 abil_num
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "ohtmps1" = 0) BEGIN SET start = 20 END ELSE BEGIN start = 14 END
  FOR (index = start ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 54 parameter1 = 0 END
  END

// tbd, cam
// stuns, but plays spmindat; iwd generally uses glows so change to that instead
COPY_EXISTING ~spcl118.spl~  ~override~ // the siren's yearning—enthralls creatures - plays spmindat
  LPF ALTER_EFFECT INT_VAR match_opcode = 215 opcode = 8 parameter1 = ((46 << 8) + (56 << 16) + (0 << 24)) parameter2 = 255 STR_VAR match_resource = spmindat END
  BUT_ONLY

// ie-6027, cam
// HP draining spells shouldn't work on undead and extraplanar creatures
COPY_EXISTING ~%MONK_LAY_ON_HANDS%.spl~ ~override~ // spcl815 lay on hands (monk)
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 2 parameter2 = 55 duration = 1 STR_VAR resource = spcl815 END
COPY_EXISTING ~%MONK_LAY_ON_HANDS%.spl~          ~override~ // spcl815 lay on hands (monk)
              ~%WIZARD_LARLOCH_MINOR_DRAIN%.spl~ ~override~ // spwi119 larloch's minor drain
              ~%WIZARD_VAMPIRIC_TOUCH%.spl~      ~override~ // spwi314 vampiric touch
  PATCH_FOR_EACH race IN 121 139 145 147 156 157 158 159 169 BEGIN // demonic mephit elemental genie (anti)solar (dark)planetar salamander
    LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 55 parameter1 = race parameter2 = 104 END
  END
  BUT_ONLY

// chromatic orb missing 2d8 magic damage at levels 7+
COPY_EXISTING ~spdr101.spl~ ~override~
              ~spwi118.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR header = 6 opcode = 12 target = 2 parameter2 = (64 << 16) timing = 1 resist_dispel = 1 dicenumber = 2 dicesize = 8 END // adding missing 2d8 magic damage

// ie-6029, cam
// Hopelessness not always canceling Hope
COPY_EXISTING ~%INNATE_MOURNFUL_WAIL%.spl~ ~override~ // spin115 Mournful Wail; do separately since it has a 206 at top of stack
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 probability1 = 100 END // play on all stuns
  LPF CLONE_EFFECT INT_VAR match_opcode = 139 opcode = 321 STR_VAR resource = spwi429 END // remove Emotion: Hope
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 44 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 45 opcode = 142 parameter1 = 0 parameter2 = 44 STR_VAR insert = last END
  BUT_ONLY
COPY_EXISTING ~%CLERIC_SYMBOL_OF_HOPELESSNESS%.spl~ ~override~ // sppr716 Symbol: Hopelessness
              ~%WIZARD_EMOTION_HOPELESSNESS%.spl~ ~override~ // spwi411
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 139 opcode = 321 parameter1 = 0 parameter2 = 0 STR_VAR resource = spwi429 END // remove Emotion: Hope
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 44 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14980 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 45 opcode = 142 parameter1 = 0 parameter2 = 44 STR_VAR insert = last END
  LPF CLONE_EFFECT INT_VAR match_opcode = 45 opcode = 139 parameter1 = 14980 parameter2 = 0 timing = 1 duration = 0 STR_VAR insert = last END
  BUT_ONLY

// ie-6033, cam
// Great Roar has inconsistent durations
COPY_EXISTING ~%INNATE_GREAT_ROAR%.spl~ ~override~ // spin119 great roar
  LPF ALTER_EFFECT INT_VAR match_duration = 120 duration = 30 END
  BUT_ONLY

// tbd, cam
// mismatching durations
COPY_EXISTING ~spin120.spl~  ~override~ // (spl) Retribution - causes sleep - additional effects
  LPF ALTER_EFFECT INT_VAR match_duration = 85 duration = 8571 END // 139 dealt with elsewhere

// swapping stacking protection from 206/318/324 for 321
// see also cwoslim.itm, #cofear.spl, sppr250.spl, spwi101/b/c.spl
COPY_EXISTING ~spin132.spl~ ~override~
              ~sppr617.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 13 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END

COPY_EXISTING ~spin139.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter2 = 0 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 146 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END

COPY_EXISTING ~spin161.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter2 = 0 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 139 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END

// tbd, davidw
// The projectile for Bombardier Beetle clouds has a duplicate string
COPY_EXISTING ~%INNATE_BOMBARDIER_BEETLE_CLOUD%.spl~ ~override~ // spin191
  LPF DELETE_EFFECT INT_VAR match_opcode=139 match_parameter1=35568 multi_match=1 END // has two 'stunned' strings
  BUT_ONLY

// ie-6028, cam
// Charm via Mantle of Hell's Furnace does not work as expected or, really, at all
COPY_EXISTING ~spitm99.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 5 parameter2 = 1 END // change to normal hostile charm
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 2 parameter1 = 187 parameter2 = 115 duration = 1 STR_VAR resource = spitm99 END // block class != fire_elemental
  BUT_ONLY

// tbd, cam
// IWDEE bug 321 should be subject to same MR check as AC effect
COPY_EXISTING ~%CLERIC_ALICORN_LANCE%.spl~ ~override~ // sppr218
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 resist_dispel = 1 END

// evasion fixes - evasion checks should provide feedback
COPY_EXISTING ~sppr323d.spl~ ~override~ // circle of bones
              ~sppr603d.spl~ ~override~ // blade barrier
              ~spwi313.spl~  ~override~ // skull trap
              ~spwi615.spl~  ~override~ // chain lightning
              ~spwi615b.spl~ ~override~ // chain lightning
  LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 324 match_parameter2 = 63 END 

// ie-5947, cam
// Cloudburst should remove Fireshields
COPY_EXISTING ~%CLERIC_CLOUDBURST%.spl~ ~override~ // sppr325
  LPF CLONE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = firau1d6 resource = spwi403 END // fireshield blue
  LPF CLONE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = firau1d6 resource = spwi418 END // fireshield red
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = firau1d2 resource = sppr730d END // immune to aura of flaming death damage

// tbd, davidw
// Storm Shell shouldn't stack with itself
COPY_EXISTING ~%CLERIC_STORM_SHELL%.spl~ ~override~ // sppr327
  LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 timing=0 resist_dispel=3 STR_VAR resource="%CLERIC_STORM_SHELL%" END
  LPF ALTER_EFFECT INT_VAR target=1 END // for some subtle reason, ability_target=5 and target=2 doesn't play nice with the 321 trick
  BUT_ONLY

// tbd, cam
// poison icon expires too early, just build into poison effect
COPY_EXISTING ~sppr411.spl~ ~override~ // (spl) Poison - causes poison - portrait icon(s) 6
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 25 special = 6 END

// ie-5945, cam
// Static Charge icon should be dispellable like the rest of the spell
COPY_EXISTING ~%CLERIC_STATIC_CHARGE%.spl~  ~override~ // sppr420
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 resist_dispel = 3 END

// ie-5753, cam
// iwdee bug: blood rage can avoid fatigue with a re-cast - clone 321 into 206 then delete it; also block ops 18 and 98
COPY_EXISTING ~%CLERIC_BLOOD_RAGE%.spl~  ~override~ // sppr422
  LPF CLONE_EFFECT  INT_VAR match_opcode = 101 match_parameter2 = 17 STR_VAR insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 17 multi_match = 1 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 101 match_parameter2 = 17 parameter2 = 18 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 101 match_parameter2 = 17 parameter2 = 98 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 321 opcode = 206 timing = 0 duration = 120 STR_VAR match_resource = EVAL ~%CLERIC_BLOOD_RAGE%~ insert = last END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = EVAL ~%CLERIC_BLOOD_RAGE%~ END

// tbd, cam
// iwdee fix: weird durations for 318/324s
COPY_EXISTING ~%CLERIC_CLOUD_OF_PESTILENCE%.spl~ ~override~ // sppr423
  LPF ALTER_EFFECT INT_VAR match_duration = 85 duration = 0 END

// tbd, cam
// iwdee fix: dupe 'unconscious' strings, remove redundant 318 at end
COPY_EXISTING ~%CLERIC_SMASHING_WAVE%.spl~ ~override~ // sppr426
  LPF DELETE_EFFECT INT_VAR match_opcode = 139 match_duration = 12 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter2 = 0 END

// tbd, cam
// stun icons provided by stun effect in EE, remove redundant 142
COPY_EXISTING ~%CLERIC_SMASHING_WAVE%.spl~ ~override~ // sppr426
              ~%CLERIC_WHIRLWIND%.spl~     ~override~ // sppr617
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 55 END

// ie-5946, cam
// Wall of Moonlight should not interact with spell protections
COPY_EXISTING ~%CLERIC_WALL_OF_MOONLIGHT%.spl~ ~override~ // sppr428
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
COPY_EXISTING ~%CLERIC_WALL_OF_MOONLIGHT%a.spl~ ~override~ // sppr428a
              ~%CLERIC_WALL_OF_MOONLIGHT%b.spl~ ~override~ // sppr428b
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 power = 0 resist_dispel = 0 END

// tbd, cam
// IWDEE bug 321 should be timing 1/dur 0
COPY_EXISTING ~%CLERIC_SPIKE_STONES%.spl~ ~override~ // sppr519
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 timing = 1 duration = 0 END

// ie-5944, cam
// Animal Rage needs power level corrections
COPY_EXISTING ~%CLERIC_ANIMAL_RAGE%.spl~ ~override~ // sppr522
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 match_timing = 4 END
COPY_EXISTING ~cd0003.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 match_timing = 4 END
COPY_EXISTING ~%CLERIC_ANIMAL_RAGE%b.spl~ ~override~ // sppr522b
  LPF ALTER_EFFECT INT_VAR power = 5 END // power stays at 5 as per its parent (since it is not an AoE spell!)

// ie-5942, cam
// Mass Cause Light Wounds should not be available to druids
COPY_EXISTING ~%CLERIC_MASS_CAUSE_LIGHT_WOUNDS%.spl~ ~override~ // sppr523.spl
  WRITE_LONG 0x1e (THIS BOR BIT31) // adds druid flag

COPY_EXISTING ~sppr606.spl~ ~override~ // fire seeds
  READ_SHORT 0x68 abil_num // duration calculations are still using oIWD rounds (seven seconds, not the BG2/EE six), so every duration is 7/6 longer than it should be
  FOR (index = abil_num ; index > 0 ; --index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = (index - 1) match_duration = (((30 - abil_num) + index) * 70) duration = (((30 - abil_num) + index) * 60) END 
  END   

// tbd, davidw
// entropy shield (remove spurious protection from Breach/Spellstrike/Imprisonment; doesn't protect against Flame Strike)
COPY_EXISTING "%CLERIC_ENTROPY_SHIELD%.spl" ~override~ // sppr615
  LPF DELETE_EFFECT INT_VAR match_opcode=83 match_parameter2=54 END // remove immunity to SPARKLGR projectile
  PATCH_FOR_EACH resource IN sppr503 spimix01 ohbeflam BEGIN // clone one flamestrike immunity to cover other flamestrike variants
    LPF CLONE_EFFECT STR_VAR match_resource=sppr984 resource END
  END
  BUT_ONLY

// tbd, cam
// oiwd fix
COPY_EXISTING ~%CLERIC_WHIRLWIND%.spl~  ~override~ // sppr617
  LPF ALTER_EFFECT INT_VAR match_resist_dispel = 0 resist_dispel = 1 END

// headers added to extend spells to iwd level 30 cap all have incorrect durations
COPY_EXISTING ~sppr706.spl~ ~override~ // symbol, fear
              ~sppr718.spl~ ~override~ // symbol, stun
              ~spwi811.spl~ ~override~ // symbol, fear
              ~spwi816.spl~ ~override~ // symbol, stun
  LPF CD_TRIM-O-MATIC INT_VAR level_cap = 21 END // trim off incorrect headers
  LPF CD_EXTEND-O-MATIC INT_VAR dur_special = 1 step_size = 3 step_dur = 6 END // 2 rds + 1 rd/3 levels

// headers added to extend spells to iwd level 30 cap all have incorrect durations
COPY_EXISTING ~sppr711.spl~ ~override~ // regeneration
              ~sppr730.spl~ ~override~ // aura of flaming death
              ~sppr740.spl~ ~override~ // wither
  LPF CD_TRIM-O-MATIC INT_VAR level_cap = 20 END // trim off incorrect headers
  LPF CD_EXTEND-O-MATIC INT_VAR dur_special = 1 step_size = 2 step_dur = 6 END // 1 rd/2 levels

// evasion fixes - missing evasion check
COPY_EXISTING ~sppr725d.spl~ ~override~ // aura o' flaming death
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 2 parameter2 = 63 END 

COPY_EXISTING ~spra302.spl~ ~override~ // Minor Spell Deflection [ranger]
  LPF DELETE_EFFECT INT_VAR header = 6 multi_match = 1 match_opcode = 233 END // dupe 233s ar level 11
  LPF ALTER_EFFECT INT_VAR header = 6 match_duration = 180 duration = 198 END // some durations wrong when cast at level 11

// death fog's slow icon could be blocked on round 2+
COPY_EXISTING ~spwi030.spl~ ~override~ // via trap
              ~spwi614.spl~ ~override~ // via trap
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 END // delete from end of effect stack
  LPF CLONE_EFFECT INT_VAR match_opcode = 126 opcode = 142 parameter1 = 0 parameter2 = 41 END // move to middle

// swapping stacking protection from 206/318/324 for 321
// see also cwoslim.itm, #cofear.spl, spin132.spl, sppr617.spl, spin161.spl, sppr250.spl, spwi101b/c.spl
COPY_EXISTING ~spwi101.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 END

// tbd, cam
// stun icons provided by stun effect, remove redundant 142
COPY_EXISTING ~%WIZARD_ICELANCE%.spl~    ~override~ // spwi327
              ~%WIZARD_GREAT_SHOUT%.spl~ ~override~ // spwi806
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 55 END

// ie-6030, cam
// Stoneskin provides too many skins at all levels
COPY_EXISTING ~%WIZARD_STONE_SKIN%.spl~ ~override~ // spwi408
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 218 parameter1 = (index + 3) END
  END
  BUT_ONLY

// ie-5941, 1i-6031, cam
// iwdee: not always checking MR correctly
COPY_EXISTING ~%WIZARD_BELTYNS_BURNING_BLOOD%.spl~ ~override~ // spwi422
  PATCH_FOR_EACH op IN 61 142 174 215 321 333 BEGIN // everything but the portrait icon is wrong except at header 0
    LPF ALTER_EFFECT INT_VAR match_opcode = op resist_dispel = 1 END
  END

// ie-5940, cam
// Emotion, Fear bypasses save/MR checks for removing Emotion, Courage
COPY_EXISTING ~%WIZARD_EMOTION_FEAR%.spl~ ~override~ // spwi428
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 resist_dispel = 1 savingthrow = BIT0 END

// tbd, cam
// iwdee fix: deafness from (great) shout not including portrait icon
COPY_EXISTING ~%WIZARD_SHOUT%.spl~       ~override~ // spwi431
              ~%WIZARD_GREAT_SHOUT%.spl~ ~override~ // spwi806
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 80 opcode = 142 parameter2 = 112 END // clone deaf into deafness icon

// shroud of flame shouldn't cause 100% fire resistant creatures to burst into flames
COPY_EXISTING ~spwi524.spl~  ~override~
              ~spwi524b.spl~ ~override~
              ~spwi524c.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 318 opcode = 324 END // from evasion fixes - update 318s to 324s
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 2 parameter2 = 3 STR_VAR resource = EVAL ~%SOURCE_RES%~ END

// ie-6032, cam
// Tenser's Transformation shouldn't block innate abilities
// thac0 progression should be capped at -2 (base fighter 0 + 2 bonus)
COPY_EXISTING ~%WIZARD_TENSERS_TRANSFORMATION%.spl~ ~override~ // spwi603
//  LPF ALTER_EFFECT INT_VAR match_opcode = 145 match_parameter2 = 2 parameter2 = 3 END // disable since i'm already having to go through manually
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT (abil_off + 0x10 + (index * 0x28)) min_lev
    READ_SHORT (abil_off + 0x1e + (index * 0x28)) abil_fx_num
    READ_SHORT (abil_off + 0x20 + (index * 0x28)) abil_fx_idx
    SET thac0 = 19 - min_lev
    PATCH_IF thac0 < "-2" BEGIN SET thac0 = "-2" END
    PATCH_IF min_lev = 1 BEGIN SET thac0 = 7 END
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off + 0x00 + ((index2 + abil_fx_idx) * 0x30)) opcode
      READ_LONG  (fx_off + 0x08 + ((index2 + abil_fx_idx) * 0x30)) parameter2
      PATCH_IF opcode = 145 AND parameter2 = 2 BEGIN // disable spellcasting: innate
        WRITE_LONG  (fx_off + 0x08 + ((index2 + abil_fx_idx) * 0x30)) 3 // change to magical
      END ELSE
      PATCH_IF opcode = 54 BEGIN // thac0
        WRITE_LONG  (fx_off + 0x04 + ((index2 + abil_fx_idx) * 0x30)) thac0 // change to magical
      END
    END
  END
  BUT_ONLY

// tbd, cam
// feeblemind and petrification are permanent, but icons are not
COPY_EXISTING ~spwi714.spl~  ~override~ // (spl) Prismatic Spray - causes blind feeblemind - portrait icon(s) 8 48 171
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_duration = 308571 timing = 1 duration = 0 END // make permanent

// tbd, cam
// iwdee bug: use 176 for movement speed, 321 needs save
COPY_EXISTING ~%WIZARD_SUFFOCATE%.spl~ ~override~ // spwi726
  LPF ALTER_EFFECT INT_VAR match_opcode = 321 savingthrow = BIT0 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 126 opcode = 176 END

// tbd, davidw
// Mind Blank is supposed to protect against petrification
COPY_EXISTING ~%WIZARD_MIND_BLANK%.spl~ ~override~ // spwi802
  LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=175 parameter2=134 END // immune to petrification opcode
  LPF CLONE_EFFECT INT_VAR match_opcode=267 match_parameter1=14102 parameter1=14127 END // immune to petrification string
  LPF CLONE_EFFECT INT_VAR match_opcode=169 match_parameter2=3 parameter2=171 END // immune to petrification icon
  BUT_ONLY

// ie-5939, cam
// Great Shout duplicates combat feedback on a failed save
WITH_SCOPE BEGIN
  COPY_EXISTING - ~%WIZARD_GREAT_SHOUT%.spl~ ~override~ // spwi806 great shout
    GET_OFFSET_ARRAY ab_arr SPL_V10_HEADERS
    PHP_EACH ab_arr AS ab_ind => ab_off BEGIN
      GET_OFFSET_ARRAY2 fx_arr ab_off SPL_V10_HEAD_EFFECTS
      PHP_EACH fx_arr AS fx_ind => fx_off BEGIN
        PATCH_MATCH SHORT_AT fx_off WITH
          326 WHEN LONG_AT (0x8 + fx_off) = 111 BEGIN
            PATCH_MATCH SLONG_AT (0x4 + fx_off) WITH
              IDS_OF_SYMBOL("splstate" "STUN_IMMUNITY") IDS_OF_SYMBOL("splstate" "DEAF_IMMUNITY") BEGIN
                READ_ASCII (0x14 + fx_off) subspell
                // patch subspell
                PATCH_WITH_SCOPE BEGIN
                  INNER_ACTION BEGIN
                    COPY_EXISTING "%subspell%.spl" "override"
                      GET_OFFSET_ARRAY ab_arr SPL_V10_HEADERS
                      PHP_EACH ab_arr AS ab_ind => ab_off BEGIN
                        GET_OFFSET_ARRAY2 fx_arr ab_off SPL_V10_HEAD_EFFECTS
                        PHP_EACH fx_arr AS fx_ind => fx_off BEGIN
                          PATCH_MATCH LONG_AT (fx_off + 0x24) WITH
                            0 BEGIN
                              READ_ASCII fx_off $temp_arr("%fx_ind%") (0x30)
                              WRITE_SHORT fx_off 999
                            END
                            DEFAULT
                          END
                        END
                      END
                      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 999 END
                      LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 80 opcode = 139 timing = 1 duration = 0 parameter1 = 14073 STR_VAR insert = below END
                      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 savingthrow = BIT0 parameter1 = "-1" resist_dispel = 3 STR_VAR resource = "%DEST_RES%" END
                      PHP_EACH temp_arr AS "" => fx_data BEGIN
                        INSERT_BYTES (LONG_AT 0x6A + 0x30 * SHORT_AT (LONG_AT 0x64 + 0x1E)) 0x30
                        WRITE_ASCII (LONG_AT 0x6A + 0x30 * SHORT_AT (LONG_AT 0x64 + 0x1E)) "%fx_data%"
                        WRITE_SHORT (LONG_AT 0x64 + 0x1E) (THIS + 1)
                      END
                    BUT_ONLY
                  END
                END
              END
              DEFAULT
            END
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY
END

// luke
// 1) Grease should not interact with spell protections
// 2) Grease (in particular "spwi101b.spl" and "spwi101c.spl") should apply the GREASE stat.
///// As you can see, those two subspells are not applying the aforementioned stat, nor the GREASE spell state. This might confuse both later patches and AI.
///// As a result, I suggest replacing op215 with op158 (param2=1|custom overlay; resource="greaseb" on "spwi101b" and resource="greasec" on "spwi101c").
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_GREASE%b.spl~ ~override~ // spwi101b
                ~%WIZARD_GREASE%c.spl~ ~override~ // spwi101c
    LPF ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
    LPF ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 215 ~opcode~ = 158 END // Play visual effect => Grease overlay
  // swapping stacking protection from 206/318/324 for 321
  // see also cwoslim.itm, #cofear.spl, spin132.spl, sppr617.spl, spin161.spl, sppr250.spl, spwi101.spl
    LPF CLONE_EFFECT INT_VAR match_opcode = 158 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END
  BUT_ONLY_IF_IT_CHANGES
END


// luke
// Chill Touch should correctly affect UNDEAD without permanently removing their immunity to Panic (op24)
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/wizard_chill_touch.tph" // chillt.itm, spwi117a.spl, spwi117b.spl
  LAF "WIZARD_CHILL_TOUCH" END
END

/*
luke
**Cause Disease**
- Make sure `op78` displays the `Diseased` portrait icon
  - function "remove_redundant_effects" won't be able to properly deal with this file because there are multiple op78 effects and just one op142 effect
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_CAUSE_DISEASE%.spl" "override" // sppr320
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 78 "special" = 7 END // Disease
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "match_opcode" = 142 "match_parameter2" = 7 END // op78 can naturally provide the "Diseased" portrait icon
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Ghast attack**
- Effects that grant a Save vs. Paralyze/Poison/Death should go into the new subspell
- Add missing feedback string
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "ghast1.spl" "override"
    LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_savingthrow" = BIT2 END
  BUT_ONLY_IF_IT_CHANGES
  COPY_EXISTING - "ghast1.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN LONG_AT (0x8 + "%fx_off%") == 111 AND SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "PARALYZE_IMMUNITY") BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 2 "timing" = 1 "parameter1" = 14650 "resist_dispel" = 3 END // feedback string
    LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 174 "target" = 2 "timing" = 1 "resist_dispel" = 3 STR_VAR "resource" = "#EFF_P11" END
    LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 174 "target" = 2 "timing" = 4 "duration" = 36 "resist_dispel" = 3 STR_VAR "resource" = "#EFF_E05" END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Jester's song**
- Make sure the Confusion subspell is refreshed every time it triggers (whether or not the target fails the save vs. Spell)
  - this is a "eefixpack/files/tph/luke/7eyes/confusion.tph" follow-up
*/

WITH_SCOPE BEGIN
  WITH_SCOPE BEGIN
    COPY_EXISTING "spcl751a.spl" "override"
      LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 146 "savingthrow" = 0 "savebonus" = 0 STR_VAR "match_resource" = "spcl751b" END // Cast spell
    BUT_ONLY_IF_IT_CHANGES
  END
  WITH_SCOPE BEGIN
    COPY_EXISTING "spcl751b.spl" "override"
      LPF "ALTER_EFFECT" INT_VAR "savingthrow" = BIT0 "savebonus" = 2 END // all effects: Save vs. Spell
      LPF "ADD_SPELL_EFFECT" INT_VAR "insert_point" = 0 "opcode" = 321 "target" = 2 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
    BUT_ONLY_IF_IT_CHANGES
  END
END

/*
luke
**Wand of Cursing**
- Move all cosmetic effects into the 3 subspells
  - this is a "eefixpack/files/tph/luke/7eyes/silence/blindness/deafness.tph" follow-up
*/

WITH_SCOPE BEGIN
  COPY_EXISTING "wand19.itm" "override"
    LPF "GET_V10_HEAD_EFFECTS" RET_ARRAY "v10_head_effects" END
    LPF "DELETE_ITEM_EFFECT" INT_VAR "opcode_to_delete" = 174 END // Play sound
    LPF "DELETE_ITEM_EFFECT" INT_VAR "opcode_to_delete" = 215 END // Play visual effect
  BUT_ONLY_IF_IT_CHANGES
  WITH_SCOPE BEGIN
    COPY_EXISTING_REGEXP "^wand19[a-c]\.spl$" "override"
      PHP_EACH "v10_head_effects" AS "fx_attributes" => "" BEGIN
        PATCH_IF ("%fx_attributes_0%" != 146) BEGIN
          LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attributes_0%" "target" = "%fx_attributes_1%" "power" = "%fx_attributes_2%" "parameter1" = "%fx_attributes_3%" "parameter2" = "%fx_attributes_4%" "timing" = "%fx_attributes_5%" "resist_dispel" = ("%fx_attributes_6%" == BIT0 ? BIT0 | BIT1 : "%fx_attributes_6%") "duration" = "%fx_attributes_7%" "special" = "%fx_attributes_15%" STR_VAR "resource" = "%fx_attributes_10%" END
        END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END

/*
luke
**Sprays Spores**
- make sure all effects are dispellable / bypass Magic Resistance
  - this is a "eefixpack\files\tph\luke\7eyes\berserk.tph" follow-up
*/

WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "subspell_arr"
  //
  COPY_EXISTING "%INNATE_MYCONID_SPORES%.spl" "override"
    GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            PATCH_MATCH SLONG_AT (0x4 + "%fx_off%") WITH
              IDS_OF_SYMBOL ("splstate" "BERSERK_IMMUNITY") IDS_OF_SYMBOL ("splstate" "STUN_IMMUNITY") IDS_OF_SYMBOL ("splstate" "PANIC_IMMUNITY") BEGIN
                READ_ASCII (0x14 + "%fx_off%") $"subspell_arr"("%fx_ind%")
                WRITE_BYTE (0xD + "%fx_off%") BIT0
              END
              DEFAULT
            END
          END
          DEFAULT
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
  ACTION_PHP_EACH "subspell_arr" AS "" => "resref" BEGIN
    COPY_EXISTING "%resref%.spl" "override"
      LPF "ALTER_EFFECT" INT_VAR "check_globals" = 0 "resist_dispel" = BIT0 | BIT1 END
    BUT_ONLY
  END
END

/*
luke
**Mental Domination**
- should also block innate abilities (as per spell description)
*/

WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_MENTAL_DOMINATION%.spl" "override"
    LPF "CLONE_EFFECT" INT_VAR "multi_match" = 1 "match_opcode" = 60 "parameter1" = 100 "parameter2" = 2 END // Casting failure (innate)
  BUT_ONLY
END

/*
luke
**Talon of the Gloomfrost +4, Holdfast Arrow +1**
- Should display the Entangled icon instead of the Held icon
- Should display the "Entangled" string
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "holdfst.itm" "override"
                "talongf.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "ENTANGLE_IMMUNITY") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
    INNER_ACTION BEGIN
      COPY_EXISTING "%subspell%.spl" "override"
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 142 "parameter1" = 0 "parameter2" = 144 "special" = 0 STR_VAR "insert" = "below" "resource" = "" END
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 139 "parameter1" = 37709 "parameter2" = 0 "special" = 0 "timing" = 1 "duration" = 0 STR_VAR "insert" = "below" "resource" = "" END
      BUT_ONLY
    END
  BUT_ONLY
END

/*
luke
**(unused, but fix anyway)**
- Should display the Entangled icon instead of the Held icon
- Should display the "Entangled" string
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "holdfst.spl" "override"
                "talongf.spl" "override"
    LPF "DELETE_EFFECT" INT_VAR "match_opcode" = 142 "match_parameter2" = 13 END
    LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 142 "parameter1" = 0 "parameter2" = 144 "special" = 0 STR_VAR "insert" = "below" "resource" = "" END
    LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 139 "parameter1" = 37709 "parameter2" = 0 "special" = 0 "timing" = 1 "duration" = 0 STR_VAR "insert" = "below" "resource" = "" END
  BUT_ONLY
END

/*
luke
**Club (unused, but fix anyway)**
- Add missing ancillary effects (it currently only displays the entangle overlay, but does not actually cause Entangle!)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "gasp.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "entangle_immunity") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
    INNER_ACTION BEGIN
      COPY_EXISTING "%subspell%.spl" "override"
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 142 "parameter1" = 0 "parameter2" = 144 "special" = 0 STR_VAR "insert" = "last" "resource" = "" END
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 139 "parameter1" = 37709 "parameter2" = 0 "special" = 0 "timing" = 1 "duration" = 0 STR_VAR "insert" = "last" "resource" = "" END
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 126 "parameter1" = 0 "parameter2" = 5 "special" = 0 STR_VAR "insert" = "last" "resource" = "" END
      BUT_ONLY
    END
  BUT_ONLY
END

/*
luke
**(unused, but fix anyway)**
- Add missing ancillary effects (it currently only displays the entangle overlay, but does not actually cause Entangle!)
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "gasp.spl" "override"
    GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "entangle_immunity") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          DEFAULT
        END
      END
    END
    INNER_ACTION BEGIN
      COPY_EXISTING "%subspell%.spl" "override"
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 142 "parameter1" = 0 "parameter2" = 144 "special" = 0 STR_VAR "insert" = "last" "resource" = "" END
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 139 "parameter1" = 37709 "parameter2" = 0 "special" = 0 "timing" = 1 "duration" = 0 STR_VAR "insert" = "last" "resource" = "" END
        LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 154 "opcode" = 126 "parameter1" = 0 "parameter2" = 5 "special" = 0 STR_VAR "insert" = "last" "resource" = "" END
      BUT_ONLY
    END
  BUT_ONLY
END

/*
luke
**Warrior Cry, Fear, Aura of Fear, Spook, Emotion:Fear, Symbol:Fear**
- op324 resource field should not be blank
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "%WARRIOR_WAR_CRY%.spl" "override"
                "%INNATE_BEHOLDER_FEAR%.spl" "override"
                "%INNATE_AURA_OF_FEAR%.spl" "override"
                "%WIZARD_SPOOK%.spl" "override"
                "%WIZARD_EMOTION_FEAR%.spl" "override"
                "spwm123.spl" "override" // Symbol, Fear
    LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 324 "match_parameter2" = 55 STR_VAR "match_resource" = "" "resource" = "%DEST_RES%" END
  BUT_ONLY
END

/*
luke
**Cornugon**
- "eefixpack\files\tph\luke\7eyes\7eyes.tph" follow-up
*/
WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "tosubspell"
  //
  COPY_EXISTING "sflail.itm" "override"
    GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
    PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
      GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
      PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
        PATCH_MATCH SHORT_AT "%fx_off%" WITH
          326 WHEN SLONG_AT (0x4 + "%fx_off%") == IDS_OF_SYMBOL ("splstate" "stun_immunity") AND LONG_AT (0x8 + "%fx_off%") == 111 BEGIN
            READ_ASCII (0x14 + "%fx_off%") "subspell"
          END
          12 139 BEGIN
            READ_ASCII "%fx_off%" $"tosubspell"("%fx_ind%") (0x30)
            WRITE_SHORT "%fx_off%" 999
          END
          DEFAULT
        END
      END
      LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "header" = "%ab_ind%" "match_opcode" = 999 END
      LPF "CLONE_EFFECT" INT_VAR "check_globals" = 0 "header" = "%ab_ind%" "match_opcode" = 326 "opcode" = 146 "parameter1" = 0 "parameter2" = 1 "probability1" = 50 "probability2" = 0 STR_VAR "resource" = "sflail01" END
    END
  BUT_ONLY
  //
  COPY_EXISTING "%subspell%.spl" "override"
    LPF "DELETE_SPELL_EFFECT" INT_VAR "opcode_to_delete" = 8 END
    LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 45 "opcode" = 8 "parameter1" = (46 << 8) + (56 << 16) + (0 << 24) "parameter2" = 255 STR_VAR "insert" = "last" END // Set color glow solid (Location: Character color)
  BUT_ONLY
  //
  COPY_EXISTING "%subspell%.spl" "override\sflail01.spl"
    LPF "DELETE_EFFECT" END
    PHP_EACH "tosubspell" AS "" => "fx_data" BEGIN
      INNER_PATCH "%fx_data%" BEGIN
        READ_SHORT 0x0 "op"
        PATCH_IF ("%op%" == 0xC) BEGIN
          TEXT_SPRINT "insert" "first"
        END ELSE BEGIN
          TEXT_SPRINT "insert" "last"
        END
      END
      PATCH_MATCH "%insert%" WITH
        "first" BEGIN
          SET "offset" = LONG_AT 0x6A
        END
        DEFAULT
          SET "offset" = LONG_AT 0x6A + 0x30 * SHORT_AT (LONG_AT 0x64 + 0x1E)
      END
      INSERT_BYTES "%offset%" 0x30
      WRITE_ASCII "%offset%" "%fx_data%"
      WRITE_BYTE ("%offset%" + 0x12) 100 // prob1
      WRITE_BYTE ("%offset%" + 0x13) 0 // prob2
      WRITE_SHORT (LONG_AT 0x64 + 0x1E) (THIS + 1) // # effects
    END
  BUT_ONLY
END

/*
luke
**Searing Orb** // (unused, but fix anyway)
- "eefixpack\files\tph\luke\7eyes\7eyes.tph" follow-up
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack\files\tph\luke\searing_orb.tph"
  LAF "CLERIC_SOL_SEARING_ORB" END
END

/////                                                  \\\\\
///// projectile fixes                                 \\\\\
/////                                                  \\\\\

// fixed dragon breath projectiles
COPY_EXISTING ~dragblck.pro~ ~override~
              ~draggree.pro~ ~override~
              ~dragred.pro~  ~override~
              ~dragsilv.pro~ ~override~
  WRITE_SHORT 0x210 8

// fixing grease projectile to fire once/round instead of twice/round
COPY_EXISTING ~idpro101.pro~ ~override~
  WRITE_SHORT 0x210 100
  WRITE_BYTE  0x216 10
  
// make protection from evil 10' radius actually, uh, have a 10' radius  
COPY_EXISTING ~idpro258.pro~ ~override~ // adjust to 10' projectile 
  WRITE_SHORT 0x204 171 
  WRITE_SHORT 0x206 171

// tbd, cam
// broken sound reference in lance of disruption projectile
COPY_EXISTING ~idpro313.pro~ ~override~
  WRITE_ASCII 0x18 ~#tra_59~ #8

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

// crieg's bag of holding has several items with charge issues
COPY_EXISTING ~bagh02.sto~ ~override~
  LPF alter_store_item INT_VAR charge2 = 0 charge3 = 0 STR_VAR match_item = wand02 END
  LPF alter_store_item INT_VAR charge1 = 1 stock = 4 STR_VAR match_item = adisease END
  LPF alter_store_item INT_VAR charge1 = 1 stock = 2 STR_VAR match_item = philter END
  LPF alter_store_item INT_VAR charge1 = 1 stock = 4 STR_VAR match_item = flamoil END
  BUT_ONLY

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\

// illusionary creatures shouldn't save when dispelled
INCLUDE ~eefixpack/files/tph/illusionary_die.tph~

// Fix reference to non-existing ACM sound resource
COPY ~music/bm2.mus~ ~music/bm2.mus~
  REPLACE_TEXTUALLY ~^N2\b~ ~N1~ UNLESS ~^N1\b~

// ie-6008, cam
// Worldmap travel fixes, https://www.gibberlings3.net/forums/topic/36416-iwd-worldmap/
COPY_EXISTING ~worldmap.wmp~ ~override~ // inconsistent travel times
  READ_LONG 0x0c mos_off
  READ_LONG (mos_off + 0x20) area_num
  READ_LONG (mos_off + 0x24) area_off
  READ_LONG (mos_off + 0x28) link_off
  FOR (index = 0 ; index < area_num ; ++index) BEGIN
    READ_ASCII (area_off + 0x08 + (index * 0xf0)) area
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar2100" = 0) BEGIN // kuldahar
      SET kuld = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar5000" = 0) BEGIN // severed hand
      SET hand = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar4000" = 0) BEGIN // dragon's eye
      SET deye = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar6000" = 0) BEGIN // dorn's deep
      SET dorn = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar7000" = 0) BEGIN // wyrm's tooth
      SET wyrm = index
    END
  END
  FOR (index = 0 ; index < area_num ; ++index) BEGIN // loop through areas
    FOR (index2 = 0 ; index2 < 4 ; ++index2) BEGIN // loop through four sets of links (n,s,e,w)
      READ_LONG (area_off + 0x50 + (index2 * 0x08) + (index * 0xf0)) link_idx
      READ_LONG (area_off + 0x54 + (index2 * 0x08) + (index * 0xf0)) link_num
      FOR (index3 = 0 ; index3 < link_num ; ++index3) BEGIN // loop through actual links
        READ_LONG (link_off +        ((link_idx + index3) * 0xd8)) target
        PATCH_IF (((index = deye) AND (target = hand)) OR
                  ((index = hand) AND (target = deye))) BEGIN // dragon's eye <> severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 12 // travel time
        END ELSE
        PATCH_IF (((index = wyrm) AND (target = hand)) OR
                  ((index = hand) AND (target = wyrm))) BEGIN // wyrm's tooth <> severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 18 // travel time
        END ELSE
        PATCH_IF (((index = deye) AND (target = dorn)) OR
                  ((index = dorn) AND (target = deye))) BEGIN // dorn's deep <> severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 12 // travel time
        END ELSE
        PATCH_IF (((index = kuld) AND (target = hand)) OR
                  ((index = hand) AND (target = kuld))) BEGIN // kuldahar <> severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 20 // travel time
        END ELSE
        PATCH_IF (((index = wyrm) AND (target = kuld)) OR
                  ((index = kuld) AND (target = wyrm))) BEGIN // wyrm's tooth <> kuldahar
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 24 // travel time
        END ELSE
        PATCH_IF (((index = deye) AND (target = kuld)) OR
                  ((index = kuld) AND (target = deye))) BEGIN // dragon's eye <> kuldahar
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 18 // travel time
        END
      END
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

// prevent spell called via op232 from interrupting caster sounds
INCLUDE ~eefixpack/files/tph/noisy_fireshields.tph~

INCLUDE ~eefixpack/files/tph/6037_iwdee_probabilities.tph~ // ie-6021 & ie-6037, cam: probability fixes
INCLUDE "%MOD_FOLDER%/files/tph/dw_fixes.tph" // tbd, davidw:  318/324 immunity, 109/175/185 disentanglement, and more

// fixes the hot mess of three white doves
// death moved back below death messages; fixing HD targeting for undead destruction
COPY_EXISTING ~doves.itm~   ~override~ // three white doves
  LPF CLONE_EFFECT INT_VAR                   match_opcode = 177 STR_VAR match_resource = cddovdie insert = last END // copy die to last effects after messages
  LPF DELETE_EFFECT INT_VAR multi_match = 18 match_opcode = 177 STR_VAR match_resource = cddovdie               END // yes, 18
  PATCH_FOR_EACH lev IN 5 6 7 8 10 11 BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 match_parameter1 = 4 match_parameter2 = 3 match_dicesize = lev dicesize = 0 END
  END

WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/redundant_effects.tph"
  LAF "remove_redundant_effects" END
END

INCLUDE ~eefixpack/files/lib/cd_effect_batches_functions.tpa~         // function for effect batches
INCLUDE ~eefixpack/files/lib/cd_effect_batches_arrays_bg_bg2_iwd.tpa~ // array definitions for effect batches
INCLUDE ~eefixpack/files/tph/tbd_vfx_removal_iwd.tph~                 // use effect batches to remove vfx from effects which have been removed
