// fixes here will run on bg, bg2, and iwd

INCLUDE ~eefixpack/files/tph/tbd_simulacrum.tph~ // tbd, cam (from jmerry) - simulacrum/projected images should have thief skills, but no shadow twin
INCLUDE ~eefixpack/files/tph/tbd_fireshields.tph~ // tbd, cam (from jmerry) - prevent infinite feedback (and resultant crash) from interacting fireshield-ish effects by giving them a brief immunity to re-application
INCLUDE ~eefixpack/files/tph/invis_expiry_sound.tph~ // expiry sound for invisibility spells

// luke
// **Spirit Fire**
// - should not interact with level-based spell protections
//// - As a result, do not use "bddoom.spl" (since its effects are coded as `power=1`)
// - *"Spirits, fey creatures, elementals, and spectral undead take double damage."* => make sure this double damage bypasses Mirror Image (since the parent `SPL` file is an AoE spell)
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cleric_spirit_fire.tph" // sppr450.spl, sppr450a.spl, sppr450b.spl
  LAUNCH_ACTION_FUNCTION "CLERIC_SPIRIT_FIRE" END
END

// luke
// **Nature's Beauty**
// - permanent blindness (until dispelled)
//   1. Timing Mode 1 will permanently set the creature's STATE_BLIND flag, leaving no removable effect.
//   2. Related: the -4 penalty to AC and THAC0 are apparently tied to the effect (op74), not STATE_BLIND.
// - As a result, we suggest to stick with a limited `timing_mode` and set `duration` to a very high random value (max signed value...?).
// - Additionally, this spell should only affect HUMANOID (there are a lot of unnatural creatures that are immune to it via op206; also, it is intended, right...?)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_NATURE_BEAUTY%.spl~ ~override~ // sppr704
    LPF "ADD_SPELL_EFFECT" INT_VAR "insert_point" = 0 "opcode" = 324 "target" = 2 "power" = 7 "parameter2" = 6 "resist_dispel" = BIT1 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (GENERAL != HUMANOID)
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 74 "timing" = 0 "duration" = 0x7FFFFFFF END // Blindness
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 142 "match_parameter2" = 8 END // op74 naturally provides the "Blind" portrait icon
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Sunray**
// - recode from scratch so as to use `op326` effects instead of messy `op177` effects
// - its insta-death effect against `UNDEAD` is now blocked by `"%CLERIC_DEATH_WARD%"` and the like
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/sunray.tph~ // sppr707.spl, sppr707a.spl, sppr707b.spl, sppr707c.spl
  LAF ~CLERIC_SUNRAY~ END
END

/*
luke
Summon Nishruu / Hakeashar
- their attack (`nishrusu.itm`) should ignore all `AC` modifiers
  - previously, since it was using `damage_type=piercing`, the Nishruu (`THAC0=11`) would need to roll a Critical Hit in order to hit `AC -9 vs. piercing` attacks targets. A similar argument holds for the Hakeashar
    - this is certainly unintended since they don't actually deal piercing damage
- they should be vulnerable to Dispel/Remove Magic
- they should be flagged as `GENERAL=MONSTER` / `RACE=MIST` / `CLASS=MIST` on all games
- make sure the Nishruu has 100% Poison resistance
- The Hakeashar should have the same passive traits as the Nishruu
  - i.e., it should be immune to Poison and see invisible creatures
- the Nishruu should use the `NISHRUU` animation on all games
- the Hakeashar should use the `HAKEASHAR` animation on all games
- removed unused spell setup
*/
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/nishruu_hakeashar.tph~
  LAF ~NISHRUU_HAKEASHAR~ END
END

// resurrection should remove salamander fireballs from polymorph (also for bg, bg2)
COPY_EXISTING ~#rdremov.spl~ ~override~ 
  LPF CLONE_EFFECT INT_VAR match_opcode = 172 STR_VAR match_resource = spin974 resource = spin160 END

// tbd, cam
// enrage is using berserk's spell icon
COPY_EXISTING ~spcl321.spl~ ~override~
  WRITE_ASCII 0x3a ~enrageb~ #8
  LPF ALTER_SPELL_HEADER STR_VAR icon = ~enrageb~ END
  BUT_ONLY

COPY_EXISTING ~spin978.spl~ ~override~ // golem haste [bg2]
  LPF CD_TRIM-O-MATIC INT_VAR level_cap = 1 END // has a bunch of headers that never get used and don't match
  LPF CLONE_EFFECT INT_VAR match_opcode = 16 opcode = 142 parameter2 = 38 END                            // clone into portrait icon
  LPF CLONE_EFFECT INT_VAR match_opcode = 16 opcode = 139 parameter1 = 14023 timing = 1 duration = 0 END // clone into "hasted" string
  
// writhing fog should stack better
COPY_EXISTING ~sppr250.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 215 opcode = 321 parameter2 = 0 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = above END 

// at levels 14-15, shield of the archons absorbs 9 levels of level 3 spells instead of the correct 7
COPY_EXISTING ~sppr701.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 201 match_parameter1 = 9 parameter1 = 7 match_duration = 252 END 
  LPF ALTER_EFFECT INT_VAR match_opcode = 201 match_parameter1 = 9 parameter1 = 7 match_duration = 270 END 
  BUT_ONLY

COPY_EXISTING ~spwi318.spl~ ~override~ // Minor Spell Deflection [sod, bg2]
  LPF ALTER_EFFECT INT_VAR header = 6 match_duration = 180 duration = 198 END // some durations wrong when cast at level 11

COPY_EXISTING ~spwi399.spl~ ~override~ // Minor Spell Deflection [sod, bg2]
  LPF ALTER_EFFECT INT_VAR match_target = 4 target = 2 END // mismatching targets between l1 effects and rest of spell

COPY_EXISTING ~spwi606.spl~ ~override~ // Protection From Magic Energy [bg2]
  LPF ALTER_EFFECT INT_VAR match_opcode = 31 parameter2 = 1 END // should be setting MDR, not incrementing on lev 19+ headers

COPY_EXISTING ~spwi608.spl~ ~override~ // pierce magic [bg2]
  LPF ALTER_EFFECT INT_VAR match_opcode = 230 match_resist_dispel = 0 resist_dispel = 2 END // level 18 effect

COPY_EXISTING ~spwi621.spl~ ~override~ // Conjure Air Elemental [sod, bg2]
  LPF ALTER_EFFECT INT_VAR header = 0 match_duration = 840 duration = 660 END // some durations wrong when cast at min level

// tbd, cam
// don't play berserk damage warnings on non-party memebers; see also spin117[ab].eff
INCLUDE ~eefixpack/files/tph/tbd_angry_noises.tph~