// Hexxat's death-revive sequence uses ActionOverrides to all party members, and also 
// doesn't have combat checks. Her death in a battle will interrupt the current actions 
// of all party members, wasting spells and canceling attacks. She'll now only do her 
// death/revive when the party member with her coffin is otherwise available, out of 
// combat, and only affect that party member specifically.

COPY_EXISTING ~hexxat.bcs~  ~override~
              ~hexxa25.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~HasItemEquiped("ohhexam4",Myself)[ %TAB%%LNL%%MNL%]+Global("OHH_RESTORE_HEXXAT","GLOBAL",1)[ %TAB%%LNL%%MNL%]+InParty(Myself)~  ~False()~ // revival
    REPLACE_TEXTUALLY ~HasItemEquiped("ohhexam4",Myself)[ %TAB%%LNL%%MNL%]+!Global("OHH_RESTORE_HEXXAT","GLOBAL",1)[ %TAB%%LNL%%MNL%]+Range(\[PC\],3)~ ~False()~ // mist death    
    REPLACE_TEXTUALLY ~HasItemEquiped("ohhexam4",Myself)[ %TAB%%LNL%%MNL%]+!InPartyAllowDead(Myself)~ ~False()~                                                  // tob perma death
    REPLACE_TEXTUALLY ~HasItemEquiped("ohhexam4",Myself)[ %TAB%%LNL%%MNL%]+OR(3)~                     ~False() OR(3)~                                            // soa perma death
  END  

COPY_EXISTING ~baldur.bcs~   ~override~
              ~baldur25.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~StateCheck("hexxat",STATE_DEAD)~ ~False()~
  END  

OUTER_FOR (index = 6 ; index > 0; --index) BEGIN 

  OUTER_SPRINT trig1 ~!Global("OHH_mazzyfight","GLOBAL",1) !Global("OHH_valygarfight","GLOBAL",1) !Global("OHH_keldornfight","GLOBAL",1)~
  OUTER_SPRINT trig2 ~OR(3) Global("OHH_mazzyfight","GLOBAL",1) Global("OHH_valygarfight","GLOBAL",1) Global("OHH_keldornfight","GLOBAL",1)~
  EXTEND_TOP ~hexxat.bcs~ ~eefixpack/files/baf/hexxat_death.baf~ EVALUATE_BUFFER

  OUTER_SPRINT trig1 ~~
  OUTER_SPRINT trig2 ~~
  EXTEND_TOP ~hexxa25.bcs~ ~eefixpack/files/baf/hexxat_death.baf~ EVALUATE_BUFFER
  
  EXTEND_BOTTOM ~baldur.bcs~   ~eefixpack/files/baf/hexxat_deathbd.baf~ EVALUATE_BUFFER
  EXTEND_BOTTOM ~baldur25.bcs~ ~eefixpack/files/baf/hexxat_deathbd.baf~ EVALUATE_BUFFER
  
END 