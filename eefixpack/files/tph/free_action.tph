/*
Right, free action. FA contains an effect to use opcode 126 to set movement rate to 100%. The
problem is that this interferes with natural movement speed increases, including those applied
via op 176 like the monk and barbarian bonuses. So we swap it over to use op337 to remove all
applications of 126.

The complication is that many creatures use 126 for inherent movement rate changes, e.g.
free action (before and after this change) allows mimics to move around. (The only reason they
don't is that their AI tells them not to move.) However, many other creatures can be affected by
this, e.g. Balthazar's monk cadre in Amketheran.

So this change from 126 > 337 must be accompanied by swapping many existing 126 effects for 176
effects instead. This only affects BGEE and BG2EE.
*/

DEFINE_PATCH_FUNCTION cd_free_action_checks
  INT_VAR cd_fa_debug = 0 // when cd_fa_debug = 1, it generates a file list to be patched instead of actually patching
          cd_fa_mode  = 0 // mode=0 is checking for free action; mode=1 is checking for any other 126 usage
BEGIN

  SET match = 0
  SET droppable      = 0
  READ_ASCII 0x00 type (3)
  SET min_size = 0
  SET fx_delta = 0
  PATCH_IF ("%type%" STRING_COMPARE_CASE "spl" = 0) BEGIN
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    SET counter_offset = 0x70
    SET abil_length    = 0x28
    SET global_loop    = 0
    SET fx_type        = 0
    SET min_size       = 0x72
  END ELSE
  PATCH_IF ("%type%" STRING_COMPARE_CASE "itm" = 0) BEGIN
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    SET counter_offset = 0x70
    SET abil_length    = 0x38
    SET global_loop    = 1
    SET fx_type        = 0
    SET min_size       = 0x72
    READ_LONG 0x18 flags
    PATCH_IF ((flags & BIT2) = BIT2) BEGIN SET droppable = 1 END
  END ELSE
  PATCH_IF ("%type%" STRING_COMPARE_CASE "cre" = 0) BEGIN
    READ_LONG  0x2c4 fx_off ELSE 0
    READ_BYTE 0x33 fx_type ELSE 2
    SET abil_off       = 0
    SET abil_num       = 0
    SET counter_offset = 0x2c8
    SET abil_length    = 0
    SET global_loop    = 1
    SET min_size       = 0x2d4
    SET cosmetic       = 0
  END
  PATCH_IF ((SOURCE_SIZE >= min_size) AND (min_size != 0)) BEGIN // min_size must get set by file type detection
    FOR (index = (0 - global_loop) ; index < abil_num ; ++index) BEGIN
      PATCH_IF (index < 0) BEGIN // if loop through globals needed
        SET abil_fx_idx = 0
      END ELSE BEGIN // otherwise normal ability
        SET counter_offset = (abil_off + 0x1e + (abil_length * index))
        WRITE_SHORT (abil_off + 0x20 + (abil_length * index)) THIS + fx_delta
        READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
      END
      READ_SHORT counter_offset counter // fx_num on global loop, otherwise abil_fx_num
      FOR (index2 = 0 ; index2 < counter ; ++index2) BEGIN
        READ_SHORT (fx_off + 0x00 + (0x08 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) opcode
        READ_LONG  (fx_off + 0x04 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) p1
        READ_LONG  (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) p2
        PATCH_IF (((cd_fa_mode = 0) AND (opcode = 126) AND  (p1 = 100) AND ((p2 = 2)  OR  (p2 = 5))) OR // free action match
                  ((cd_fa_mode = 1) AND (opcode = 126) AND ((p1 != 100) OR ((p2 != 2) AND (p2 != 5))))) BEGIN // other 126 match
          PATCH_IF cd_fa_debug BEGIN
            SET match = 1
          END ELSE BEGIN
            PATCH_IF cd_fa_mode BEGIN
              WRITE_SHORT (fx_off + 0x00 + (0x08 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 176
            END ELSE BEGIN
              WRITE_SHORT (fx_off + 0x00 + (0x08 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 337
              WRITE_LONG  (fx_off + 0x04 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) "-1"
              WRITE_LONG  (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 126
              // clone new 337 into 337s for web and entangle
              READ_ASCII   (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) clone (0x30 + (0xd8 * fx_type))
              INSERT_BYTES (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (0x30 + (0xd8 * fx_type))
              WRITE_ASCIIE (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) "%clone%"
              WRITE_LONG   (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 154 // entangle
              INSERT_BYTES (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (0x30 + (0xd8 * fx_type))
              WRITE_ASCIIE (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) "%clone%"
              WRITE_LONG   (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 157 // web
              INSERT_BYTES (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (0x30 + (0xd8 * fx_type))
              WRITE_ASCIIE (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) "%clone%"
              WRITE_LONG   (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 16 // haste
              INSERT_BYTES (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (0x30 + (0xd8 * fx_type))
              WRITE_ASCIIE (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) "%clone%"
              WRITE_LONG   (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 40 // slow
              INSERT_BYTES (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (0x30 + (0xd8 * fx_type))
              WRITE_ASCIIE (fx_off + 0x00 + (0x00 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) "%clone%"
              WRITE_LONG   (fx_off + 0x08 + (0x10 * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) 158 // grease
              SET index2   += 5
              SET counter  += 5
              SET fx_delta += 5
            END
          END
        END
      END
      WRITE_SHORT counter_offset counter // fx_num on global loop, otherwise abil_fx_num
    END
  END
  PATCH_IF (("%type%" STRING_COMPARE_CASE "cre" = 0) AND (fx_delta > 0)) BEGIN // fix offsets for cre files if fx inserted
    PATCH_FOR_EACH offset IN 0x2a0 0x2a8 0x2b0 0x2b8 0x2bc BEGIN
      READ_LONG offset curr_off
      PATCH_IF (fx_off < curr_off) BEGIN
        WRITE_LONG offset (THIS + ((0x30 + (0xd8 * fx_type)) * fx_delta))
      END
    END
  END
  PATCH_IF match BEGIN
    READ_LONG 0x0c name
    PATCH_IF (name > 0) AND (name < 999999) BEGIN
      READ_STRREF 0x0c name
    END ELSE BEGIN
      READ_STRREF 0x08 name
    END
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^........$" = 0) BEGIN SPRINT spc " " END ELSE
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^.......$" = 0) BEGIN SPRINT spc "  " END ELSE
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^......$" = 0) BEGIN SPRINT spc "   " END ELSE
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^.....$" = 0) BEGIN SPRINT spc "    " END ELSE
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^....$" = 0) BEGIN SPRINT spc "     " END ELSE
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^...$" = 0) BEGIN SPRINT spc "      " END ELSE
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^..$" = 0) BEGIN SPRINT spc "       " END ELSE
                                                              BEGIN SPRINT spc "        " END
    TO_LOWER SOURCE_FILE
    TO_LOWER name
    SPRINT warningpost ~~
    SPRINT warningpre ~  ~
    PATCH_IF droppable AND abil_length = 0x38 AND cd_fa_mode BEGIN SPRINT warningpost ~ - item is droppable~ SPRINT warningpre ~//~ END
    PATCH_IF abil_length = 0x28 AND cd_fa_mode BEGIN SPRINT warningpost ~ - spell effects~ SPRINT warningpre ~//~ END
    PATCH_PRINT "%warningpre%              ~%SOURCE_FILE%~%spc%~override~ // %name%%warningpost%"
  END

END

/*
// use this to generate file patch list
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  LPF cd_free_action_checks INT_VAR cd_fa_debug = 1 END
  BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  LPF cd_free_action_checks INT_VAR cd_fa_debug = 1 cd_fa_mode = 1 END
  BUT_ONLY
*/

ACTION_IF game_is_bgee BEGIN

  COPY_EXISTING ~bdring09.itm~ ~override~ // ring of free action
                ~beholder.itm~ ~override~ // ring
                ~freering.itm~ ~override~ // edventar's gift
                ~jwsuper.itm~  ~override~ // edventar's gift
                ~potn45.itm~   ~override~ // potion of freedom
                ~ring09.itm~   ~override~ // edventar's gift
                ~sppr403.spl~  ~override~ // free action
                ~sprngb03.itm~ ~override~ // ring
                ~sprngb04.itm~ ~override~ // ring
                ~sprngl03.itm~ ~override~ // ring
                ~sprngl04.itm~ ~override~ // ring
                ~sprngs01.itm~ ~override~ // ring
                ~sprngs02.itm~ ~override~ // ring
                ~sprngs03.itm~ ~override~ // ring
                ~sprngs04.itm~ ~override~ // ring
                ~sprngz05.itm~ ~override~ // ring
                ~sw2h06.itm~   ~override~ // spider's bane +2
    LPF cd_free_action_checks END
    BUT_ONLY IF_EXISTS

  COPY_EXISTING ~bdfistcx.cre~ ~override~ // flaming fist mercenary
                ~bearspir.itm~ ~override~ // attack
                ~bootdriz.itm~ ~override~ // the paws of the cheetah
                ~bpbort.cre~   ~override~ // bort
                ~lionspir.itm~ ~override~ // attack
                ~movrat10.itm~ ~override~ // ring
                ~planwish.cre~ ~override~ // fallen planetar
                ~reghp1.itm~   ~override~ // ring
                ~slaysh01.cre~ ~override~ // slayer shadow
                ~snakspir.itm~ ~override~ // attack
                ~spbatt1.itm~  ~override~ // attack
                ~spbatt2.itm~  ~override~ // attack
                ~spbatt3.itm~  ~override~ // attack
                ~spbatt4.itm~  ~override~ // attack
                ~spbatt5.itm~  ~override~ // attack
                ~splatt1.itm~  ~override~ // attack
                ~splatt2.itm~  ~override~ // attack
                ~splatt3.itm~  ~override~ // attack
                ~splatt4.itm~  ~override~ // attack
                ~splatt5.itm~  ~override~ // attack
                ~spsatt1.itm~  ~override~ // attack
                ~spsatt2.itm~  ~override~ // attack
                ~spsatt3.itm~  ~override~ // attack
                ~spsatt4.itm~  ~override~ // attack
                ~spsatt5.itm~  ~override~ // attack
                ~spwatt1.itm~  ~override~ // attack
                ~spwatt2.itm~  ~override~ // attack
                ~spwatt3.itm~  ~override~ // attack
                ~spwatt4.itm~  ~override~ // attack
                ~spwatt5.itm~  ~override~ // attack
                ~test.cre~     ~override~ // bort
                ~trollboo.itm~ ~override~ // the paws of the cheetah
                ~waterele.itm~ ~override~ // attack
                ~wolfspir.itm~ ~override~ // attack
//              ~bdbow06.spl~  ~override~ // <invalid strref -1> - spell effects
//              ~bdimpale.spl~ ~override~ // impale - spell effects
//              ~bdrunrun.spl~ ~override~ // <invalid strref -1> - spell effects
//              ~bdshamb.spl~  ~override~ // <invalid strref -1> - spell effects
//              ~boot01.itm~   ~override~ // the paws of the cheetah - item is droppable
//              ~hgnya01.itm~  ~override~ // quarterstaff
//              ~mound.itm~    ~override~ // <invalid strref -1>
//              ~spcl123.spl~  ~override~ // <invalid strref -1> - spell effects
//              ~spcl521.spl~  ~override~ // offensive spin - spell effects
//              ~spcl522.spl~  ~override~ // defensive spin - spell effects
//              ~spin688.spl~  ~override~ // plant growth - spell effects
//              ~sppr105.spl~  ~override~ // entangle - spell effects
//              ~spwi101.spl~  ~override~ // grease - spell effects
//              ~spwm111.spl~  ~override~ // entangle - spell effects
//              ~spwm120.spl~  ~override~ // repulsion, caster - spell effects
    LPF cd_free_action_checks INT_VAR cd_fa_mode = 1 END
    BUT_ONLY IF_EXISTS

END ELSE BEGIN // bg2ee

  COPY_EXISTING ~blun30.itm~   ~override~ // flail of ages +5
                ~npplat.itm~   ~override~ // firecam full-plate armor
                ~potn45.itm~   ~override~ // potion of freedom
                ~ring09.itm~   ~override~ // edventar's gift
                ~sper12.itm~   ~override~ // ixil's spike +6
                ~sppr403.spl~  ~override~ // free action
                ~sprngb03.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngb04.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngl03.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngl04.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngs01.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngs02.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngs03.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngs04.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sprngz05.itm~ ~override~ // perhaps you should have been more careful about wearing such a thing to this part of the city. have you tried retracing your steps?
                ~sw2h06.itm~   ~override~ // spider's bane +2
                ~sw2h13.itm~   ~override~ // spider's bane +2
    LPF cd_free_action_checks END
    BUT_ONLY

  COPY_EXISTING ~amcst02.cre~  ~override~ // mercenary
                ~amcst03.cre~  ~override~ // mercenary
                ~ammgrd01.cre~ ~override~ // monk
                ~ammgrd05.cre~ ~override~ // monk
                ~ammlegs.cre~  ~override~ // monk
                ~ammonk01.cre~ ~override~ // monk
                ~ammonk03.cre~ ~override~ // monk
                ~ammonk07.cre~ ~override~ // monk
                ~ammonk08.cre~ ~override~ // monk
                ~balelite.cre~ ~override~ // monk
                ~balth.cre~    ~override~ // balthazar
                ~bazdra02.cre~ ~override~ // draconis
                ~bazdra03.cre~ ~override~ // fll'yissetat
                ~bazmonk.cre~  ~override~ // monk
                ~bearspir.itm~ ~override~ // attack
                ~behgas01.cre~ ~override~ // gas spore
                ~bootdriz.itm~ ~override~ // the paws of the cheetah
                ~chains.itm~   ~override~ // worn-out boots
                ~chalslay.cre~ ~override~ // slayer
                ~cutamgrd.cre~ ~override~ // monk
                ~cutbalth.cre~ ~override~ // balthazar
                ~demlord.cre~  ~override~ // lesser demon lord
                ~demogor2.cre~ ~override~ // demogorgon
                ~dragblue.cre~ ~override~ // abazigal
                ~famdog.cre~   ~override~ // wild dog
                ~famrab25.cre~ ~override~ // rabbit
                ~finmel01.cre~ ~override~ // melissan
                ~finsol04.cre~ ~override~ // fallen solar
                ~fsdragon.cre~ ~override~ // dragon
                ~gorgua01.cre~ ~override~ // aurumach rilmani
                ~gorgua02.cre~ ~override~ // ferrumach rilmani
                ~gormim01.cre~ ~override~ // killer mimic
                ~gormim02.cre~ ~override~ // killer mimic
                ~gormim03.cre~ ~override~ // killer mimic
                ~gormim04.cre~ ~override~ // killer mimic
                ~gorsal.cre~   ~override~ // saladrex
                ~gorsku01.cre~ ~override~ // flaming skull
                ~hgsal01.cre~  ~override~ // fire salamander
                ~hgsku01.cre~  ~override~ // flaming skull
                ~hlkoshi.cre~  ~override~ // koshi
                ~icmarili.cre~ ~override~ // marilith
                ~killmonk.cre~ ~override~ // monk
                ~lionspir.itm~ ~override~ // attack
                ~moondog.cre~  ~override~ // cerebus
                ~move7.itm~    ~override~ // the paws of the cheetah
                ~movrat10.itm~ ~override~ // ring
                ~ohb2phar.cre~ ~override~ // phaere
                ~ohb3bodh.cre~ ~override~ // bodhi
                ~ohb3iren.cre~ ~override~ // jon irenicus
                ~ohb4mlsn.cre~ ~override~ // melissan
                ~ohb5bhal.cre~ ~override~ // bhaal
                ~ohdazot1.cre~ ~override~ // azothet
                ~ohdazoth.cre~ ~override~ // azothet
                ~ohdby02.cre~  ~override~ // mercenary
                ~ohdcdawg.cre~ ~override~ // celestial hound
                ~ohdddolr.cre~ ~override~ // silver dragon
                ~ohddixth.cre~ ~override~ // silver dragon
                ~ohhghst1.cre~ ~override~ // thief
                ~ohhghst2.cre~ ~override~ // thief
                ~ohhghst3.cre~ ~override~ // thief
                ~ohhgm01.cre~  ~override~ // ghostly monk
                ~ohhgm02.cre~  ~override~ // ghostly monk
                ~ohhgm03.cre~  ~override~ // ghostly monk
                ~ohhgmonk.cre~ ~override~ // ghostly monk
                ~ohhkeno.cre~  ~override~ // keno
                ~ohhkichi.cre~ ~override~ // ki chin sang
                ~ohhkwon.cre~  ~override~ // yi kwon
                ~ohhphrey.cre~ ~override~ // phreya
                ~ohhspid1.cre~ ~override~ // bloated spider
                ~planwish.cre~ ~override~ // fallen planetar
                ~polyochr.cre~ ~override~ // mustard jelly
                ~ppireni1.cre~ ~override~ // coordinator
                ~reghp1.itm~   ~override~ // ring
                ~sendai.cre~   ~override~ // sendai
                ~senmonk.cre~  ~override~ // master bennon
                ~slaysh01.cre~ ~override~ // slayer shadow
                ~snakspir.itm~ ~override~ // attack
                ~spbatt1.itm~  ~override~ // attack
                ~spbatt2.itm~  ~override~ // attack
                ~spbatt3.itm~  ~override~ // attack
                ~spbatt4.itm~  ~override~ // attack
                ~spbatt5.itm~  ~override~ // attack
                ~splatt1.itm~  ~override~ // attack
                ~splatt2.itm~  ~override~ // attack
                ~splatt3.itm~  ~override~ // attack
                ~splatt4.itm~  ~override~ // attack
                ~splatt5.itm~  ~override~ // attack
                ~spsatt1.itm~  ~override~ // attack
                ~spsatt2.itm~  ~override~ // attack
                ~spsatt3.itm~  ~override~ // attack
                ~spsatt4.itm~  ~override~ // attack
                ~spsatt5.itm~  ~override~ // attack
                ~spwatt1.itm~  ~override~ // attack
                ~spwatt2.itm~  ~override~ // attack
                ~spwatt3.itm~  ~override~ // attack
                ~spwatt4.itm~  ~override~ // attack
                ~spwatt5.itm~  ~override~ // attack
                ~tobpar03.cre~ ~override~ // long po
                ~torgal.cre~   ~override~ // torgal
                ~torgal2.cre~  ~override~ // torgal
                ~torgal3.cre~  ~override~ // torgal
                ~trolic03.cre~ ~override~ // blizzard troll
                ~trollboo.itm~ ~override~ // the paws of the cheetah
                ~uddemon.cre~  ~override~ // lesser demon lord
                ~udspid01.cre~ ~override~ // giant spider
                ~vorsqui1.itm~ ~override~ // <invalid strref -1>
                ~wolfspir.itm~ ~override~ // attack
                ~ysgp01.cre~   ~override~ // toop the brave
                ~ysgp04.cre~   ~override~ // chinchilla
//              ~amul26.itm~   ~override~ // amulet of cheetah speed - item is droppable
//              ~boot01.itm~   ~override~ // the paws of the cheetah - item is droppable
//              ~boot10.itm~   ~override~ // boots of lightning speed - item is droppable
//              ~bow22.itm~    ~override~ // taralash +4 - item is droppable
//              ~bow23.itm~    ~override~ // taralash +5 - item is droppable
//              ~cdspeed.itm~  ~override~ // oil of speed - item is droppable
//              ~hgnya01.itm~  ~override~ // quarterstaff - reduces target movement on hit
//              ~leat24.itm~   ~override~ // grandmaster's armor +6 - item is droppable
//              ~mound.itm~    ~override~ // <invalid strref -1> - reduces target movement on hit
//              ~npsw01.itm~   ~override~ // sword of arvoreen +2 - item is droppable
//              ~ohnrobe3.itm~ ~override~ // tunic of blindeye - item is droppable
//              ~plat17.itm~   ~override~ // t'rachie's plate +5 - item is droppable
//              ~runrun.spl~   ~override~ // <invalid strref -1>
//              ~spcl123.spl~  ~override~ // <invalid strref -1>
//              ~spcl521.spl~  ~override~ // offensive spin
//              ~spcl914.spl~  ~override~ // greater evasion
//              ~spin688.spl~  ~override~ // plant growth
//              ~sppr105.spl~  ~override~ // entangle
//              ~spwi101.spl~  ~override~ // grease
//              ~spwm111.spl~  ~override~ // entangle
//              ~spwm120.spl~  ~override~ // repulsion, caster
//              ~sw1h58.itm~   ~override~ // short sword of mask +4 - item is droppable
//              ~sw1h59.itm~   ~override~ // short sword of mask +5 - item is droppable
    LPF cd_free_action_checks INT_VAR cd_fa_mode = 1 END
    BUT_ONLY

END
