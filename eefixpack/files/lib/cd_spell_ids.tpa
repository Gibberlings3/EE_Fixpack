// Let's set a variable for each valid entry in SPELL.IDS
// You can now write ADD_MEMORIZED_SPELL ~%CLERIC_BLESS%~ instead of ~SPPR101~

DEFINE_ACTION_MACRO "SPELL_IDS" BEGIN
  LOCAL_SET "cols" = 0
  LOCAL_SET "read_spell" = 0
  LOCAL_SET "i" = 0
  LOCAL_SPRINT "identifier" ""
  LOCAL_SPRINT "spell_res" ""
  // The following variables are all set by `COPY_EXISTING`
  LOCAL_SPRINT "SOURCE_DIRECTORY" ""
  LOCAL_SPRINT "SOURCE_FILESPEC" ""
  LOCAL_SPRINT "SOURCE_FILE" ""
  LOCAL_SPRINT "SOURCE_RES" ""
  LOCAL_SPRINT "SOURCE_EXT" ""
  LOCAL_SET "SOURCE_SIZE" = 0
  LOCAL_SPRINT "DEST_DIRECTORY" ""
  LOCAL_SPRINT "DEST_FILESPEC" ""
  LOCAL_SPRINT "DEST_FILE" ""
  LOCAL_SPRINT "DEST_RES" ""
  LOCAL_SPRINT "DEST_EXT" ""
  // Main
  COPY_EXISTING - "SPELL.IDS" "override"
    COUNT_2DA_COLS "cols"
    READ_2DA_ENTRIES_NOW "read_spell" "%cols%"
    FOR ("i" = 0; "%i%" < "%read_spell%"; "i" += 1) BEGIN
      READ_2DA_ENTRY_FORMER "read_spell" "%i%" 1 "identifier"
      PATCH_IF ("%identifier%" STRING_COMPARE_CASE "V1.0") BEGIN // skip 1st row ~IDS V1.0~ if needed (so as to avoid checking "spell_num" = "-1")
        LPF "RES_NAME_OF_SPELL_NUM"
        INT_VAR
          "spell_num" = IDS_OF_SYMBOL (~SPELL~ ~%identifier%~)
        RET
          "spell_res"
        END
        // BG(2):EE => we'd like to discard false positive such as `2610 WIZARD_MONSTER_SUMMONING_4`, where the corresponding file ~SPWI610.SPL~ does not exist
        // That is, make sure that ~%spell_res%.spl~ does exist as a game resource
        PATCH_IF (FILE_EXISTS_IN_GAME ~%spell_res%.spl~) BEGIN
          TEXT_SPRINT "%identifier%" "%spell_res%"
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

LAUNCH_ACTION_MACRO "SPELL_IDS"

/*COPY_EXISTING ~spell.ids~ ~override~
  REPLACE_EVALUATE ~^\([0-9]+\)\([ %TAB%]+\)\([^ %TAB%%LNL%%MNL%%WNL%]+\)~ BEGIN
    SET number = 0
    PATCH_IF MATCH1 >= 4000 BEGIN SPRINT prefix spcl SET number = MATCH1 - 4000 END ELSE
    PATCH_IF MATCH1 >= 3000 BEGIN SPRINT prefix spin SET number = MATCH1 - 3000 END ELSE
    PATCH_IF MATCH1 >= 2000 BEGIN SPRINT prefix spwi SET number = MATCH1 - 2000 END ELSE
    PATCH_IF MATCH1 >= 1000 BEGIN SPRINT prefix sppr SET number = MATCH1 - 1000 END
    SPRINT ~%MATCH3%~ ~%prefix%%number%~
  END ~%MATCH1%%MATCH2%%MATCH3%~
  BUT_ONLY*/
